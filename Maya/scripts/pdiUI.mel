/****************************************
	PDI CREATE BODIES PANEL
******************************************/

global proc setPdiDynamicType()
{
    global string $ChkPdiFirstHit;
    global string $comboDynamicType;   
    global int $pdiUpdate;
         
	int $value = `optionMenu -query -select $comboDynamicType`;

	$pdiUpdate = 1;//enable update			
    
	if( $value == 3)
	{
		//kinematic
//		checkBox -edit -value 1 $ChkPdiPassive;				
//		setPptyPassive();
		createPDIMeshBody();		
	}    
	else
	if( $value == 2)
	{
		//static
//		checkBox -edit -value 1 $ChkPdiPassive;				
//		setPptyPassive();
		createPDIAutoBody();		
	}
	else
	{
		//dynamic
//		checkBox -edit -value 0 $ChkPdiPassive;				
//		setPptyPassive();
		//disable first hit
		checkBox -edit -value 0 $ChkPdiFirstHit;
		setPptyFirstHitActive();	
		createPDIAutoBody();		
	}	
}
global proc pdiUI_createRigidBody(int $dynamic_type, int $boundingVolumeType)
{
    string $selection[] = `ls -selection -dag -leaf -visible -type "geometry"`;

    if( size($selection) > 0)
    {    
	    //create pdiSolver node if necessary
		pdiSolver;
    
        waitCursor -state on;

//		print( " execute new_PDIBody\n");

        new_PDIBody -a ($dynamic_type-1) -st $boundingVolumeType $selection;

        waitCursor -state off;

		updatePdiPropertiesPanel();
    }
    else
	{
	    warning "select a a polygonal object first";			
	}	 
}
global proc createPDICapsuleBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;        
    global int $pdiUpdate;

	if( $pdiUpdate==0)
	{
		return;	
	}	
	
//    $state = `checkBox -query -value $ChkPdiPassive`;
	int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
    if( $dynamic_type==4)$dynamic_type=1;
    
	pdiUI_createRigidBody( $dynamic_type, 1);

	updatePdiBodyPanel();

}
global proc createPDIVoidBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;            
    global int $pdiUpdate;

	if( $pdiUpdate==0)
	{
		return;	
	}	
	
//    $state = `checkBox -query -value $ChkPdiPassive`;   
    int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
    if( $dynamic_type==4)$dynamic_type=1;
    
    pdiUI_createRigidBody( $dynamic_type, 0);

	updatePdiBodyPanel();

}

global proc createPDIHullBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;            
    global int $pdiUpdate;

	if( $pdiUpdate==0)
	{
		return;	
	}	
	
//    $state = `checkBox -query -value $ChkPdiPassive`;
    int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
    if( $dynamic_type==4)$dynamic_type=1;
    
	pdiUI_createRigidBody( $dynamic_type, 2);

	updatePdiBodyPanel();

}
global proc createPDIMeshBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;            
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

//    $state = `checkBox -query -value $ChkPdiPassive`;
    int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
    if( $dynamic_type==4)$dynamic_type=1;
    
	pdiUI_createRigidBody( $dynamic_type, 3);

	updatePdiBodyPanel();

}
global proc createPDIAutoBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;            
    global int $pdiUpdate;

	if( $pdiUpdate==0)
	{
//		print( "$pdiUpdate==0\n");
		return;	
	}	
	
//    $state = `checkBox -query -value $ChkPdiPassive`;
    int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
    
    if( $dynamic_type==4)$dynamic_type=1;
    
	pdiUI_createRigidBody( $dynamic_type, 4);
	updatePdiBodyPanel();
}

global proc setPptyAnimated()
{
    global string $chkPdiAnimated;
    global string $ChkPdiPassive;
    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}
		
    $state = `checkBox -query -value $chkPdiAnimated`;
	
	if( $state == true)
	{
		//animated objects are always passive
//		print( "animated on\n");
//		checkBox -edit -value 1 $ChkPdiPassive;	
//		setPptyPassive();				
	}
			
	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
	for ( $obj in $PDIObjects )
	{
		string  $PDIShapes[];
		$PDIShapes = `listConnections -type pdiCollisionShape $obj`;
		if( size($PDIShapes) > 0)
		{		
			setAttr ($PDIShapes[0] + ".animated(Mesh)" ) $state;

            //force update 
			getAttr ($PDIShapes[0] + ".ca_collisionShape" );            
		}			
	}

}
global proc setPptyFlipSide()
{
    global string $chkPdiFlipNormals;
    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}
		
    $state = `checkBox -query -value $chkPdiFlipNormals`;
	
	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
	for ( $obj in $PDIObjects )
	{
		string  $PDIShapes[];
		$PDIShapes = `listConnections -type pdiCollisionShape $obj`;
		if( size($PDIShapes) > 0)
		{		
           
            setAttr ($PDIShapes[0] + ".flipCollisionSide(Mesh)" ) $state;
            //force update 
			getAttr ($PDIShapes[0] + ".ca_collisionShape" );            

		}			
	}
}
global proc setPptyFirstHitActive()
{
    global string $ChkPdiFirstHit;
    global string $ChkPdiPassive;
    global string $comboDynamicType;

    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    int $bChkExist  = `checkBox -ex $ChkPdiFirstHit`;
    if( $bChkExist == 0)
    {
        return;
    }

    $state = `checkBox -query -value $ChkPdiFirstHit`;

	if( $state == true)
	{
		//first hit objects  are always passive
        int $bChkExist2  = `checkBox -ex $ChkPdiPassive`;
        if( $bChkExist2 > 0)
        {
		    checkBox -edit -value 1 $ChkPdiPassive;	
		    setPptyPassive();				
        }
		int $dynType = `optionMenu -query -select $comboDynamicType`;
        if( $dynType==1)
		{ 
		optionMenu -edit -select 2 $comboDynamicType;  
		setPdiDynamicType();      
		}			
		checkBox -edit -value 1 $ChkPdiFirstHit;		
	}

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
	for ( $obj in $PDIObjects )
	{
		if( $state==true)
		{		
			setAttr ($obj + ".firstHitActive" ) true;
		}
		else
		{
			setAttr ($obj + ".firstHitActive" ) false;
		}		

        setAttr ($obj + ".usercommand" ) true;

        //force update
        getAttr ($obj + ".ca_activeState" );
        
	}

//update prop windows
//    updatePdiPropertiesPanel();

}
global proc setPptyPassive()
{
//	print( "setting passive\n");

    global string $ChkPdiPassive;
    global string $ChkPdiFirstHit;
    
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	
	
    int $state=0;
    int $bChkExist  = `checkBox -ex $ChkPdiPassive`;
    if( $bChkExist > 0)
    {
        $state = `checkBox -query -value $ChkPdiPassive`;
    }

	if( $state == false)
	{
		//first hit objects  are always passive
        int $bChkExist3  = `checkBox -ex $ChkPdiFirstHit`;
        if( $bChkExist3 > 0)
        {
		    checkBox -edit -value 0 $ChkPdiFirstHit;	
		    setPptyFirstHitActive();				
        }
	}

    
	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
	for ( $obj in $PDIObjects )
	{
		if( $state==true)
		{		
			setAttr ($obj + ".passive" ) 1;
		}
		else
		{
			setAttr ($obj + ".passive" ) 0;
		}		

        setAttr ($obj + ".usercommand" ) 1;

        //force update
        getAttr ($obj + ".ca_activeState" );
        
	}
	
	if( $state==true)
	{		
		if(size($PDIObjects) == 0)
		{
		// create default rigid body
			createPDIAutoBody();
		}
	}

//update prop windows
    updatePdiPropertiesPanel();
}
global proc updateTransformBody()
{
    global string $ChkPdiPassive;
    global string $comboDynamicType;        

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size($PDIObjects) > 0)
    {    
	    if( size($PDIObjects) > 1)	    
		{
			string $result = `confirmDialog -title "Confirm" -message "There are multiple objects selected, proceed?"
			-button "Yes" -button "No" -defaultButton "Yes"
			-cancelButton "No" -dismissString "No"`;

			if( $result == "No")
			{
				return;
			}			    		
		}
		
//			$state = `checkBox -query -value $ChkPdiPassive`;
		int $dynamic_type = `optionMenu -query -select $comboDynamicType`;
		
	    for ( $obj in $PDIObjects )
		{			
			string  $PDIShapes[];
			$PDIShapes = `listConnections -type pdiCollisionShape $obj`;
			if( size($PDIShapes) > 0)
			{	
				int $BVolume = getAttr ($PDIShapes[0] + ".type" );
				string $selection[] = `ls -selection -dag -leaf -visible -type "geometry"`;

				new_PDIBody -a ($dynamic_type-1) -st $BVolume $selection;		
	        
//					print ($selection[0] + " has updated its transform");		
			}
		}			
		updatePdiPropertiesPanel();
	}	

}
global proc updatePdiBodyPanel()
{
    global string $pdiCreateBodyWindow;
    global int $pdiUpdate;
    global string $BVcollection;
   	global string $rb[6];
    global string $chkPdiFlipNormals;
    global string $chkPdiAnimated;
    global string $ChkPdiFirstHit;
    global string $comboDynamicType;        
    
    int $bWExist = `window -ex winCreatePdiBodies`;
    //int $bIconify = `window -query -iconify $pdiCreateBodyWindow`;
    if( $bWExist == false)
    {
//		print("pidBodyPanel iconified, abort\n");			
        return;
    }

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

	if(size($PDIObjects) != 0)
    {
		$pdiUpdate = 0;//disable update					

		int $dynamic_type = getAttr ($PDIObjects[0] + ".passive" );
//print( "dynamic type = \n");
//print $dynamic_type;
		

	    if( $dynamic_type == 1)
	    {
			optionMenu -edit -select 2 $comboDynamicType;		    
	    }
	    else
	    if( $dynamic_type == 2)
	    {	
			optionMenu -edit -select 3 $comboDynamicType;
					    
	    }
	    else
	    {	
			optionMenu -edit -select 1 $comboDynamicType;		    
	    }

		int $frstHitActive = getAttr ($PDIObjects[0] + ".firstHitActive" );
	    if( $frstHitActive > 0)
	    {
		    checkBox -edit -value 1 $ChkPdiFirstHit;		
	    }
	    else
	    {	
		    checkBox -edit -value 0 $ChkPdiFirstHit;		
	    }


		string  $PDIShapes[];
		$PDIShapes = `listConnections -type pdiCollisionShape $PDIObjects[0]`;
		if( size($PDIShapes) > 0)
		{	
			int $BVolume = getAttr ($PDIShapes[0] + ".type" );
			if( `radioCollection -ex $BVcollection`	&& $BVolume >= 0 && $BVolume < 5)
			{
				radioCollection -edit -select $rb[$BVolume] $BVcollection;	
				if( $BVolume == 3)
				{
					int $flipSide = getAttr ($PDIShapes[0] + ".flipCollisionSide(Mesh)" );
					int $animatedMesh = getAttr ($PDIShapes[0] + ".animated(Mesh)" );
				
					checkBox -edit -enable 1 -value $flipSide $chkPdiFlipNormals;						
					checkBox -edit -enable 1 -value $animatedMesh $chkPdiAnimated;						
				}
				else					
				{
					checkBox -edit -enable 0 $chkPdiFlipNormals;						
					checkBox -edit -enable 0 $chkPdiAnimated;						
				}
			}
		}			
		$pdiUpdate = 1;//enable update			
	}
	else
	{
		optionMenu -edit -select 4 $comboDynamicType;		    
		radioCollection -edit -select $rb[5] $BVcollection;		
	}		

    showWindow $pdiCreateBodyWindow;

	setFocus MayaWindow;
}

global proc pdiChangeContext()
{
    setToolTo `selectContext`; 

//    print ("Window gets deleted!\n");
}
global proc pdiSetPdiContext()
{
    setToolTo `selectPdiContext`;  

//    print ("Window gets deleted!\n");
}

global proc killUpdatePdiShatterPanelJobId()
{
    global int $updatePdiShatterPanelJobId;

    scriptJob -kill $updatePdiShatterPanelJobId;

	// remove shatter locator if exist
	string $shatterLocator[] = `ls  -dag -type "ShatterLocator"`;
	if( size($shatterLocator) > 0)
	{
		delete $shatterLocator[0];
	}		
}

global proc killUpdatePdiFragmentsJobId()
{
    global int $UpdatePdiFragmentsJobId;
    global string $pdiCurrentSelectedFragments[];

    //remove select color for last selected
    for ( $obj in $pdiCurrentSelectedFragments )
    {
	    setAttr ($obj + ".selected" ) 0;
        //force update 
        getAttr ($obj + ".ca_selected" );            
    }
    clear($pdiCurrentSelectedFragments);

    scriptJob -kill $UpdatePdiFragmentsJobId;
}

global proc killUpdateManagePanelJob()
{
    global int $UpdatePdiManagePanelJobId;

    scriptJob -kill $UpdatePdiManagePanelJobId;
}

global proc killUpdatePropertiesPanelJob()
{
    global int $UpdatePdiPropertiesPanelJobId;

    scriptJob -kill $UpdatePdiPropertiesPanelJobId;
}

global proc killUpdateBodyPanelJob()
{
    global int $UpdatePdiBodyPanelJobId;

    scriptJob -kill $UpdatePdiBodyPanelJobId;
}

global proc createPdiBodyPanel()
{
    global string $pdiCreateBodyWindow;
    global int $pdiUpdate;
    global string $BVcollection;
   	global string $rb[6];
    global string $chkPdiFlipNormals;
    global string $chkPdiAnimated;
    global string $ChkPdiPassive;
    global string $ChkPdiFirstHit;
    global string $comboDynamicType;        
    global int $UpdatePdiBodyPanelJobId;
                 
//    setToolTo `selectPdiContext`; 
//    headsUpMessage "Pdi Interactive selection Tool has been activated";

    //check if the window exists
    if ( `window -ex winCreatePdiBodies` )
    {                
        updatePdiBodyPanel();
        
        return;
    }

    $pdiUpdate = 1;

	$pdiCreateBodyWindow = `window -rtf true -t "Create Rigid Body" -width 50 -height 300 winCreatePdiBodies`;
	columnLayout -adjustableColumn true;   
	columnLayout -columnAttach "left" 25;
    $comboDynamicType = `optionMenu -label "Type     " 
    -ann " set dynamic behaviour of the object" -cc "setPdiDynamicType"`;
        menuItem -label "Dynamic";
        menuItem -label "Static";
        menuItem -label "Kinematic";
        menuItem -label " ";        	 
    setParent ..;                   
	frameLayout -width 30 -height 170 -label "Bounding Volume" -borderStyle "etchedOut";
		columnLayout -columnAttach "left" 50;
	    $BVcollection = `radioCollection`;
			$rb[0] = `radioButton -label "None" -ann "object will move but not collide" -onCommand "createPDIVoidBody"`;
			$rb[1] = `radioButton -label "Capsule" -ann "object will roll on collision" -onCommand "createPDICapsuleBody"`;
			$rb[2] = `radioButton -label "Convex Hull" -ann "recommended for faster computation" -onCommand "createPDIHullBody"`;
			$rb[3] = `radioButton -label "Mesh" -ann "use it in objects with holes or mesh-animated" -onCommand "createPDIMeshBody"`;
			columnLayout -columnAttach "left" 25;
				$chkPdiFlipNormals = `checkBox -label "Flip collision side" -align "left" -enable 0 -changeCommand "setPptyFlipSide"`;
				$chkPdiAnimated = `checkBox -label "Animated" -align "left" -enable 0 -changeCommand "setPptyAnimated"`;
			setParent ..;
			
			$rb[4] = `radioButton -label "Auto" -ann "set the best BVolume automatically" -onCommand "createPDIAutoBody" `;
			//create a last one only for select it for unassigned shapes
			$rb[5] = `radioButton -label "Unassigned" -ed 0 -vis 0`;
		setParent ..;
    setParent ..;

//    $ChkPdiPassive = `checkBox -label "passive" -align "left"
//     -ann "passive objects will collide with others but not move" -changeCommand "setPptyPassive"`;
    $ChkPdiFirstHit = `checkBox -label "activate at first hit" -align "left" 
    -ann "object will become dynamic at first hit with others" -changeCommand "setPptyFirstHitActive"`;
    $bttPdiUpdateBody = `button  -w 50 -h 30 -label "Update Transform"
    -ann "update the Bounding volume of the object" -command "updateTransformBody"`;
    
    $UpdatePdiBodyPanelJobId = `scriptJob -event SelectionChanged updatePdiBodyPanel`;
    scriptJob -uiDeleted $pdiCreateBodyWindow killUpdateBodyPanelJob;
	
	//update the window with selected objects attributes
	updatePdiBodyPanel();
}
/****************************************
	PDI SOLVER PANEL
******************************************/
global proc setPptyGravityEnabled()
{
    global string $chkPdiGravityEnabled;
    
//    print("setPptyGravityEnabled()\n");			

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
//        print("updating off\n");			
		return;	
	}	

    $state = `checkBox -query -value $chkPdiGravityEnabled`;
    
    string $PDISolver = `pdiSolver`;

	if( $state==true)
	{		
		setAttr ($PDISolver + ".gravityEnabled" ) false;
	}
	else
	{
		setAttr ($PDISolver + ".gravityEnabled" ) true;
	}		

     //force update
     getAttr ($PDISolver + ".ca_solverParams" );
}
global proc setPptyMayaUnits()
{
    global string $chkPdiFitUnitsMaya;
    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    confirmDialog
    -title "Pdi Info"
    -message "By changing this parameter, you´ll have probably to reassign dynamics properties for already created PDI objects"
    -button "OK" 
    -defaultButton "OK" ;

    $state = `checkBox -query -value $chkPdiFitUnitsMaya`;
    
    string $PDISolver = `pdiSolver`;

	if( $state==true)
	{		
		setAttr ($PDISolver + ".fitUnitsToMayaScale" ) true;
	}
	else
	{
		setAttr ($PDISolver + ".fitUnitsToMayaScale" ) false;
	}		
     //force update
     getAttr ($PDISolver + ".ca_solverParams" );
}
global proc updatePdiStartFrame()
{
    global string $fieldPdiStartFrame;

    int $value = `intField -q -value $fieldPdiStartFrame`;	

    int $minValue = `playbackOptions -q -min`;
    if( $value >= $minValue)
    {
        string $PDISolver = `pdiSolver`;

        setAttr( $PDISolver + ".startFrame" ) $value;

     //force update
//     getAttr ($PDISolver + ".ca_numThreads" );
    }
    else
    {
       warning "Value out of range";
    }

}
global proc updatePdiNumThreads()
{

}
global proc setPdiCachedMode()
{
    global string $chkPdiCachedMode;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    $state = `checkBox -query -value $chkPdiCachedMode`;
    
    string $PDISolver = `pdiSolver`;

	if( $state==true)
	{		
		setAttr ($PDISolver + ".cacheActive" ) true;
	}
	else
	{
		setAttr ($PDISolver + ".cacheActive" ) false;
	}		
     //force update
     getAttr ($PDISolver + ".ca_cacheSize" );

}

global proc setPdiDisplayMode()
{
    global string $pdiDisplayStylecollection;
   	global string $pdiDisplayStyle[2];
    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	
    string $PDISolver = `pdiSolver`;

    if( `radioButton -query -select $pdiDisplayStyle[1]`)
    {
		setAttr ($PDISolver + ".displaySelected" ) true;
    }
    else
    {
		setAttr ($PDISolver + ".displaySelected" ) false;
    }
    
     //force update
     getAttr ($PDISolver + ".ca_displaySelected" );
     
     evalDeferred -lp "refresh";//defered it until evaluation ends 
}
global proc updatePdiSimQuality()
{
    global string $pdiCreateSolverWindow;
    global string $fieldPdiSubstep;    
    global string $comboSimQaulity;    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}

    string $PDISolver = `pdiSolver`;	

	int $quality = `optionMenu -query -select $comboSimQaulity`;

	if( $quality == 1)
	{
		setAttr ($PDISolver + ".substeps" ) 3;	
	}				
	else
	if( $quality == 2)
	{
		setAttr ($PDISolver + ".substeps" ) 5;	
	}				
	if( $quality == 3)
	{
		setAttr ($PDISolver + ".substeps" ) 10;	
	}				
	if( $quality == 4)
	{
		setAttr ($PDISolver + ".substeps" ) 20;	
	}				
}
global proc updatePdiSubStep()
{
    global string $pdiCreateSolverWindow;
    global string $fieldPdiSubstep;    
    global string $comboSimQaulity;    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $PDISolver = `pdiSolver`;

//	int $substep = `attrFieldSliderGrp -query -s $fieldPdiSubstep`;
	int $substep = getAttr ($PDISolver + ".substeps" );
	if( $substep < 5)
	{
		optionMenu -edit -select 1 $comboSimQaulity;
	}
	else
	if( $substep < 10)
	{
		optionMenu -edit -select 2 $comboSimQaulity;
	}
	else
	if( $substep < 20)
	{
		optionMenu -edit -select 3 $comboSimQaulity;
	}
	else
	{
		optionMenu -edit -select 4 $comboSimQaulity;
	}

   showWindow $pdiCreateSolverWindow;
}
global proc setPdiGridAsGround()
{
    global string $chkPdiGridAsGround;
    
    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    $state = `checkBox -query -value $chkPdiGridAsGround`;
    
    string $PDISolver = `pdiSolver`;

	if( $state==true)
	{
		int $state=`grid -q -toggle`;
		if( $state==false)
		{
			grid -toggle true;
		}				
//		displayColor -dormant "grid" 15;	
		setAttr ($PDISolver + ".useGroundGrid" ) true;
	}
	else
	{	   
//		displayColor -dormant "grid" 3;	
		setAttr ($PDISolver + ".useGroundGrid" ) false;
	}		
     //force update
     getAttr ($PDISolver + ".ca_solverParams" );
}
global proc updatePdiSolverPanel()
{
    global string $pdiCreateSolverWindow;
    global string $chkPdiFitUnitsMaya;
    global string $chkPdiGravityEnabled;
    global string $chkPdiGridAsGround;    
//    global string $fieldPdiNumThreads;
    global string $chkPdiNumThreads;
    global string $chkPdiCachedMode;
    global string $fieldPdiSubstep;    
    global string $fieldPdiStartFrame;
    global string $pdiDisplayStylecollection;
   	global string $pdiDisplayStyle[2];
    global string $comboSimQaulity;    

    global int $pdiUpdate;

    string $PDISolver = `pdiSolver`;

    $pdiUpdate = 0;//disable update					

    int $displayMode = getAttr ($PDISolver + ".displaySelected" );
	if( $displayMode > 0)
	{	
		radioCollection -edit -select $pdiDisplayStyle[1] $pdiDisplayStylecollection;
	}
	else
	{	
		radioCollection -edit -select $pdiDisplayStyle[0] $pdiDisplayStylecollection;
	}

//   int $useMayaUnits = getAttr ($PDISolver + ".fitUnitsToMayaScale" );
//	 if( $useMayaUnits > 0)
//	 {	
// 		checkBox -edit -value 1 $chkPdiFitUnitsMaya;		
//   }
//   else
//	 {	
//	 	checkBox -edit -value 0 $chkPdiFitUnitsMaya;		
//	 }

     int $gridAsGround = getAttr ($PDISolver + ".useGroundGrid" );
	 if( $gridAsGround > 0)
	 {	
 		checkBox -edit -value 1 $chkPdiGridAsGround;		
     }
     else
	 {	
	 	checkBox -edit -value 0 $chkPdiGridAsGround;		
	 }

    int $startFrame = getAttr ($PDISolver + ".startFrame" );

    intField -edit -value $startFrame $fieldPdiStartFrame;

//    int $numThreads = getAttr ($PDISolver + ".numThreads" );
//    intField -edit -value $numThreads $fieldPdiNumThreads;

    int $multithreadingEnabled = getAttr ($PDISolver + ".numThreads" );
	if( $multithreadingEnabled > 1)
	{	
		checkBox -edit -value 1 $chkPdiNumThreads;		
	}
	else
	{	
		checkBox -edit -value 0 $chkPdiNumThreads;		
	}

    int $gravityEnabled = getAttr ($PDISolver + ".gravityEnabled" );
	if( $gravityEnabled > 0)
	{	
		checkBox -edit -value 0 $chkPdiGravityEnabled;		
	}
	else
	{	
		checkBox -edit -value 1 $chkPdiGravityEnabled;		
	}

    int $useCachedMode = getAttr ($PDISolver + ".cacheActive" );
	if( $useCachedMode > 0)
	{	
		checkBox -edit -value 1 $chkPdiCachedMode;		
	}
	else
	{	
		checkBox -edit -value 0 $chkPdiCachedMode;		
	}
	int $substep = getAttr ($PDISolver + ".substeps" );
	if( $substep < 5)
	{
		optionMenu -edit -select 1 $comboSimQaulity;
	}
	else
	if( $substep < 10)
	{
		optionMenu -edit -select 2 $comboSimQaulity;
	}
	else
	if( $substep < 20)
	{
		optionMenu -edit -select 3 $comboSimQaulity;
	}
	else
	{
		optionMenu -edit -select 4 $comboSimQaulity;
	}
	

    $pdiUpdate = 1;//enable update			

    showWindow $pdiCreateSolverWindow;

}

global proc createPdiSolverPanel()
{
    global string $pdiCreateSolverWindow;
//    global string $chkPdiFitUnitsMaya;
    global string $chkPdiGridAsGround;
    global string $chkPdiGravityEnabled;
//    global string $fieldPdiNumThreads;
    global string $chkPdiNumThreads;
    global string $fieldPdiSubstep;
    global string $chkPdiCachedMode;
    global string $fieldPdiStartFrame;
    global int $pdiUpdate;
    global string $pdiDisplayStylecollection;
   	global string $pdiDisplayStyle[2];
   	global string $comboSimQaulity;    
    
    //check if the window exists
    if ( `window -ex winPdiSolver` )
    {                
        updatePdiSolverPanel();
        
        return;
    }
 
    $pdiUpdate = 1;//enable update			

    //create pdiSolver node if necessary
    string $PDISolver = `pdiSolver`;

//    string  $PDISolver[];
//    $PDISolver = `ls -dag -leaf -type pdiSolver `;
    
	$pdiCreateSolverWindow = `window -rtf true -t "Pdi Solver Options" -width 100 -height 80 winPdiSolver`;
	columnLayout -adjustableColumn true; 
    frameLayout -width 100 -height 50 -label "Display" -borderStyle "etchedOut";
	    rowLayout -numberOfColumns 2 -cw2 100 200;
	    $pdiDisplayStylecollection =`radioCollection`;
        $pdiDisplayStyle[0]=`radioButton -label "All Pdi Entities" -select -onc "setPdiDisplayMode"`;
        $pdiDisplayStyle[1]=`radioButton -label "Selected only" -onc "setPdiDisplayMode"`;
        setParent ..;
    setParent ..;
	frameLayout -width 100 -height 50 -label "" -borderStyle "etchedOut";
	    columnLayout -columnAttach "left" 30;    
//	    $chkPdiFitUnitsMaya = `checkBox -label "Fits units to Maya Grid" -align "left" -changeCommand "setPptyMayaUnits"`;	              
	    $chkPdiGridAsGround = `checkBox -label "Use Maya grid as ground" -align "left" -changeCommand "setPdiGridAsGround"`;	              
	    setParent ..;
	setParent ..;    	            
    frameLayout -width 100 -height 50 -label "Performance" -borderStyle "etchedOut";
        rowColumnLayout -numberOfRows 1 -co 1 "left" 30;
//        text -label "Number of threads"   -align "left";
//        $fieldPdiNumThreads = `intField -w 50 -s 1 -minValue -1 -maxValue 4 -value 1 -changeCommand "updatePdiNumThreads"`;      
        $chkPdiNumThreads = `checkBox -label "Enable Multithreading" -align "left" -enable 0
        -ann " enable/disable using all cores for computing" -changeCommand "updatePdiNumThreads"`;	                       
        text -label "        "   -align "left";        
        $chkPdiCachedMode = `checkBox -label "Enable cached mode" -align "left" -enable 1
        -ann " enable/disable computation cached during playback" -changeCommand "setPdiCachedMode"`;	              
        setParent ..;
    setParent ..;
	frameLayout -width 100 -height 70 -label "Gravity" -borderStyle "etchedOut";
	    columnLayout -columnAttach "left" 30;
		$chkPdiGravityEnabled = `checkBox -label "disabled" -align "left" -changeCommand "setPptyGravityEnabled"`;
        attrFieldGrp -at ($PDISolver + ".gravity");
	    setParent ..;
    setParent ..;    
    frameLayout -width 100 -height 100 -label "" -borderStyle "etchedOut";    
	    columnLayout -columnAttach "left" 1;
			rowColumnLayout -numberOfRows 1 -co 1 "left" 1;
			text -label "Start frame "   -align "left" ;
//free version disabled			
//			$fieldPdiStartFrame = `intField -w 50 -enable false -value 1 -changeCommand "updatePdiStartFrame"`;  
			$fieldPdiStartFrame = `intField -w 50 -value 1
			-ann " changes the start frame for dynamics" -changeCommand "updatePdiStartFrame"`;  
			setParent ..;
        $comboSimQaulity = `optionMenu -label "Quality      " 
        -ann " set the overall acurracy of the computation" -cc "updatePdiSimQuality"`;
            menuItem -label "Preview";
            menuItem -label "Low";
            menuItem -label "Medium";
            menuItem -label "High";
        $fieldPdiSubstep = `attrFieldSliderGrp -min 1 -max 150 -s 5 -at ($PDISolver + ".substeps")
        -ann "set computation steps per frame" -cc "updatePdiSubStep"`;
	    setParent ..;        
    setParent ..;

    
	int $state=`grid -q -toggle`;
	if( $state == 0)
	{	
		setAttr ($PDISolver + ".useGroundGrid" ) false;			
	}

    updatePdiSolverPanel();

}
/****************************************
	PDI MANAGE PANEL
******************************************/
global proc addBodiesToPdiWorld()
{
//    global string $pdiManageWindow;
    global string $pdiBodiesList;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";

       return;
    }

	string  $disabledPDIObjects[];
    for( $obj in $PDIObjects)
    {
		int $enabled = getAttr ($obj + ".enabled" );
        if( $enabled == 0)
        {
            $disabledPDIObjects[size($disabledPDIObjects)]= $obj;
        }
    }
    ManagePdiListCmd -add true $disabledPDIObjects;

    int $i;
    for ( $i=0; $i < size($disabledPDIObjects); $i++ )
	{
        textScrollList -edit -append $disabledPDIObjects[$i] $pdiBodiesList;
    }
 //   showWindow $pdiManageWindow;
}
global proc deleteBodiesFromPdiWorld()
{
    global string $pdiBodiesList;

    undoInfo -swf off;

    string $result = `confirmDialog -title "Confirm" -message "This action will destroy selected Pdi nodes, are you sure?"
    -button "Yes" -button "No" -defaultButton "Yes"
    -cancelButton "No" -dismissString "No"`;

    if( $result == "Yes")
    {
        string $selection[];
        $selection = `textScrollList -query -selectItem $pdiBodiesList`;

        ManagePdiListCmd $selection;

        int $i;
        for ( $i=0; $i < size($selection); $i++ )
	    {
            textScrollList -edit -removeItem $selection[$i] $pdiBodiesList;
        }

    	for ( $obj in $selection )
	    {
//            deleteUserNode $obj;
            delete $obj;
        }
    }
    undoInfo -swf on;
}
global proc deleteAllPdiEntities()
{
    string $result = `confirmDialog -title "Confirm" -message "this action will prevent forever to apply Pdi jaggyness to fragments,are you sure?"
    -button "Yes" -button "No" -defaultButton "Yes"
    -cancelButton "No" -dismissString "No"`;

    if( $result == "Yes")
    {
	    undoInfo -swf off;
	    //delete history of pdi fragmetns
   	    string $PDIFragments[];
		$PDIFragments =`ls  -dag -type "transform"`;
	    for( $obj in $PDIFragments)
		{		
			$Parent=`listRelatives -p $obj`;
			$voronoiNode=`listRelatives -ad -typ voronoiNode $Parent`;
			if( size($voronoiNode) > 0)
			{
//			    print ("delete history fragment" + $obj + "\n");
				delete -ch $obj;
			}
		}		
	    
        //delete voronoi nodes( removed becouse of jaggyness params)
    	 string  $VoroObjects[];
	     $VoroObjects = `ls -dag -leaf -type voronoiNode`;
	    //print $VObjects;
    	 for ( $obj in $VoroObjects )
	     {
            $transform = `listRelatives -parent $obj`;

            delete $obj;
            delete $transform;
         }

         print( "all dependencies with Pdi plugin have been removed");	    

		undoInfo -swf on;    
	}	
}
global proc deleteAllBodiesFromPdiWorld()
{
    global string $pdiBodiesList;
	global string $butDeletePdiHelpers;

    undoInfo -swf off;

    string $result = `confirmDialog -title "Confirm" -message "This action will destroy all Pdi nodes, are you sure?"
    -button "Yes" -button "No" -defaultButton "Yes"
    -cancelButton "No" -dismissString "No"`;

    if( $result == "Yes")
    {
//        deleteAllPdiFbodies();
    	string  $PDIFbodies[];
	    $PDIFbodies = `ls -dag -leaf -type fBodyNode`;
    	string  $PDIObjects[];
	    $PDIObjects = `ls -dag -leaf -type pdiRigidBody`;
	    
//	    int nfbodies=size( $PDIFbodies);
//	    int nbodies=size( $PDIObjects);
	    
			for ( $obj in $PDIFbodies )
			{
				delete_dFBody $obj;
			}
			if ( `textScrollList -ex $pdiBodiesList` )
			{
				textScrollList -edit -removeAll $pdiBodiesList;
			}
    		for ( $obj in $PDIObjects )
			{
				deleteUserNode $obj;
	//            delete $obj;
			}
	    
			string $PDISolver = `pdiSolver`;

			deleteUserNode $PDISolver;
	//        delete $PDISolver;
	
	    button -e -enable true  $butDeletePdiHelpers;		

	    if ( `window -ex winPdiBasicFractures` )
		{                
			updatePdiBasicFracturesPanel();
		}
		if ( `window -ex winPdiAvFractures` )
		{                
			updatePdiAdvancedFracturesPanel();
		}
				
        print( "all PDI bodies has been deleted");	    
    }

    undoInfo -swf on;

}
global proc deleteAllBodiesFromPdiWorldWithoutConfirm()
{
    global string $pdiBodiesList;

    print( "deleting all Pdi entities\n");

//        deleteAllPdiFbodies();
	string  $PDIFbodies[];
    $PDIFbodies = `ls -dag -leaf -type fBodyNode`;
    for ( $obj in $PDIFbodies )
    {
        delete_dFBody $obj;
    }

	string  $PDIObjects[];
    $PDIObjects = `ls -dag -leaf -type pdiRigidBody`;
    if ( `textScrollList -ex $pdiBodiesList` )
    {
        textScrollList -edit -removeAll $pdiBodiesList;
    }
	for ( $obj in $PDIObjects )
    {
        delete $obj;
    }

    //delete voronoi nodes
	string  $VoroObjects[];
    $VoroObjects = `ls -dag -leaf -type voronoiNode`;
    //print $VObjects;
	for ( $obj in $VoroObjects )
    {
        $transform = `listRelatives -parent $obj`;

        delete $obj;
        delete $transform;
    }
    
    string $PDISolver = `pdiSolver`;

    deleteUserNode $PDISolver;

}

global proc removeAllBodiesFromPdiWorld()
{
    global string $pdiBodiesList;

    string $selection[];
    $selection = `textScrollList -query -allItems $pdiBodiesList`;

    ManagePdiListCmd -add false $selection;

    int $i;
    for ( $i=0; $i < size($selection); $i++ )
	{
        textScrollList -edit -removeItem $selection[$i] $pdiBodiesList;
    }

//    textScrollList -removeAll $pdiBodiesList;
}

global proc removeBodiesFromPdiWorld()
{
    global string $pdiBodiesList;

    string $selection[];
    $selection = `textScrollList -query -selectItem $pdiBodiesList`;

    ManagePdiListCmd $selection;

    int $i;
    for ( $i=0; $i < size($selection); $i++ )
	{
        textScrollList -edit -removeItem $selection[$i] $pdiBodiesList;
    }
}
global proc updateSelectedPdiManagePanel()
{
    global string $pdiManageWindow;
   global string $pdiBodiesList;

    int $bWExist = `window -ex winPdiManageWorld`;
    //int $bIconify = `window -query -iconify $pdiCreateBodyWindow`;
    int $bListExist  = `textScrollList -ex $pdiBodiesList`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bListExist == false)
    {
//		print("pidBodyPanel iconified, abort\n");			
        return;
    }

	string  $PDISelected[];
    $PDISelected = `ls -selection -dag -leaf -type pdiRigidBody`;

//    if( size($PDISelected) > 0)
//    {
//            textScrollList -edit -selectItem $PDISelected[0] $pdiBodiesList;
        textScrollList -edit -deselectAll $pdiBodiesList;
        for ( $obj in $PDISelected )
        {
    		int $enabled = getAttr ($obj + ".enabled" );
            $connections = `listConnections -d off -s on ($obj +".fracture")`;

            if( $enabled > 0 && size($connections)==0 )
            {
                textScrollList -edit -selectItem $obj $pdiBodiesList;
            }
        }
//    }

}
global proc selectNegativeMassobjects()
{
	string  $PDIObjects[];
	$PDIObjects = `ls -dag -leaf -type pdiRigidBody`;
    if( size($PDIObjects) > 0)
    {
	    int $i=0;
	    string $dynamicSelection[];	   
		for( $obj in $PDIObjects)
	    {
	        int $isMassNegative =`queryPdiBody -imn  $obj`;
		    if( $isMassNegative > 0 )
		    {
				string $parentobject[] =`listRelatives -p $obj`;		    
			    $dynamicSelection[$i] = $parentobject[0];$i++;
			}
		}
		int $nSelected=size($dynamicSelection);
		if( $nSelected > 0)
		{
			select $dynamicSelection;
			print($nSelected + " PDi bodies are non-solid\n");					
		}		
		else
		{
			print(" PDi Scene is clean of non-solid objects\n");					
		}		
	}	
	else
	{
       warning "no Pdi rigid bodies selected";		
	}	
}
global proc updatePdiManagePanel()
{
    global string $pdiManageWindow;
    global string $pdiBodiesList;
	global string $butDeletePdiHelpers;

    int $bWExist = `window -ex winPdiManageWorld`;
    int $bLstExist  = `textScrollList -ex $pdiBodiesList`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bLstExist == false)
    {
//		print("pdiManageWindow iconified, abort\n");			
        return;
    }

	string  $PDIObjects[];
	$PDIObjects = `ls -dag -leaf -type pdiRigidBody`;
    
	if(size($PDIObjects) != 0)
	{
        string  $connections[];

		$pdiUpdate = 0;//disable update		

//        print( "creating list..");
        //int $n = size($PDIObjects);
        //textScrollList -edit -numberOfRows $n $pdiBodiesList;


        textScrollList -edit -removeAll $pdiBodiesList;

    	for ( $obj in $PDIObjects )
	    {
    		int $enabled = getAttr ($obj + ".enabled" );
            $connections = `listConnections -d off -s on ($obj +".fracture")`;

            if( $enabled > 0 && size($connections)==0 )
            {
                textScrollList -edit -append $obj $pdiBodiesList;
            }
        }


		button -e -enable false $butDeletePdiHelpers;			

        //select the objects selected 
        updateSelectedPdiManagePanel();

		$pdiUpdate = 1;//enable update			
    }
    else
	{
    	 string  $VoroObjects[];
	     $VoroObjects = `ls -dag -leaf -type voronoiNode`;
		 if(size($VoroObjects) != 0)
		 {
			button -e -enable true  $butDeletePdiHelpers;			
		 }	
	}	 

    showWindow $pdiManageWindow;
}
global proc createPdiManagePanel()
{
    global string $pdiManageWindow;
    global string $pdiBodiesList;
	global string $butDeletePdiHelpers;
    global int $pdiUpdate;
    global int $UpdatePdiManagePanelJobId;

//    setToolTo `selectPdiContext`; 
//    headsUpMessage "Pdi Interactive selection Tool has been activated";

    //check if the window exists
    if ( `window -ex winPdiManageWorld` )
    {                
        updatePdiManagePanel();
        
        return;
    }
    $pdiUpdate = 1;//enable update			

	$pdiManageWindow = `window -rtf true -t "Manage PDI Rigid Bodies" -width 160 -height 160 winPdiManageWorld`;
    string $form = `formLayout -numberOfDivisions 200`;
    $pdiBodiesList = `textScrollList -w 100 -numberOfRows 12 -allowMultiSelection true` ;
            //-append "one"      -append "two"      -append "three"
            //-append "four"     -append "five"     -append "six"
            //-append "seven"    -append "eight"    -append "nine"
            //-append "ten"      -append "eleven"   -append "twelve"
            //-append "thirteen" -append "fourteen" -append "fifteen"
            //-selectItem "six"
            //-showIndexedItem 4`;
    string $butAdd = `button  -w 100 -label "Add"
    -ann " add selected bodies to computation"
     -command "addBodiesToPdiWorld"`;
    string $butRemove =`button  -w 100 -label "Remove"
    -ann " remove temporally selected bodies from computation "    
     -command "removeBodiesFromPdiWorld"`;
    string $butRemoveAll =`button  -w 100 -label "Remove All"
    -ann " remove temporally all bodies from computation "        
     -command "removeAllBodiesFromPdiWorld"`;
    string $butDelete =`button  -w 150 -label "Delete selected"
    -ann " remove permanently selected bodies from computation "    
     -command "deleteBodiesFromPdiWorld"`;
    string $butDeleteAllNodes =`button  -w 140 -label "Delete All Pdi Bodies" 
						-ann "HINT:delete Pdi nodes after baking simulation for freeing resources"
						-command "deleteAllBodiesFromPdiWorld"`;
    $butDeletePdiHelpers =`button  -w 140 -label "Delete All Pdi Entities" -en false
						-ann "HINT:delete all Pdi entities removes any dependence on Pulldownit plugin"
						-command "deleteAllPdiEntities"`;

    string $butSelectPdiNoMass =`button  -w 140 -label "Select non-solid Objects" -en true
						-ann "HINT:non-solid objects cause issues in dynamics"
						-command "selectNegativeMassobjects"`;
    formLayout -edit
        -attachForm     $pdiBodiesList      "top"    0
        -attachForm     $pdiBodiesList      "left"    0
        -attachForm     $pdiBodiesList      "right"    0
        -attachForm     $pdiBodiesList      "bottom" 160
        -attachControl  $butAdd      "top"    0 $pdiBodiesList
        -attachForm     $butAdd      "left"   0
        -attachControl      $butRemove      "top"    0 $pdiBodiesList
        -attachControl     $butRemove     "left" 0 $butAdd
        -attachControl      $butRemoveAll      "top"    0 $pdiBodiesList
        -attachControl     $butRemoveAll     "left" 0 $butRemove
        -attachForm     $butRemoveAll      "right" 0
        -attachControl      $butDelete      "top"    0 $butRemoveAll
        -attachForm     $butDelete      "left"   0
        -attachForm     $butDelete      "right" 0
        //-attachControl  $butBakePdiSim      "top" 10 $butAdd
        //-attachForm     $butBakePdiSim      "left" 20
        //-attachForm     $butBakePdiSim      "right" 20
        -attachControl  $butDeleteAllNodes      "top" 10 $butDelete
        -attachForm     $butDeleteAllNodes      "left" 20
        -attachForm     $butDeleteAllNodes      "right" 20
        -attachControl  $butDeletePdiHelpers      "top" 10 $butDeleteAllNodes
        -attachForm     $butDeletePdiHelpers      "left" 20
        -attachForm     $butDeletePdiHelpers      "right" 20
        -attachControl  $butSelectPdiNoMass      "top" 10 $butDeletePdiHelpers
        -attachForm     $butSelectPdiNoMass      "left" 20
        -attachForm     $butSelectPdiNoMass      "right" 20
    $form;

    $UpdatePdiManagePanelJobId = `scriptJob -event SelectionChanged updateSelectedPdiManagePanel`;
    scriptJob -uiDeleted $pdiManageWindow killUpdateManagePanelJob;

    updatePdiManagePanel();
}
/****************************************
	PDI PPTIES PANEL
******************************************/
global proc setPptyPdiFriction()
{
   global string $fieldPdiFriction;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $friction = `floatSliderGrp -query -value $fieldPdiFriction`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".friction" ) $friction;
        setAttr ($obj + ".usercommand" ) true;
        //force update
        getAttr ($obj + ".ca_rigidBodyParam" );                
    }

//	updatePdiPropertiesPanel();

}
global proc setPptyPdiBounciness()
{
    global string $fieldPdiBounciness;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $bounciness = `floatSliderGrp -query -value $fieldPdiBounciness`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".bounciness" ) $bounciness;
        setAttr ($obj + ".usercommand" ) true;
        //force update
        getAttr ($obj + ".ca_rigidBodyParam" );        
        
    }


//    updatePdiPropertiesPanel();
}
global proc setPptyPdiLdamping()
{
    global string $fieldPdiLdamping;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $ldamping = `floatSliderGrp -query -value $fieldPdiLdamping`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".linearDamping" ) $ldamping;
        setAttr ($obj + ".usercommand" ) true;
        //force update
        getAttr ($obj + ".ca_rigidBodyParam" );                
    }


//    updatePdiPropertiesPanel();

}
global proc setPptyPdiAdamping()
{
    global string $fieldPdiAdamping;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $adamping = `floatSliderGrp -query -value $fieldPdiAdamping`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".angularDamping" ) $adamping;
        setAttr ($obj + ".usercommand" ) true;
        //force update
        getAttr ($obj + ".ca_rigidBodyParam" );                
    }


//    updatePdiPropertiesPanel();

}
global proc setPptyPdiMass()
{
    global string $fieldPdiMass;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $mass = `floatFieldGrp -query -value1 $fieldPdiMass`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".mass" ) $mass;
        setAttr ($obj + ".usercommand" ) true;
        //force update
        getAttr ($obj + ".ca_rigidBodyParam" );        
    }

//    updatePdiPropertiesPanel();

}
global proc setPptyPdiCenterMass()
{
   global string $fieldPdiCenterMass;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $cx = `floatFieldGrp -query -value1 $fieldPdiCenterMass`;
    float $cy = `floatFieldGrp -query -value2 $fieldPdiCenterMass`;
    float $cz = `floatFieldGrp -query -value3 $fieldPdiCenterMass`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
        setAttr ($obj + ".massCenter" ) -type "float3" $cx $cy $cz;
        //force update
        getAttr ($obj + ".ca_centerMass" );                
    }

//    updatePdiPropertiesPanel();

}
global proc setPptyPdiVelocity()
{
   global string $fieldPdiVelocity;
    global string $fieldPdiRandomV;
    //global int $rvx;
    //global int $rvy;
    //global int $rvz;

    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $vx0 = `floatFieldGrp -query -value1 $fieldPdiVelocity`;
    float $vy0 = `floatFieldGrp -query -value2 $fieldPdiVelocity`;
    float $vz0 = `floatFieldGrp -query -value3 $fieldPdiVelocity`;

    int $rvx = `intFieldGrp -query -value1 $fieldPdiRandomV`;
    int $rvy = `intFieldGrp -query -value2 $fieldPdiRandomV`;
    int $rvz = `intFieldGrp -query -value3 $fieldPdiRandomV`;

    seed 7718;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
        float $vx = $vx0; 
        float $vy = $vy0; 
        float $vz = $vz0; 

        float $rnd = `rand 1`;
        if( $rvx > 0.0)
        {
            float $randFactorX = $rnd * (2.0*$rvx) - $rvx;
            $vx += $vx*($randFactorX/100.0);
        }    
        if( $rvy > 0.0)
        {
            float $randFactorY = $rnd * (2.0*$rvy) - $rvy;
            $vy += $vy*($randFactorY/100.0);
        }    
        if( $rvz > 0.0)
        {
            float $randFactorZ = $rnd * (2.0*$rvz) - $rvz;
            $vz += $vz*($randFactorZ/100.0);
        }    

        setAttr ($obj + ".initialVelocity" ) -type "float3" $vx $vy $vz;
        setAttr ($obj + ".randomVX" )  $rvx;
        setAttr ($obj + ".randomVY" )  $rvy;
        setAttr ($obj + ".randomVZ" )  $rvz;

        //force update
        getAttr ($obj + ".velocity" );
    }

//    updatePdiPropertiesPanel();

}
global proc setPptyPdiWelocity()
{
   global string $fieldPdiWelocity;
   global string $fieldPdiRandomW;

    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $vx0 = `floatFieldGrp -query -value1 $fieldPdiWelocity`;
    float $vy0 = `floatFieldGrp -query -value2 $fieldPdiWelocity`;
    float $vz0 = `floatFieldGrp -query -value3 $fieldPdiWelocity`;

    int $bFieldExist  = `intFieldGrp -ex $fieldPdiRandomW`;
    if( $bFieldExist)
    {
        print "$fieldPdiRandomW exist\n";
    }
    else
    {
        print "$fieldPdiRandomW doesnt exist\n";
    }

    int $rwx = `intFieldGrp -query -value1 $fieldPdiRandomW`;
    int $rwy = `intFieldGrp -query -value2 $fieldPdiRandomW`;
    int $rwz = `intFieldGrp -query -value3 $fieldPdiRandomW`;

    seed 7718;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size( $PDIObjects) == 0)
    {
       warning "Not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
        float $vx = $vx0; 
        float $vy = $vy0; 
        float $vz = $vz0; 

        float $rnd = `rand 1`;
        if( $rwx > 0.0)
        {
            float $randFactorX = $rnd * (2.0*$rwx) - $rwx;
            $vx += $vx*($randFactorX/100.0);
        }    
        if( $rwy > 0.0)
        {
            float $randFactorY = $rnd * (2.0*$rwy) - $rwy;
            $vy += $vy*($randFactorY/100.0);
        }    
        if( $rwz > 0.0)
        {
            float $randFactorZ = $rnd * (2.0*$rwz) - $rwz;
            $vz += $vz*($randFactorZ/100.0);
        }    

        setAttr ($obj + ".initialSpin" ) -type "float3" $vx $vy $vz;
        setAttr ($obj + ".randomWX" )  $rwx;
        setAttr ($obj + ".randomWY" )  $rwy;
        setAttr ($obj + ".randomWZ" )  $rwz;

        //force update
        getAttr ($obj + ".welocity" );

    }

//    updatePdiPropertiesPanel();

}
global proc setPptyPdiMayaFields()
{
    global string $butPdiForceFieldList;
    global string $chkPdiMayaFields;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    $state = `checkBox -query -value $chkPdiMayaFields`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
	for ( $obj in $PDIObjects )
	{
        setAttr ($obj + ".affectForceFields" ) $state;
         //force update
        getAttr ($obj + ".ca_forceFields" );
    }


    string  $forceFields[];
    if( $state == true)
    {
        $forceFields = `ls -tr -dag -leaf`;//warning: this includes all transform nodes in scene
    }

    for( $obj in $PDIObjects)
    {
        pdiConnectForceFields -bodyName $obj -connect $forceFields;
    }

//    updatePdiPropertiesPanel();

}
global proc setPptyPdiGapFactor()
{
    global string $fieldPdiGapFactor;
    global int $pdiUpdate;
	if( $pdiUpdate == false)
	{
		return;	
	}	

    float $gapfactor = `floatSliderGrp -query -value $fieldPdiGapFactor`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( $gapfactor > 0.25)
    {
        warning " This gap value can cause unstabilities";
    }
    else
    {
        print (" ");
    }

    if( size( $PDIObjects) == 0)
    {
        warning "not Pdi bodies selected";
    }

	for ( $obj in $PDIObjects )
	{
		setAttr ($obj + ".gapfactor" ) $gapfactor;
        setAttr ($obj + ".usercommand" ) true;
    }
}

global proc pdiIncludeField()
{
}

global proc pdiExcludeField()
{
}
global proc pdiAcceptFields()
{
    global string $pdiIncludedFieldsList;

    string  $forceFields[];
    $forceFields = `textScrollList -query -allItems $pdiIncludedFieldsList`;

	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;

    for( $obj in $PDIObjects)
    {
        pdiConnectForceFields -bodyName $obj -connect $forceFields;
    }

    layoutDialog -dismiss "OK";
}

global proc createPdiIncludeExcludeFieldsWin()
{
    
}

global proc pdiExcludeIncludeForceFields()
{
}
global proc updatePdiPropertiesPanel()
{
   global string $pdiPropertiesWindow;
   global string $chkPdiMayaFields;
   global string $fieldPdiFriction;
   global string $fieldPdiBounciness;
   global string $fieldPdiLdamping;
   global string $fieldPdiAdamping;
   global string $fieldPdiGapFactor;
   global string $fieldPdiGapFactor;
   global string $fieldPdiMass;
   global string $fieldPdiCenterMass;
   global string $fieldPdiVelocity;
   global string $fieldPdiWelocity;
   global string $fieldPdiRandomV;
   global string $fieldPdiRandomW;
   global string $butPdiForceFieldList;
   global string $ChkPdiPassive;    

   global int $pdiUpdate;

    int $bWExist = `window -ex winPdiProperties`;
    //int $bIconify = `window -query -iconify $pdiCreateBodyWindow`;
    int $bFieldExist  = `floatFieldGrp -ex $fieldPdiMass`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bFieldExist == false)
    {
//		print("pidBodyPanel iconified, abort\n");			
        return;
    }
	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
//    print "pdi objects update  ppties\n";
	if(size($PDIObjects) != 0)
	{
		$pdiUpdate = 0;//disable update					

        string $selectedObject = $PDIObjects[0];
        //select a dynamics objects if any
        int $passive = 0;
        for( $obj in $PDIObjects)
        {
            $selectedObject = $obj;
    		$passive = getAttr ($obj + ".passive" );
            if( $passive == 0)
            {
                break;
            }
        }
                
        float $mass = getAttr ($selectedObject + ".mass" );
        int $mayaFieldsValue = getAttr ($selectedObject + ".affectForceFields" );
        float $friction = getAttr ($selectedObject + ".friction" );
        float $bounciness = getAttr ($selectedObject + ".bounciness" );
        float $ldamping = getAttr ($selectedObject + ".linearDamping" );
        float $adamping = getAttr ($selectedObject + ".angularDamping" );
        float $gapfactor = getAttr ($selectedObject + ".gapfactor" );
        vector $masscenter = getAttr ($selectedObject + ".massCenter" );
        vector $velocity = getAttr ($selectedObject + ".initialVelocity" );
        vector $welocity = getAttr ($selectedObject + ".initialSpin" );
        int $rvx = getAttr ($selectedObject + ".randomVX" );
        int $rvy = getAttr ($selectedObject + ".randomVY" );
        int $rvz = getAttr ($selectedObject + ".randomVZ" );
        int $rwx = getAttr ($selectedObject + ".randomWX" );
        int $rwy = getAttr ($selectedObject + ".randomWY" );
        int $rwz = getAttr ($selectedObject + ".randomWZ" );
        int $tfr = getAttr ($selectedObject + ".transFrame" );
        int $isTransition =`queryPdiBody -itr  $selectedObject`;    

        checkBox -edit -value $mayaFieldsValue $chkPdiMayaFields;
        floatSliderGrp -edit -value $friction $fieldPdiFriction;
        floatSliderGrp -edit -value $bounciness $fieldPdiBounciness;
        floatSliderGrp -edit -value $ldamping $fieldPdiLdamping;
        floatSliderGrp -edit -value $adamping $fieldPdiAdamping;
        floatSliderGrp -edit -value $gapfactor $fieldPdiGapFactor;
        if( $gapfactor > 0.25)
        {
            warning " This gap value can cause unstabilities";
        }
        floatFieldGrp  -edit -value1 $mass $fieldPdiMass;
        floatFieldGrp  -edit
            -value1 ($masscenter.x)
            -value2 ($masscenter.y)
            -value3 ($masscenter.z)
            $fieldPdiCenterMass;
        floatFieldGrp  -edit
            -value1 ($velocity.x)
            -value2 ($velocity.y)
            -value3 ($velocity.z)
            $fieldPdiVelocity;
        floatFieldGrp  -edit
            -value1 ($welocity.x)
            -value2 ($welocity.y)
            -value3 ($welocity.z)
            $fieldPdiWelocity;
        intFieldGrp  -edit
            -value1 $rvx
            -value2 $rvy
            -value3 $rvz
            $fieldPdiRandomV;
        intFieldGrp  -edit
            -value1 $rwx
            -value2 $rwy
            -value3 $rwz
            $fieldPdiRandomW;

	    if( $passive > 0)
	    {
			checkBox -edit -enable 0 $chkPdiMayaFields;
			floatSliderGrp -edit -enable 0 $fieldPdiLdamping;
			floatSliderGrp -edit -enable 0 $fieldPdiAdamping;
			floatSliderGrp -edit -enable 0 $fieldPdiGapFactor;	   
	        floatFieldGrp  -edit -enable 0 $fieldPdiMass;	
	        floatFieldGrp  -edit -enable 0 $fieldPdiCenterMass;	
	        floatFieldGrp  -edit -enable 0 $fieldPdiVelocity;	
	        floatFieldGrp  -edit -enable 0 $fieldPdiWelocity;	
	        intFieldGrp  -edit -enable 0 $fieldPdiRandomV;	
	        intFieldGrp  -edit -enable 0 $fieldPdiRandomW;	 	
		}
		else
		{
			checkBox -edit -enable 1 $chkPdiMayaFields;
			floatSliderGrp -edit -enable 1 $fieldPdiLdamping;
			floatSliderGrp -edit -enable 1 $fieldPdiAdamping;
			floatSliderGrp -edit -enable 1 $fieldPdiGapFactor;	   
	        floatFieldGrp  -edit -enable 1 $fieldPdiMass;	
	        floatFieldGrp  -edit -enable 1 $fieldPdiCenterMass;	
	        floatFieldGrp  -edit -enable 1 $fieldPdiVelocity;	
	        floatFieldGrp  -edit -enable 1 $fieldPdiWelocity;	
	        intFieldGrp  -edit -enable 1 $fieldPdiRandomV;	
	        intFieldGrp  -edit -enable 1 $fieldPdiRandomW;	 
		}

		$pdiUpdate = 1;//enable update			
    }

    showWindow $pdiPropertiesWindow;

	setFocus MayaWindow;    
}
global proc createPdiPropertiesPanel()
{
    global string $pdiPropertiesWindow;
    global string $chkPdiMayaFields;
    global string $fieldPdiFriction;
    global string $fieldPdiBounciness;
    global string $fieldPdiLdamping;
    global string $fieldPdiAdamping;
    global string $fieldPdiGapFactor;
    global string $fieldPdiMass;
    global string $fieldPdiCenterMass;
    global string $fieldPdiVelocity;
    global string $fieldPdiRandomV;
    global string $fieldPdiWelocity;
    global string $fieldPdiRandomW;
    global string $butPdiForceFieldList;
    global int $UpdatePdiPropertiesPanelJobId;
    global int $pdiUpdate;

//    setToolTo `selectPdiContext`; 
//    headsUpMessage "Pdi Interactive selection Tool has been activated";


    //check if the window exists
    if ( `window -ex winPdiProperties` )
    {                
        updatePdiPropertiesPanel();
        
        return;
    }
    $pdiUpdate = 1;//enable update		

    string $pdiPropertiesWindow = `window -rtf true -t "Pdi Dynamics Properties" -width 200 -height 300 winPdiProperties`;
	columnLayout -adjustableColumn true; 
        $fieldPdiBounciness = `floatSliderGrp -label "Bounciness" -field true
	        -minValue 0.0 -maxValue 1.0 -pre 3
	        -ann "set the strenght of the bounce in collision"
            -changeCommand "setPptyPdiBounciness"` ;
        $fieldPdiFriction = `floatSliderGrp -label "Friction" -field true
	        -minValue 0.0 -maxValue 1.0 -pre 3
            -ann "set the strenght of friction in collision"
            -changeCommand "setPptyPdiFriction"` ;

////////////////////////////////////////active params
        $fieldPdiLdamping = `floatSliderGrp -label "Linear Damping" -field true
	        -minValue 0.0 -maxValue 1.0 -pre 3
            -ann "set linear damping value for dynamics objects"            	        
            -changeCommand "setPptyPdiLdamping"` ;
        $fieldPdiAdamping = `floatSliderGrp -label "Angular Damping" -field true
	        -minValue 0.0 -maxValue 1.0 -pre 3
            -ann "set angular damping value for dynamics objects"            	        
            -changeCommand "setPptyPdiAdamping"` ;
        $fieldPdiGapFactor = `floatSliderGrp -label "Gap factor" -field true
	        -minValue 0.0 -maxValue 1.0 -pre 3
            -ann "gap factor forces objects to keep separate"            	        	        
            -changeCommand "setPptyPdiGapFactor"` ;
        $fieldPdiMass = `floatFieldGrp -numberOfFields 1 -label "Mass" 
	        -value1 1.0  -pre 3
            -ann "set mass of the object"            	        	        	        
            -changeCommand "setPptyPdiMass"` ;
        $fieldPdiCenterMass = `floatFieldGrp -numberOfFields 3 -label "Mass Centre" 
	        -value1 0.0 -value2 0.0 -value3 0.0  
            -ann "set center of mass for the object"            	        	        	        	        
            -changeCommand "setPptyPdiCenterMass"` ;
        frameLayout -width 100 -height 70 -label "Initial Velocity" -borderStyle "etchedOut";
		columnLayout -columnAttach "left" 20;
            $fieldPdiVelocity = `floatFieldGrp -numberOfFields 3 -label "Value" 
	            -value1 0.0 -value2 0.0 -value3 0.0  
				-ann "set linear velocity of the object"            	        	        	        	        	            
                -changeCommand "setPptyPdiVelocity"` ;
            $fieldPdiRandomV = `intFieldGrp -numberOfFields 3 -label "Random" -extraLabel "%"
	            -value1 0 -value2 0 -value3 0  
				-ann "set linear random offset when setting several objects at once"            	        	        	        	        	            
                -changeCommand "setPptyPdiVelocity"` ;
		    setParent ..;
		setParent ..;
        frameLayout -width 100 -height 70 -label "Initial Spin" -borderStyle "etchedOut";
		columnLayout -columnAttach "left" 20;
            $fieldPdiWelocity = `floatFieldGrp -numberOfFields 3 -label "Value" 
	            -value1 0.0 -value2 0.0 -value3 0.0  
				-ann "set angular velocity of the body"            	        	        	        	        	            	            
                -changeCommand "setPptyPdiWelocity"` ;
            $fieldPdiRandomW = `intFieldGrp -numberOfFields 3 -label "Random" -extraLabel "%"
	            -value1 0 -value2 0 -value3 0  
				-ann "set angular random offset when setting several objects at once"            	        	        	        	        	            	            
                -changeCommand "setPptyPdiWelocity"` ;
		    setParent ..;
		setParent ..;
	$chkPdiMayaFields = `checkBox -v false -label "affected by Force Fields" -align "left"
	-ann "enable/disable force fields affecting the motion of the body"            	        	        	        	        	            	            
	 -changeCommand "setPptyPdiMayaFields"`;	              
    $butPdiForceFieldList =`button  -enable false -w 150 -h 30 -label "Include/Exclude"
	-ann "select the fields affecting the motion of the body"            	        	        	        	        	            	                
     -command "pdiExcludeIncludeForceFields"`;


   $UpdatePdiPropertiesPanelJobId = `scriptJob -event SelectionChanged updatePdiPropertiesPanel`;
   scriptJob -uiDeleted $pdiPropertiesWindow killUpdatePropertiesPanelJob;


    updatePdiPropertiesPanel();

}
/////////////////////////////////////////////////
//  
//  Bake  Keys  Panel
//
/////////////////////////////////////////////////
global proc bakePdiSimulation()
{
//    global string $pdiBakeSimWindow;
    global string $gMainProgressBar;  // This is defined on maya startup
    global string $fieldPdiKeysThr;
    global string $fieldPdiRotKeysThr;
    global string $fieldPdiSampleBy;

    undoInfo -swf off;

    string $transform[];
    string $pdiSelection[];
//    $pdiSelection = `textScrollList -query -allItems $pdiBodiesList`;
	$pdiSelection = `ls -dag -leaf -type pdiRigidBody`;

    int $allowFastBaking=1;
    string $dynamicSelection[];
    for( $obj in $pdiSelection)
    {
        int $enabled = getAttr ($obj + ".enabled" );
		int $passive = getAttr ($obj + ".passive" );
        int $isFirstHitActive = getAttr ($obj + ".firstHitActive" );

	    if($enabled > 0)
	    {
            if( $passive != 0)
            {
                int $isTransition =`queryPdiBody -itr  $obj`;
                if( $isTransition > 0 || $isFirstHitActive)
                {
                    $dynamicSelection[size($dynamicSelection)] = $obj;
                    $allowFastBaking=0;
                }
            }
            else
            {
                $dynamicSelection[size($dynamicSelection)] = $obj;
            }
//            delete -sc -c -timeAnimationCurves true $obj;
        }
    }

//    dResumeSim $dynamicSelection;// just in case resume simulation

    string $pdiSolver = `pdiSolver`;

    int $startTime = `playbackOptions -query -minTime`;
    int $endTime = `playbackOptions -query -maxTime`;

    int $simTine = $endTime - $startTime;

    float $treshold = 0;
    float $treshold2 = 0;
    float $sampleby = 0;
    if ( `floatSliderGrp -ex $fieldPdiKeysThr` )
    {                
        $treshold = `floatSliderGrp -query -value $fieldPdiKeysThr`;

    }
    if ( `floatSliderGrp -ex $fieldPdiRotKeysThr` )
    {                
        $treshold2 = `floatSliderGrp -query -value $fieldPdiRotKeysThr`;
    }
    if ( `floatSliderGrp -ex $fieldPdiSampleBy` )
    {                
        $sampleby = `floatSliderGrp -query -value $fieldPdiSampleBy`;
    }

    if( $treshold != 0.0 || $treshold2!=0.0)
    {
        $allowFastBaking=0;
    }

    if( $allowFastBaking > 0)
    {
        print ("Bake fast");

        string $dynamicSelectionTransform[];

        for( $obj in $dynamicSelection)
        {
            string $parentobject[] =`listRelatives -p $obj`;
            $dynamicSelectionTransform[size($dynamicSelectionTransform)] =  $parentobject[0];
        }
        string $timelapse = $startTime +":" + $endTime;
        bakeResults -t $timelapse -sm true -shape false -sb $sampleby $dynamicSelectionTransform ; 
        clear($dynamicSelectionTransform);
    }
    else
    {
        progressBar -edit
        -beginProgress
        -isInterruptable true
        -status "Baking keys..."
        -maxValue $simTine
        $gMainProgressBar;

        float $i=0.0;
//        float $time_sample = 0.25;
        
        for($i=$startTime; $i <= $endTime; $i+= $sampleby)
        {
//            print ("baking frame.." + $i + "\n");

	        currentTime -edit $i;

            if(`progressBar -query -isCancelled $gMainProgressBar`)
                break;

            progressBar -edit -step 1 $gMainProgressBar;

            int $j;
            for( $j = 0; $j  < size($dynamicSelection); $j++)
            {
    		    int $passive = getAttr ($dynamicSelection[$j] + ".passive" );
        		int $isFirstHitActive = getAttr ($dynamicSelection[$j] + ".firstHitActive" );

                if( $passive > 0 && $isFirstHitActive == 0)// && $isTransitionNode[$j]==0)
                {
                    continue;
                }

                $transform = `listRelatives -parent $dynamicSelection[$j]`;

                if( size( $transform) > 0)
                {
                    //bake translation
	                float  $linX    = `getAttr ($transform[0] + ".translateX")`;
	                float  $linY    = `getAttr ($transform[0] + ".translateY")`;
	                float  $linZ    = `getAttr ($transform[0] + ".translateZ")`;
                    
                    float  $dist = 1000000.0;
                    if( $i > $startTime)
                    {
                        vector $pos0 = getAttr ($dynamicSelection[$j] + ".lastPosition" );

                        float  $distX = ( $linX - ($pos0.x)); 
                        float  $distY = ( $linY - ($pos0.y)); 
                        float  $distZ = ( $linZ - ($pos0.z)); 
                        $dist = $distX*$distX + $distY*$distY +$distZ*$distZ;
                    }
                    
                    if( $dist > $treshold)
                    {
//                        string $keytras =  "key tras : " + $linX + " " + $linY + " " + $linZ + "\n";
//                        print $keytras;

	                    setKeyframe -shape false -v $linX -at translateX $transform[0];
	                    setKeyframe -shape false -v $linY -at translateY $transform[0];
	                    setKeyframe -shape false -v $linZ -at translateZ $transform[0];

                        setAttr ($dynamicSelection[$j] + ".lastPosition" ) -type "float3" $linX $linY $linZ;
                    }

                    //bake rotation
	                float  $angleX    = `getAttr ($transform[0] + ".rotateX")`;
	                float  $angleY    = `getAttr ($transform[0] + ".rotateY")`;
	                float  $angleZ    = `getAttr ($transform[0] + ".rotateZ")`;

                    int $bEqual = 0;                         
                    if( $i > $startTime)
                    {
                         vector $rot0 = getAttr ($dynamicSelection[$j] + ".lastRotation" );

                         $bEqual = `dComputeRotDist -rx $angleX -ry $angleY -rz $angleZ
                                    -rx2 ($rot0.x) -ry2 ($rot0.y) -rz2 ($rot0.z)
                                    -tol $treshold2`;
                    }

                    if( $bEqual == 0 )
                    {
//                        string $keyrot =  "key rot : " + $angleX + " " + $angleY + " " + $angleZ + "\n";
//                        print $keyrot;

	                    setKeyframe -shape false -v $angleX -at rotateX $transform[0];
	                    setKeyframe -shape false -v $angleY -at rotateY $transform[0];
	                    setKeyframe -shape false -v $angleZ -at rotateZ $transform[0];

                        setAttr ($dynamicSelection[$j] + ".lastRotation" ) -type "float3" $angleX $angleY $angleZ;
                    }
                }
            }
        }

        progressBar -edit -endProgress $gMainProgressBar;
    }
    
    //set pdi keys attribute
    for( $obj in $dynamicSelection)
    {            
        int $passive = getAttr ($obj + ".passive" );
        int $isFirstHitActive = getAttr ($obj + ".firstHitActive" );

        if( $passive > 0 && $isFirstHitActive == 0 )
        {
            continue;
        }

        setAttr ($obj + ".hasPdiKeys" ) true;
        
        //paste aniamtion keys for transition nodes
        int $transFrame = getAttr ($obj + ".transFrame" );

        string $ctime = $startTime + ":" + $transFrame;
        $transform = `listRelatives -parent $obj`;

        int $nCurves = `cutKey -time $ctime  -attribute "keysTX"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "translateX" -o "replace"  $transform;
        }
        $nCurves = ` cutKey -time $ctime  -attribute "keysTY"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "translateY"  -o "replace"  $transform;
        }
        $nCurves = `cutKey -time $ctime  -attribute "keysTZ"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "translateZ"  -o "replace"  $transform;
        }
        $nCurves = `cutKey -time $ctime  -attribute "keysRX"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "rotateX"  -o "replace"  $transform;
        }
        $nCurves = `cutKey -time $ctime  -attribute "keysRY"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "rotateY"  -o "replace"  $transform;
        }
        $nCurves = `cutKey -time $ctime  -attribute "keysRZ"  $obj`;
        if( $nCurves > 0)
        {
            pasteKey -time $ctime  -attribute "rotateZ"  -o "replace"  $transform;
        }

    }

    //diconnect pdi dynamics
    string $time[];
    $time = `ls -type time`;

    disconnectAttr ($time[0] + ".outTime") ($pdiSolver + ".inTime");

    setAttr ($pdiSolver + ".cachedMode" ) false;

    //this fail why?
//    string $startCrono = `timerX`;
//    string $totalCrono = `timerX -startTime $startCrono`;
//    string $strTotalCrono = "Total Time Baking: "+$totalCrono+"\n";
//    print $strTotalCrono;

    undoInfo -swf on;

    confirmDialog
    -title "Pdi Info"
    -message "Pdi dynamics has been disabled, delete all Pdi keys will enable it again"
    -button "OK" 
    -defaultButton "OK" ;
    
    //set playback speed to working units
    evalDeferred -lp "playbackOptions -ps 1";//defered it until bakeResults ends
    
}
global proc deleteAllPdiKeyswithoutConfirm()
{
    string $transform[];
    string $pdiSelection[];

    undoInfo -swf off;

	$pdiSelection = `ls -dag -leaf -type pdiRigidBody`;

    int $i=0;
    string $dynamicSelection[];
    for( $obj in $pdiSelection)
    {
        int $enabled = getAttr ($obj + ".enabled" );
		int $passive = getAttr ($obj + ".passive" );
	    if($enabled > 0 && $passive == 0 )
	    {
            $dynamicSelection[$i] = $obj;$i++;
            setAttr ($obj + ".hasPdiKeys" ) false;
        }
    }

    for( $obj in $dynamicSelection)
    {
        $transform = `listRelatives -parent $obj`;

        delete -channels -tac true $transform;
    }

    //connect pdi dynamics
    string $solver = `pdiSolver`;
    string $time[];
    $time = `ls -type time`;

    int $isconnected = `isConnected ($time[0] + ".outTime") ($solver + ".inTime")`;
    if( $isconnected == 0)
    {
        connectAttr ($time[0] + ".outTime") ($solver + ".inTime");
    }

    undoInfo -swf on;

}
global proc deleteAllPdiKeys()
{    
    string $transform[];
    string $pdiSelection[];


    string $result = `confirmDialog -title "Confirm" -message "This action will destroy all Pdi keys, are you sure?"
    -button "Yes" -button "No" -defaultButton "Yes"
    -cancelButton "No" -dismissString "No"`;

    if( $result == "No")
    {
        return;
    }

    undoInfo -swf off;   
    waitCursor -state on;
    
//    $pdiSelection = `textScrollList -query -allItems $pdiBodiesList`;
	$pdiSelection = `ls -dag -leaf -type pdiRigidBody`;

    int $i=0;
    string $dynamicSelection[];
    for( $obj in $pdiSelection)
    {
        int $isEnabled = getAttr ($obj + ".enabled" );
		int $isPassive = getAttr ($obj + ".passive" );
		int $isFirstHitActive = getAttr ($obj + ".firstHitActive" );
        int $isTransition =`queryPdiBody -itr  $obj`;

	    if( $isEnabled > 0 && ( $isPassive == 0 || $isTransition != 0 ) )
	    {
            $dynamicSelection[$i] = $obj;$i++;
            setAttr ($obj + ".hasPdiKeys" ) false;
        }
	    if( $isEnabled > 0 && ( $isFirstHitActive == 1 ) )
	    {
            $dynamicSelection[$i] = $obj;$i++;
            setAttr ($obj + ".hasPdiKeys" ) false;
        }

    }

    for( $obj in $dynamicSelection)
    {
        $transform = `listRelatives -parent $obj`;

//        delete -channels -tac true $transform;
        cutKey -clear -s false $transform;
    }

    //connect pdi dynamics
    string $pdiSolver = `pdiSolver`;
    string $time[];
    $time = `ls -type time`;

    int $isconnected = `isConnected ($time[0] + ".outTime") ($pdiSolver + ".inTime")`;
    if( $isconnected == 0)
    {
        connectAttr ($time[0] + ".outTime") ($pdiSolver + ".inTime");
    }

    waitCursor -state off;
//    flushUndo;
    undoInfo -swf on;

}
global proc setDefaultKeysTreshold()
{
    global string $fieldPdiKeysThr;
    global string $fieldPdiRotKeysThr;
    global string $butDefaultTreshold;
    global string $fieldPdiSampleBy;

    floatSliderGrp -edit -value 0.005 $fieldPdiKeysThr;
    floatSliderGrp -edit -value 0.12  $fieldPdiRotKeysThr;
    floatSliderGrp -edit -value 1.0  $fieldPdiSampleBy;
}
global proc cleanPdiData()
{
	createPdiManagePanel();
}
global proc updatePdiBakePanel()
{
    global string $pdiBakeSimWindow;

    showWindow $pdiBakeSimWindow;
}
global proc createPdiBakePanel()
{
    global string $pdiBakeSimWindow;
    global string $butDeleteAllPdiSim;
    global string $butDeleteAllPdiKeys;
    global string $butBakePdiSim;
    global string $fieldPdiKeysThr;
    global string $fieldPdiRotKeysThr;
    global string $fieldPdiSampleBy;
    global string $butDefaultTreshold;

    //check if the window exists
    if ( `window -ex winPdiBakeSim` )
    {                
        updatePdiBakePanel();
        
        return;
    }

	$pdiBakeSimWindow = `window -rtf true -t "Bake Pdi Simulation" -width 80 -height 60 winPdiBakeSim`;
	columnLayout -adjustableColumn true -rowSpacing 10; 
//        $fieldPdiKeysThr = `floatSliderGrp -label "Linear Threshold" -field true
//	        -minValue 0.0 -maxValue 1.0 -pre 3 -step 0.001 -value 0.0 `;
//        $fieldPdiRotKeysThr = `floatSliderGrp -label "Angular Threshold" -field true
//	        -minValue 0.0 -maxValue 1.0 -pre 3 -step 0.001 -value 0.0 `;
        $fieldPdiSampleBy = `floatSliderGrp -label "Sample by" -field true
	        -minValue 0.0 -maxValue 2.0 -pre 2 -step 0.1 -value 1.0 `;        
//        $butDefaultTreshold =`button  -w 150 -h 25 -label "Default treshold for saving resources"
//            -ann "HINT:Recommended use these setting in big scenes (1000 objects or more) "
//            -command "setDefaultKeysTreshold"`;
        $butBakePdiSim =`button  -w 80 -h 40 -label "Bake Pdi Simulation"
        -ann "bake keys for all PDI objects"
         -command "bakePdiSimulation"`;   
        $butDeleteAllPdiSim =`button  -w 80 -h 23 -label "Clean Pdi data"
            -ann "HINT:delete Pdi nodes after baking simulation for freeing resources"
            -command "cleanPdiData"`;
        $butDeleteAllPdiKeys =`button  -w 80 -h 25 -label "Delete All Pdi Keys"
        -ann "delete keys for all PDI objects"        
         -command "deleteAllPdiKeys"`;

   updatePdiBakePanel();
}
/////////////////////////////////////////////////
//  
//  Basic Fractures Panel
//
/////////////////////////////////////////////////
global proc SetUpPdiFbody()
{
    global string $PdiSelectionForFracture[];
    global string $pdiSetUpFracturesWindow;
    global string $pdiFBodiesList;
//    global string $pdiFbodySelectedBodiesList;
    global string $namePdiFbody;
    global string $fieldPdiAttachThr;
    global string $fieldPdiAttachNearest;
    global string $fieldPdiBreakThr;
    global string $fieldPdiFMass;
    global string $fieldPdiClusterize;
    global string $chkPdiEnabledFbody;
    global string $chkPdiEnabledTorsion;
    global string $chkPdiSet2Mass;    
    global string $selectedPdiFbody;
    global string $fieldPdiFBFriction;
    global string $fieldPdiFBounciness;
    global string $chkPdiMayaFieldsFbody;
    global string $chkPdiStaticFbody;   
   	global string $pdiFactivationCollection;  	
   	global string $pdiFA[2];
    
    string $name = `textFieldGrp -query -text $namePdiFbody`;

    float $attachThr = `floatSliderGrp -query -value $fieldPdiAttachThr`;
    int $bAttachNearest = `checkBox -q -value $fieldPdiAttachNearest`;	

	//string  $PDIObjects[];
 //   $PDIObjects = `ls -selection -dag -leaf -type "pdiRigidBody"`;

    waitCursor -state on;

    //string $SelectObjects[];
    //$SelectObjects = `textScrollList -query -allItems $pdiFbodySelectedBodiesList`;

    string $shapeObjects[];
    int $i=0;
    for($obj in $PdiSelectionForFracture)
    {
        string $parentobject[] =`listRelatives -p $obj`;

        string $pdiobject[] =`listRelatives -type pdiRigidBody $parentobject[0]`;
        if( size($pdiobject) == 0)
        {
            $shapeObjects[size($shapeObjects)] = $obj;
        }
        else
        {
    		string  $PDIShapes[];
	    	$PDIShapes = `listConnections -type pdiCollisionShape $pdiobject[0]`;
		    if( size($PDIShapes) > 0)
		    {	
			    int $BVolume = getAttr ($PDIShapes[0] + ".type" );
			    if( $BVolume < 2 )
                {
                    //none and capsule bvolume are not allowed
                    $shapeObjects[size($shapeObjects)] = $obj;
                }
            }
        }
      //  else
      //  {
    		//int $passive = getAttr ($pdiobject[0] + ".passive" );
    	 //   if( $passive == 0)
	     //   {
      //          $shapeObjects[size($shapeObjects)] = $obj;
      //      }
      //  }
    }

    if( size($shapeObjects) > 0)
    {
        new_PDIBody -a 0 -st 4 $shapeObjects;
    }

    string $pdiobjects[];
    for($obj in $PdiSelectionForFracture)
    {
        string $parentobject[] =`listRelatives -p $obj`;

        string $pdiRigidBody[] =`listRelatives -type pdiRigidBody $parentobject[0]`;
        if( size($pdiRigidBody) > 0)
        {
            $pdiobjects[size($pdiobjects)] = $pdiRigidBody[0];
        }
    }

    //remove first hit attr for rigid bodies
    string $fractureBodyNode;

    if( $bAttachNearest > 0)
    {
       $fractureBodyNode = `new_dFractureBody -n $name -dTh $attachThr -nearest $pdiobjects`;
    }
    else
    {
       $fractureBodyNode = `new_dFractureBody -n $name -dTh $attachThr $pdiobjects`;
    }

    waitCursor -state off;

    if( $fractureBodyNode==$name)
    {

    // group objects
    // warning: if grouping needed remove group when delete fbody
    //    group -name $name $SelectObjects;

        string $PDISolver = `pdiSolver`;
        int $indexFbodies = getAttr ($PDISolver + ".caif" );
        $indexFbodies++;
        setAttr ($PDISolver + ".caif" ) $indexFbodies;

        textScrollList -edit -append $fractureBodyNode $pdiFBodiesList;
        textScrollList -edit -selectItem $fractureBodyNode $pdiFBodiesList;
        $selectedPdiFbody = $fractureBodyNode;//select the last created by default

    	float $breakThr = getAttr ($fractureBodyNode + ".stressThreshold" );
    	float $fmass = getAttr ($fractureBodyNode + ".mass" );
    	int $clusterize = getAttr ($fractureBodyNode + ".randomThreshold" );
    	int $bEnabled = getAttr ($fractureBodyNode + ".enabled" );
    	int $bEnabledTor = getAttr ($fractureBodyNode + ".torsion" );
    	int $bEnabledS2M = getAttr ($fractureBodyNode + ".stress2mass" );
	    float $friction = getAttr ($fractureBodyNode + ".friction" );
	    float $bounciness = getAttr ($fractureBodyNode + ".bounciness" );
    	int $bForceField = getAttr ($fractureBodyNode + ".affectForceFields" );
	    int $isStatic = getAttr ($fractureBodyNode + ".passive" );    	
    	int $isfirsthit = getAttr ($fractureBodyNode + ".firstHitActive" );

	    checkBox -edit -value $isStatic $chkPdiStaticFbody;	    
        floatSliderGrp -edit -value $breakThr $fieldPdiBreakThr;
        floatFieldGrp -edit -value1 $fmass $fieldPdiFMass;
        intSliderGrp -edit -value $clusterize $fieldPdiClusterize;
		checkBox -edit -value $bEnabledTor $chkPdiEnabledTorsion;
		checkBox -edit -value $bEnabledS2M $chkPdiSet2Mass;		
        floatSliderGrp -edit -value $friction $fieldPdiFBFriction;
        floatSliderGrp -edit -value $bounciness $fieldPdiFBounciness;

		if( $isfirsthit > 0)
		{
			radioCollection -edit -select $pdiFA[1] $pdiFactivationCollection;	
        }	    
        else
		{
			radioCollection -edit -select $pdiFA[0] $pdiFactivationCollection;	
        }	    

        if( $bEnabled > 0)
        {
		    checkBox -edit -value false $chkPdiEnabledFbody;	
        }
        else
        {
		    checkBox -edit -value true $chkPdiEnabledFbody;	
        }
        if( $bForceField > 0)
        {
		    checkBox -edit -value true $chkPdiMayaFieldsFbody;	
        }
        else
        {
		    checkBox -edit -value false $chkPdiMayaFieldsFbody;	
        }

    }
    
    //do not show vertex color
    polyOptions -cs false;

//    deleteUI -window setUpFractures;
    layoutDialog -dismiss "OK";
}
global proc CheckFractureSelection()
{
    global string $pdiCkeckText;
//    global string $pdiFbodySelectedBodiesList;
    global string $PdiSelectionForFracture[];

    scrollField -edit -text "" $pdiCkeckText;

    //string $selection[];
    //$selection = `textScrollList -query -allItems $pdiFbodySelectedBodiesList`;

    string $shapeObjects[];
    int $i=0;
    for($obj in $PdiSelectionForFracture)
    {
        string $parentobject[] =`listRelatives -p $obj`;

        string $pdiobject[] =`listRelatives -type pdiRigidBody $parentobject[0]`;
        if( size($pdiobject) == 0)
        {
            $shapeObjects[size($shapeObjects)] = $obj;
        }
    }

    if( size($shapeObjects) > 0)
    {
        new_PDIBody -a 0 -st 4 $shapeObjects;
    }

    string $pdiobjects[];
    for($obj in $PdiSelectionForFracture)
    {
        string $parentobject[] =`listRelatives -p $obj`;

        string $pdiobject[] =`listRelatives -type pdiRigidBody $parentobject[0]`;
        if( size($pdiobject) > 0)
        {
            $pdiobjects[size($pdiobjects)] = $pdiobject[0];
        }
    }
    
    waitCursor -state on;

    string $result[];
    $result = `new_dFractureBody -check true $pdiobjects`;

    waitCursor -state off;

    if( size($result) > 0)
    {
        for( $str in $result)
        {
            scrollField -edit -it $str $pdiCkeckText;
        }
        
        string $endTxt = size($result) + " objects possible solaped\n";
        if( size($result) >= 15)
        {
            $endTxt += " abort checking..";
        }
        scrollField -edit -it $endTxt $pdiCkeckText;
    }
    else
    {
        scrollField -edit -it "selection seems OK\n" $pdiCkeckText;

    }

//    scrollField -edit -it "bodies 2 are solaped\n" $pdiCkeckText;
//    scrollField -edit -it "bodies 3 are solaped\n" $pdiCkeckText;

}
global proc cancelSetUpPdiFbody()
{
    layoutDialog -dismiss "CANCEL";
}
global proc createPdiSetUpFbodyWindow()
{
    global string $PdiSelectionForFracture[];
    global string $pdiSetUpFracturesWindow;
    global string $namePdiFbody;
    global string $fieldPdiAttachThr;
    global string $fieldPdiAttachNearest;
    global string $pdiFBodiesList;
    global string $pdiCkeckText;
    string $pdiFbodySelectedBodiesList;


    //int $nFbodies = `textScrollList -query -numberOfItems $pdiFBodiesList`;

    string $PDISolver = `pdiSolver`;

    int $indexFbodies = getAttr ($PDISolver + ".caif" );

    string $name = "Fbody" + $indexFbodies;

    string $form = `setParent -q`;

    // layoutDialog's are not resizable, so hard code a size here,
    // to make sure all UI elements are visible.
    //

    formLayout -e -width 300 $form;

	string $txt1= `text -label "Selected: "  -font "boldLabelFont"`;
    $pdiFbodySelectedBodiesList = `textScrollList -w 150 -numberOfRows 15 -allowMultiSelection false` ;
	$namePdiFbody =`textFieldGrp -label "Name" -text  $name`;
    $fieldPdiAttachThr = `floatSliderGrp -label "Attach Threshold" -field true
	        -minValue 0.0 -maxValue 10.0 -value 0.05 -pre 4` ;
    $fieldPdiAttachNearest = `checkBox -label "attach nearest"`;
    string $butCheck = `button  -w 120 -h 30 -label "Check selection" -command "CheckFractureSelection"`;
    string $butOKFBody = `button  -w 60  -h 30 -label "Accept" -command "SetUpPdiFbody"`;
    string $butCancelFBody = `button  -w 60 -h 30 -label "Cancel" -command "cancelSetUpPdiFbody"`;
    $pdiCkeckText = `scrollField -h 150 -wordWrap true -text "" -editable false`;

    int $spacer = 15;
    int $top = 5;
    int $edge = 5;

    formLayout -edit
    -attachForm            $txt1  "left"   $edge
        -attachControl         $pdiFbodySelectedBodiesList "top" $edge $txt1
    -attachForm            $pdiFbodySelectedBodiesList  "left"   $edge
    -attachForm            $pdiFbodySelectedBodiesList  "right"  $edge
    -attachForm            $namePdiFbody  "left" $edge
        -attachControl         $namePdiFbody "top"    $spacer  $pdiFbodySelectedBodiesList
	-attachForm           $fieldPdiAttachThr "left"   $edge
        -attachControl         $fieldPdiAttachThr  "top"  $spacer $namePdiFbody
	-attachForm           $fieldPdiAttachNearest "left"   $edge
        -attachControl         $fieldPdiAttachNearest  "top"  $spacer $fieldPdiAttachThr
	-attachForm           $butCheck "left"   $edge
        -attachControl         $butCheck  "top" $spacer $fieldPdiAttachNearest
        -attachControl         $butOKFBody  "left" $edge $butCheck
        -attachControl         $butOKFBody  "top" $spacer $fieldPdiAttachNearest
//	-attachForm           $butCancelFBody "right"   $edge
        -attachControl         $butCancelFBody  "left" $edge $butOKFBody
        -attachControl         $butCancelFBody  "top" $spacer $fieldPdiAttachNearest
	-attachForm           $pdiCkeckText "left"   $edge
	-attachForm           $pdiCkeckText "right"   $edge
        -attachControl         $pdiCkeckText "top"   $spacer  $butCheck
   $form;


    for ( $obj in $PdiSelectionForFracture)
	{
//        if( `nodeType $obj`== "pdiRigidBody")
//        {
            textScrollList -edit -append $obj $pdiFbodySelectedBodiesList;
//        }
    }
}
global proc createPdiFbody()
{
    global string $PdiSelectionForFracture[];
	global int $pdiTransFrame;
	
	$pdiTransFrame =`currentTime -query`; //save current time for trans fbodies			
	
    //create solver node if necessary
    pdiSolver;

	string  $meshObjects[];
    $meshObjects = `ls -selection -dag -leaf -visible -ni -type "mesh"`;

    if( size($meshObjects)==0)
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select at least two poligonal shapes"
        -button "OK" 
        -defaultButton "OK" ;

        return;
    }
    if( size($meshObjects)==1)
    {
		//select all meshes in the shatter group
		string $transformObject[] =`listRelatives -p  $meshObjects[0] `;
		string $shatterGroup[] =`listRelatives -p  $transformObject[0] `;
		string $transformNodes[] =`listRelatives -c -type transform $shatterGroup[0]`;	
		if( size($transformNodes) > 0)
		{
			select -clear;
		    clear( $meshObjects);
			
			for ( $obj in $transformNodes)
			{
				string $shapes[] =`listRelatives -c -s $obj`;
				if( size( $shapes) > 0)
				{		
					$meshObjects[size($meshObjects)] = $shapes[0];		
				}	
			}		
		}			    
	}
   //create pdi bodies for raw meshes
    clear($PdiSelectionForFracture);
    for ( $obj in $meshObjects)
	{
        if( `nodeType -api $obj`== "kMesh")
        {
            $PdiSelectionForFracture[size($PdiSelectionForFracture)] = $obj;
        }
    }

    string $result = `layoutDialog -t "Set up Fracture Body" -ui "createPdiSetUpFbodyWindow"`;

}

global proc deletePdiFbody()
{
    global string $pdiFBodiesList;
    global string $chkPdiEnabledFbody;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
		string $result = `confirmDialog -title "Confirm" -message "are you sure you want to delete this fbody?"
		-button "Yes" -button "No" -defaultButton "Yes"
		-cancelButton "No" -dismissString "No"`;

		if( $result == "No")
		{
			return;
		}
    
        textScrollList -edit -removeItem $selected[0] $pdiFBodiesList;
     
        waitCursor -state on;

        string $result = `delete_dFBody  $selected`;

        waitCursor -state off;
    }    
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiEnabledFbody;
    }    
}
global proc deleteAllPdiFbodies()
{
    global string $pdiFBodiesList;

    string $PDIFbodies[];
    $PDIFbodies = `textScrollList -query -allItems $pdiFBodiesList`;

    if( size( $PDIFbodies) > 0)
    {
		string $result = `confirmDialog -title "Confirm" -message "are you sure you want to delete all PDI Fbodies in scene?"
		-button "Yes" -button "No" -defaultButton "Yes"
		-cancelButton "No" -dismissString "No"`;

		if( $result == "No")
		{
			return;
		}

        textScrollList -edit -removeAll $pdiFBodiesList;

        waitCursor -state on;

        for( $obj in $PDIFbodies )
        {
            string $result = `delete_dFBody $obj`;
        }

        waitCursor -state off;

    }    
}
global proc setStatePdiFBody()
{
    global string $pdiFBodiesList;
    global string $chkPdiEnabledFbody;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
	    int $bEnabled = `checkBox -q -value $chkPdiEnabledFbody`;	

        if( $bEnabled > 0)
        {
    	    setAttr ($selected[0] + ".enabled" ) false;
        }
        else
        {
    	    setAttr ($selected[0] + ".enabled" ) true;
        }
         //force update
	     getAttr ($selected[0] + ".ca_enabled" );
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiEnabledFbody;
    }
}
global proc setStatePdiFBodyTorsion()
{
    global string $pdiFBodiesList;
    global string $chkPdiEnabledTorsion;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
	    int $value = `checkBox -q -value $chkPdiEnabledTorsion`;	

        if( $value > 0)
        {
    	    setAttr ($selected[0] + ".torsion" ) true;
        }
        else
        {
    	    setAttr ($selected[0] + ".torsion" ) false;
        }
         //force update
	     getAttr ($selected[0] + ".ca_torsion" );
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiEnabledTorsion;
    }

}
global proc setPptyPdiBreakTreshold()
{
    global string $pdiFBodiesList;
    global string $fieldPdiBreakThr;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
	    float $breakThr = `floatSliderGrp -q -value $fieldPdiBreakThr`;	

         waitCursor -state on;

    	 setAttr ($selected[0] + ".stressThreshold" ) $breakThr;
         setAttr ($selected[0] + ".usercommand" ) true;
         //force update
	     getAttr ($selected[0] + ".ca_stressThreshold" );


         waitCursor -state off;

    }
    else
    {
        ///warning: this make maya hold when clicking on the slider 
        //confirmDialog
        //-title "Pdi Info"
        //-message "Select a fracture body from the list"
        //-button "OK" 
        //-defaultButton "OK" ;

        floatSliderGrp -edit -value 0.0 $fieldPdiBreakThr;
    }

}
global proc setPptyPdiClusterize()
{
    global string $pdiFBodiesList;
    global string $fieldPdiClusterize;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
	    int $value = `intSliderGrp -q -value $fieldPdiClusterize`;	

         waitCursor -state on;

    	 setAttr ($selected[0] + ".randomThreshold" ) $value;
         setAttr ($selected[0] + ".usercommand" ) true;
         //force update
	     getAttr ($selected[0] + ".ca_randomThreshold" );


         waitCursor -state off;
    }
    else
    {
        ///warning: this make maya hold when clicking on the slider 
        //confirmDialog
        //-title "Pdi Info"
        //-message "Select a fracture body from the list"
        //-button "OK" 
        //-defaultButton "OK" ;

        intSliderGrp -edit -value 0 $fieldPdiClusterize;
    }

}
global proc setPptyPdiFMass()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFMass;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        float $mass = `floatFieldGrp -query -value1 $fieldPdiFMass`;

        setAttr ($selected[0] + ".mass" ) $mass;
         //force update
	    getAttr ($selected[0] + ".ca_mass" );

    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        floatFieldGrp -edit -value 1 $fieldPdiFMass;
    }
}

global proc setPptyPdiFBFriction()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFBFriction;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        float $friction = `floatSliderGrp -query -value $fieldPdiFBFriction`;

        setAttr ($selected[0] + ".friction" ) $friction;
         //force update
	    getAttr ($selected[0] + ".ca_friction" );

    }
    else
    {
        floatSliderGrp -edit -value 0 $fieldPdiFBFriction;
    }

}

global proc setPptyPdiFBounciness()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFBounciness;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        float $bounciness = `floatSliderGrp -query -value $fieldPdiFBounciness`;

        setAttr ($selected[0] + ".bounciness" ) $bounciness;
         //force update
	    getAttr ($selected[0] + ".ca_bounciness" );

    }
    else
    {
        floatSliderGrp -edit -value 0 $fieldPdiFBounciness;
    }
}
global proc setPptyPdiFAirdamping()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFairdamping;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        float $airDamping = `floatSliderGrp -query -value $fieldPdiFairdamping`;

        setAttr ($selected[0] + ".linearDamping" ) $airDamping;
         //force update
	    getAttr ($selected[0] + ".ca_airdamping" );

    }
    else
    {
        floatSliderGrp -edit -value 0 $fieldPdiFairdamping;
    }
}
global proc setPptyPdiFAngulardamping()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFairdamping2;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        float $angularDamping = `floatSliderGrp -query -value $fieldPdiFairdamping2`;

        setAttr ($selected[0] + ".angularDamping" ) $angularDamping;
         //force update
	    getAttr ($selected[0] + ".ca_angdamping" );

    }
    else
    {
        floatSliderGrp -edit -value 0 $fieldPdiFairdamping2;
    }
}


global proc setPptyFVelocity()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFVelocity;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        float $vx = `floatFieldGrp -query -value1 $fieldPdiFVelocity`;
        float $vy = `floatFieldGrp -query -value2 $fieldPdiFVelocity`;
        float $vz = `floatFieldGrp -query -value3 $fieldPdiFVelocity`;

        setAttr ($selected[0] + ".initialVelocity" ) -type "float3" $vx $vy $vz;
         //force update
	    getAttr ($selected[0] + ".ca_initialVelocity" );

    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        floatFieldGrp -edit -value1 0.0 $fieldPdiFVelocity;
        floatFieldGrp -edit -value2 0.0 $fieldPdiFVelocity;
        floatFieldGrp -edit -value3 0.0 $fieldPdiFVelocity;
    }
}
global proc setPptyFWelocity()
{
    global string $pdiFBodiesList;
    global string $fieldPdiFWelocity;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        float $vx = `floatFieldGrp -query -value1 $fieldPdiFWelocity`;
        float $vy = `floatFieldGrp -query -value2 $fieldPdiFWelocity`;
        float $vz = `floatFieldGrp -query -value3 $fieldPdiFWelocity`;

        setAttr ($selected[0] + ".initialSpin" ) -type "float3" $vx $vy $vz;
         //force update
	    getAttr ($selected[0] + ".ca_initialSpin" );

    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        floatFieldGrp -edit -value1 0.0 $fieldPdiFWelocity;
        floatFieldGrp -edit -value2 0.0 $fieldPdiFWelocity;
        floatFieldGrp -edit -value3 0.0 $fieldPdiFWelocity;
    }

}
global proc setStatePdiMayaFieldsFbody()
{
    global string $chkPdiMayaFieldsFbody;
    global string $pdiFBodiesList;
    global string $butPdiFbodyFieldList;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
	    int $value = `checkBox -q -value $chkPdiMayaFieldsFbody`;	

        if( $value > 0)
        {
    	    setAttr ($selected[0] + ".affectForceFields" ) true;
        }
        else
        {
    	    setAttr ($selected[0] + ".affectForceFields" ) false;
        }
         //force update
        getAttr ($selected[0] + ".ca_forceFields" );

        string  $forceFields[];
        if( $value > 0) 
        {
            $forceFields = `ls -tr -dag -leaf`;//warning: this includes all transform nodes in scene
        }
        pdiConnectForceFields -fbodyName $selected[0] -connect $forceFields ;

        button -e -enable $value $butPdiFbodyFieldList;
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiMayaFieldsFbody;
    }
}


global proc updateTransformFbdody()
{
    global string $pdiFBodiesList;
    global string $chkPdiMayaFieldsFbody;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
//	$selected = `ls -selection`;

    if( size( $selected) > 0)
    {
        update_dFBody  $selected[0];
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiMayaFieldsFbody;
    }
}
global proc setDisplayStresses()
{
    global string $chkPdiMayaFieldsFbody;
    global string $pdiFBodiesList;
    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        string $transformNodes[] = `queryPdiFBody -gtn $selected[0]`;
        select $transformNodes;//needed for polyoptions

        int $PdiDisplayStresses = getAttr ($selected[0] + ".displayStressses" );

        if( $PdiDisplayStresses==0)
        {
            setAttr ($selected[0] + ".displayStressses" ) 1;
            //force update
            getAttr ($selected[0] + ".cdst" );

			update_dFBody -map $selected[0];
			
            polyOptions -cs true;

        }
        else
        {
            setAttr ($selected[0] + ".displayStressses" ) 0;

            //force update
            getAttr ($selected[0] + ".cdst" );

            polyOptions -cs false;
        }   

		select -clear;

        clear( $transformNodes);
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiMayaFieldsFbody;
    }

}

global proc pdiExcludeFbodyField()
{
}
global proc pdiIncludeFbodyField()
{
}
global proc pdiAcceptFbodyFields()
{
    global string $pdiFBodiesList;
    global string $pdiIncludedFbodyFieldsList;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        string  $forceFields[];
        $forceFields = `textScrollList -query -allItems $pdiIncludedFbodyFieldsList`;

        pdiConnectForceFields -fbodyName $selected[0] -connect $forceFields ;
    }
    layoutDialog -dismiss "OK";
}
global proc createPdiIncludeExcludeFbodyFieldsWin()
{
}
global proc pdiExcludeIncludeFbodyFields()
{
}
global proc updatePdiPropStyle()
{
    global string $pdiPropStyleCollection;
   	global string $pdiPS[2];
    global string $pdiFBodiesList;
    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        if( `radioButton -query -select $pdiPS[1]`)
        {
            setAttr ($selected[0] + ".propStyle" ) 1;
        }
        else
        {
            setAttr ($selected[0] + ".propStyle" ) 0;
        }
         setAttr ($selected[0] + ".usercommand" ) true;

        //force update
        getAttr ($selected[0] + ".caps" );
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;
    }
}

global proc setStatePdiFBodyStatic()
{
    global string $pdiFBodiesList;
    global string $chkPdiStaticFbody;     
   	global string $framePdiFactivation; 
   	global string $pdiFactivationCollection;
   	global string $pdiFA[2];

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
	    $state = `checkBox -q -value $chkPdiStaticFbody`;	

		frameLayout -edit -enable $state $framePdiFactivation;

		if( $state == true)
        {
    	    setAttr ($selected[0] + ".passive" ) true;  
    		setAttr ($selected[0] + ".firstHitActive" ) 1;
			radioCollection -edit -select $pdiFA[1] $pdiFactivationCollection;	    		
        }
        else
        {
    	    setAttr ($selected[0] + ".passive" ) false;
    	    setAttr ($selected[0] + ".firstHitActive" ) 0;
			radioCollection -edit -select $pdiFA[0] $pdiFactivationCollection;	    		
        }
         //force update
	     getAttr ($selected[0] + ".ca_passive" );		  
	     getAttr ($selected[0] + ".ca_firstHit" );
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;

        checkBox -edit -value 0 $chkPdiStaticFbody;       
    }
}
global proc setStatePdiFBodyFirstHit()
{
    global string $pdiFBodiesList;
    global string $chkPdiStaticFbody;  
   	global string $pdiFA[2];
   	global string $fieldPdiFAFrame;
   	       
    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
        if( `radioButton -query -select $pdiFA[1]`)
        {
    	    setAttr ($selected[0] + ".firstHitActive" ) 1;
		    intField -edit -enable false $fieldPdiFAFrame;	    	    
        }
        else
        if( `radioButton -query -select $pdiFA[2]`)
        {
    	    setAttr ($selected[0] + ".firstHitActive" ) 2;
		    intField -edit -enable true $fieldPdiFAFrame;	    	    
        }
        else
        {
    	    setAttr ($selected[0] + ".firstHitActive" ) 0;
		    intField -edit -enable false $fieldPdiFAFrame;	    	    
        }
         //force update
	     getAttr ($selected[0] + ".ca_firstHit" );
    }
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;
    }

}
global proc updatePdiFbodyParams()
{
    global string $pdiBasicFracturesWindow;
    global string $pdiFBodiesList;
    global string $fieldPdiBreakThr;
    global string $fieldPdiClusterize;
    global string $chkPdiEnabledFbody;
    global string $chkPdiEnabledTorsion;
    global string $chkPdiSet2Mass;    
    global string $fieldPdiFMass;
    global string $fieldPdiFBFriction;
    global string $fieldPdiFBounciness;
    global string $fieldPdiFairdamping;    
    global string $fieldPdiFairdamping2;    
    global string $fieldPdiFVelocity;
    global string $fieldPdiFWelocity;
    global string $chkPdiMayaFieldsFbody;
    global string $butPdiFbodyFieldList;
    global int $pdiUpdate;
    global string $selectedPdiFbody;
    global string $pdiPropStyleCollection;
   	global string $pdiPS[2];
    global string $chkPdiStaticFbody;     
   	global string $framePdiFactivation;  
   	global string $pdiFactivationCollection;  	
   	global string $pdiFA[2];
   	global string $fieldPdiFAFrame;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;

    if( size( $selected) > 0)
    {
        $selectedPdiFbody = $selected[0];

        if( `window -ex winPdiAvFractures`)
        {
            updatePdiAdvancedFracturesPanel();
        }

        $pdiUpdate = 0;//enable update			

	    float $breakThr = getAttr ($selected[0] + ".stressThreshold" );
	    float $fmass = getAttr ($selected[0] + ".mass" );
	    float $friction = getAttr ($selected[0] + ".friction" );
	    float $bounciness = getAttr ($selected[0] + ".bounciness" );
	    float $airdamping = getAttr ($selected[0] + ".linearDamping" );
	    float $angdamping = getAttr ($selected[0] + ".angularDamping" );
	    int $clusterize = getAttr ($selected[0] + ".randomThreshold" );
	    int $bEnabled = getAttr ($selected[0] + ".enabled" );
	    int $isFirstHit = getAttr ($selected[0] + ".firstHitActive" );
	    int $isStatic = getAttr ($selected[0] + ".passive" );
	    int $bEnabledTor = getAttr ($selected[0] + ".torsion" );
	    int $bEnabledS2M = getAttr ($selected[0] + ".stress2mass" );
        vector $velocity = getAttr ($selected[0] + ".initialVelocity" );
        vector $welocity = getAttr ($selected[0] + ".initialSpin" );
	    int $bForceFields = getAttr ($selected[0] + ".affectForceFields" );
	    int $propStyle = getAttr ($selected[0] + ".propStyle" );
        int $bDoTrans = getAttr ($selected[0] + ".doTrans" );
        int $actFrame = getAttr ($selected[0] + ".actFrame" );  
        int $transFrame = getAttr ($selected[0] + ".transFrame" );  

		frameLayout -edit -enable $isStatic $framePdiFactivation;
		if( `radioCollection -ex $pdiPropStyleCollection` && $propStyle >= 0 && $propStyle < 2)
		{
			radioCollection -edit -select $pdiPS[$propStyle] $pdiPropStyleCollection;	
        }
        floatSliderGrp -edit -value $breakThr $fieldPdiBreakThr;
        floatSliderGrp -edit -value $friction $fieldPdiFBFriction;
        floatSliderGrp -edit -value $bounciness $fieldPdiFBounciness;
        floatSliderGrp -edit -value $airdamping $fieldPdiFairdamping;        
        floatSliderGrp -edit -value $angdamping $fieldPdiFairdamping2;        
        floatFieldGrp -edit -value1 $fmass $fieldPdiFMass;
        intSliderGrp -edit -value $clusterize $fieldPdiClusterize;
	    checkBox -edit -value $bEnabledTor $chkPdiEnabledTorsion;
	    checkBox -edit -value $bEnabledS2M $chkPdiSet2Mass;
		radioCollection -edit -select $pdiFA[$isFirstHit] $pdiFactivationCollection;	
	    checkBox -edit -value $isStatic $chkPdiStaticFbody;	
	    intField -edit -value $actFrame $fieldPdiFAFrame;	
	    if( $isFirstHit==2)
		{
		    intField -edit -enable true $fieldPdiFAFrame;	
		}		
		else
		{
		    intField -edit -enable false $fieldPdiFAFrame;	
		}		
	        
        if( $bEnabled > 0)
        {
	        checkBox -edit -value false $chkPdiEnabledFbody;	
        }
        else
        {
	        checkBox -edit -value true $chkPdiEnabledFbody;	
        }

	    checkBox -edit -value $bForceFields $chkPdiMayaFieldsFbody;

        floatFieldGrp  -edit
            -value1 ($velocity.x)
            -value2 ($velocity.y)
            -value3 ($velocity.z)
            $fieldPdiFVelocity;

        floatFieldGrp  -edit
            -value1 ($welocity.x)
            -value2 ($welocity.y)
            -value3 ($welocity.z)
            $fieldPdiFWelocity;

//        select $selected[0];
//		print("fbody " + $selected[0] + " selected\n");			


        $pdiUpdate = 1;//enable update			

        string $allFbodies[];
        $allFbodies = `textScrollList -query -allItems $pdiFBodiesList`;

        for( $obj in $allFbodies)
        {
            setAttr( $obj + ".selected" ) false;
        }
        setAttr( $selected[0] + ".selected" ) true;

        //force update
	    getAttr ($selected[0] + ".ca_selected" );

        //select in maya panels
        //select $selected[0];

        refresh -force;
    }

}
global proc updatePdiFactivation()
{
	setStatePdiFBodyFirstHit();
}
global proc updatePdiFAframe()
{
    global string $pdiFBodiesList;
   	global string $fieldPdiFAFrame;

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiFBodiesList`;
    if( size( $selected) > 0)
    {
		int $actFrame = `intField -q -value $fieldPdiFAFrame`;	
    
		setAttr ($selected[0] + ".actFrame" ) $actFrame;
	}
    else
    {
        confirmDialog
        -title "Pdi Info"
        -message "Select a fracture body from the list"
        -button "OK" 
        -defaultButton "OK" ;
    }	
}

global proc updatePdiBasicFracturesPanel()
{
    global string $pdiBasicFracturesWindow;
    global string $pdiFBodiesList;
    global string $selectedPdiFbody;

    int $bWExist = `window -ex winPdiBasicFractures`;
    int $bLstExist  = `textScrollList -ex $pdiFBodiesList`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bLstExist == false)
    {
        return;
    }
	string  $PDIFbodies[];
	$PDIFbodies = `ls -exactType fBodyNode`;

	if(size($PDIFbodies) != 0)
	{
		$pdiUpdate = 0;//disable update		

        textScrollList -edit -removeAll $pdiFBodiesList;

    	for ( $obj in $PDIFbodies )
	    {
            textScrollList -edit -append $obj $pdiFBodiesList;
        }
    }


    showWindow $pdiBasicFracturesWindow;
}

global proc createPdiBasicFracturesPanel()
{
    global string $pdiBasicFracturesWindow;
    global string $pdiFBodiesList;
    global string $chkPdiEnabledFbody;
    global string $chkPdiEnabledTorsion;
    global string $chkPdiStaticFbody;    
    global string $fieldPdiBreakThr;
    global string $fieldPdiClusterize;
    global string $chkPdiSet2Mass;
    global string $fieldPdiFMass;
    global string $fieldPdiFBFriction;
    global string $fieldPdiFBounciness;
    global string $fieldPdiFairdamping;
    global string $fieldPdiFairdamping2;
    global string $fieldPdiFVelocity;
    global string $fieldPdiFWelocity;
    global string $chkPdiMayaFieldsFbody;
    global string $butPdiFbodyFieldList;
    global string $bttPdiUpdateFbody;
    global string $framePdiPropStyle;
    global string $pdiPropStyleCollection;
   	global string $pdiPS[2];
   	global string $framePdiFactivation; 
   	global string $pdiFactivationCollection;  	
   	global string $pdiFA[2];
   	global string $fieldPdiFAFrame;
    global int $pdiUpdate;

    //check if the window exists
    if ( `window -ex winPdiBasicFractures` )
    {                
        updatePdiBasicFracturesPanel();
        
        return;
    }
    $pdiUpdate = 1;//enable update			

	$pdiBasicFracturesWindow = `window -rtf true -t "Pdi Basic Fractures" -width 150 -height 210 winPdiBasicFractures`;
    string $form = `formLayout -numberOfDivisions 200`;
    $pdiLabelFbodies = `text -w 200 -font "boldLabelFont" -label "Fracture Bodies"`;
    $pdiFBodiesList = `textScrollList -w 100 -numberOfRows 5 -allowMultiSelection false -selectCommand "updatePdiFbodyParams"` ;
    string $butCreate = `button  -w 120 -label "Create"
    -ann " Create a fracture body from selected"
     -command "createPdiFbody"`;
    string $butDelete =`button  -w 120 -label "Delete"
    -ann " Delete selected fracture body"    
     -command "deletePdiFbody"`;
    string $butDeleteAll =`button  -w 120 -label "Delete All"
    -ann " Delete all fracture bodies in scene"        
     -command "deleteAllPdiFbodies"`;
    $chkPdiEnabledFbody = `checkBox -label "Disabled"  -enable 1
    -ann " enable/disable selected fracture body from computing"        
     -changeCommand "setStatePdiFBody"`;
    $chkPdiEnabledTorsion = `checkBox -label "Torsion" -enable 1
    -ann " enable/disable torsion forces affecting selected fracture body"            
     -changeCommand "setStatePdiFBodyTorsion"`;
    $chkPdiStaticFbody = `checkBox -label "Static" -enable 1
    -ann "static fracture bodies can break but doesnt move"        
     -changeCommand "setStatePdiFBodyStatic"`;

    $framePdiFactivation = `frameLayout -width 100 -height 50 -label "Activation" -borderStyle "etchedOut"`;
		rowLayout -columnWidth4 130 90 80 60 -numberOfColumns 4;
			$pdiFactivationCollection=`radioCollection`;
			$pdiFA[0]=`radioButton -label "Breaks upon impact"  -ann "object doenst move but breaks upon impact" -onc "updatePdiFactivation"`;
			$pdiFA[1]=`radioButton -label "At first hit" -select -ann " becomes dynamics at first collision with other object" -onc "updatePdiFactivation"`;
			$pdiFA[2]=`radioButton -label "At frame" -ann " becomes dynamics at specified frame" -onc "updatePdiFactivation"`;
			$fieldPdiFAFrame = `intField -w 50 -value 1
				-ann " set activation frame" -changeCommand "updatePdiFAframe"`;  
			setParent..;
        setParent..;
     
    $fieldPdiBreakThr = `floatSliderGrp -label "Hardness" -field true
	        -minValue 0.0 -maxValue 1000.0 -fieldMaxValue 1000000.0
	        -ann "set overall brittleness for the object"
            -changeCommand "setPptyPdiBreakTreshold"` ;
    $fieldPdiClusterize = `intSliderGrp -label "Clusterize(%)" -field true
	        -minValue 0 -maxValue 100 
	        -ann "forces object to break in clusters of fragments"
            -changeCommand "setPptyPdiClusterize"` ;
                  
    $chkPdiSet2Mass = `checkBox -label "relative to mass" -enable true
	-ann "enable/disable stresses values relative to fragments mass"            	        	        	        	        	            	            
     -changeCommand "SetPdiStresses2Mass"`;	              
	      
	////////////dynamic properties	      
    $fieldPdiFMass = `floatFieldGrp -numberOfFields 1 -label "Mass" 
        -value1 1.0  -pre 3
	    -ann " set mass for the fracture body"            
        -changeCommand "setPptyPdiFMass"` ;
    $fieldPdiFBFriction = `floatSliderGrp -label "Friction" -field true
        -minValue 0.0 -maxValue 1.0 -v 0.7 -pre 3
	    -ann " set friction coefficient for the fracture body"                    
        -changeCommand "setPptyPdiFBFriction"` ;
    $fieldPdiFBounciness = `floatSliderGrp -label "Bounciness" -field true
        -minValue 0.0 -maxValue 1.0 -v 0.3 -pre 3
	    -ann " set bounciness for the fracture body"                    
        -changeCommand "setPptyPdiFBounciness"` ;
    $fieldPdiFairdamping = `floatSliderGrp -label "Linear Damping" -field true
        -minValue 0.0 -maxValue 1.0 -v 0.01 -pre 3
        -ann "set linear damping value"            	        
        -changeCommand "setPptyPdiFAirdamping"` ;
    $fieldPdiFairdamping2 = `floatSliderGrp -label "Angular Damping" -field true
        -minValue 0.0 -maxValue 1.0 -v 0.01 -pre 3
        -ann "set angular damping value"            	        
        -changeCommand "setPptyPdiFAngulardamping"` ;        
    $fieldPdiFVelocity = `floatFieldGrp -numberOfFields 3 -label "Initial Velocity" 
	            -value1 0.0 -value2 0.0 -value3 0.0 
				-ann " set initial linear velocity for the fracture body"                    	             
                -changeCommand "setPptyFVelocity"` ;
    $fieldPdiFWelocity = `floatFieldGrp -numberOfFields 3 -label "Initial Spin" 
	            -value1 0.0 -value2 0.0 -value3 0.0  
				-ann " set initial angular velocity for the fracture body"                    	             	            
                -changeCommand "setPptyFWelocity"` ;
    $chkPdiMayaFieldsFbody = `checkBox -label "affected by Force Fields" -enable 1
	-ann "enable/disable force fields affecting the motion of the fracture body"            	        	        	        	        	            	            
     -changeCommand "setStatePdiMayaFieldsFbody"`;	              
    $butPdiFbodyFieldList =`button  -enable false -w 10 -label "Include/Exclude"
	-ann "select the fields affecting the motion of the fracture body"            	        	        	        	        	            	                        
     -command "pdiExcludeIncludeFbodyFields"`;
    string $butDisplayStresses =`button  -w 250 -h 40 -label "Switch On/Off stresses view"
	-ann "turn on stresses view to check distribution of brittleness"            	        	        	        	        	            	                            
     -command "setDisplayStresses"`;
    $bttPdiUpdateFbody = `button  -w 250 -h 40 -label "Update Transform"
	-ann " update transform after rotating/scaling/trasnlating fracture body in viewport"            	        	        	        	        	            	                                
     -command "updateTransformFbdody"`;
     
    $framePdiPropStyle = `frameLayout -width 100 -height 50 -label "Propagation scheme" -borderStyle "etchedOut"`;
    rowLayout -columnWidth2 100 100 -numberOfColumns 2;
        $pdiPropStyleCollection=`radioCollection`;
        $pdiPS[0]=`radioButton -label "Continuous" -select -ann "cracks propagate across the object" -onc "updatePdiPropStyle"`;
        $pdiPS[1]=`radioButton -label "Local" -ann "break only on colision areas" -onc "updatePdiPropStyle"`;
        setParent..;
        setParent..;
    
    formLayout -edit
        -attachForm     $pdiLabelFbodies      "top"    0
        -attachForm     $pdiLabelFbodies      "left"    0
        -attachControl     $pdiFBodiesList    "top"   10 $pdiLabelFbodies
        -attachForm     $pdiFBodiesList      "left"    5
        -attachForm     $pdiFBodiesList      "right" 5
        -attachControl  $pdiFBodiesList      "bottom" 0 $butCreate
        -attachForm     $butCreate      "top"    100
        -attachForm     $butCreate      "left"   5
        -attachControl      $butDelete      "top"    0 $pdiFBodiesList
        -attachControl     $butDelete     "left" 0 $butCreate
        -attachControl      $butDeleteAll      "top"    0 $pdiFBodiesList
        -attachControl     $butDeleteAll     "left" 0 $butDelete      
        -attachForm        $butDeleteAll     "right" 5  
        -attachForm     $butDisplayStresses      "left" 5   
        -attachForm     $butDisplayStresses      "right" 5   
        -attachControl  $butDisplayStresses    "top" 15 $butDeleteAll            
        -attachControl  $chkPdiStaticFbody     "top" 15 $butDisplayStresses
        -attachForm		$chkPdiStaticFbody      "left" 30 
        -attachControl  $chkPdiEnabledFbody      "top" 15 $butDisplayStresses
        -attachControl	$chkPdiEnabledFbody      "left" 10  $chkPdiStaticFbody 
        -attachControl  $chkPdiEnabledTorsion    "top" 15 $butDisplayStresses
        -attachControl  $chkPdiEnabledTorsion    "left" 10  $chkPdiEnabledFbody
        -attachForm   $framePdiFactivation      "left" 5   
        -attachForm   $framePdiFactivation     "right" 5   
        -attachControl  $framePdiFactivation    "top" 5 $chkPdiStaticFbody        
        -attachControl  $fieldPdiBreakThr     "top" 10 $framePdiFactivation
        -attachForm     $fieldPdiBreakThr       "left" 0 
        -attachControl  $fieldPdiClusterize     "top" 10 $fieldPdiBreakThr
        -attachForm     $fieldPdiClusterize       "left" 0 
        -attachForm		$chkPdiSet2Mass			"left" 150
        -attachControl  $chkPdiSet2Mass			"top" 5 $fieldPdiClusterize
        -attachForm   $framePdiPropStyle      "left" 5   
        -attachForm   $framePdiPropStyle      "right" 5   
        -attachControl  $framePdiPropStyle    "top" 5 $chkPdiSet2Mass
        -attachControl     $fieldPdiFMass     "top" 20 $framePdiPropStyle        
        -attachForm  $fieldPdiFMass       "left" 0 
        -attachControl     $fieldPdiFBFriction     "top" 10 $fieldPdiFMass
        -attachForm  $fieldPdiFBFriction       "left" 0 
        -attachControl     $fieldPdiFBounciness     "top" 10 $fieldPdiFBFriction
        -attachForm  $fieldPdiFBounciness       "left" 0      
        -attachControl     $fieldPdiFairdamping     "top" 10 $fieldPdiFBounciness
        -attachForm  $fieldPdiFairdamping       "left" 0              
        -attachControl     $fieldPdiFairdamping2     "top" 10 $fieldPdiFairdamping 
        -attachForm  $fieldPdiFairdamping2       "left" 0              
        -attachControl      $fieldPdiFVelocity     "top" 10 $fieldPdiFairdamping2
        -attachForm   $fieldPdiFVelocity        "left" 0 
        -attachControl      $fieldPdiFWelocity     "top" 10 $fieldPdiFVelocity
        -attachForm   $fieldPdiFWelocity        "left" 0     
        -attachControl  $chkPdiMayaFieldsFbody  "top" 10 $fieldPdiFWelocity
        -attachForm   $chkPdiMayaFieldsFbody   "left" 150   
        -attachControl  $bttPdiUpdateFbody  "top" 10  $chkPdiMayaFieldsFbody
        -attachForm   $butPdiFbodyFieldList   "right" 5  
        -attachForm   $butPdiFbodyFieldList   "left" 5  
        -attachControl   $butPdiFbodyFieldList   "top" 10 $chkPdiMayaFieldsFbody  
        -attachForm   $bttPdiUpdateFbody   "left" 0                 
        -attachForm   $bttPdiUpdateFbody   "right" 0                         
        -attachControl  $bttPdiUpdateFbody    "top" 5 $butPdiFbodyFieldList

    $form;

    updatePdiBasicFracturesPanel();

}
/////////////////////////////////////////////////
//  
//  Advanced Fractures Panel
//
/////////////////////////////////////////////////
global proc SetPdiStresses2Mass()
{
    global string $chkPdiSet2Mass;
    global string $selectedPdiFbody;

        string $result[];
        string $selected[1];
        $selected[0]=$selectedPdiFbody;        
//        print " SetStresses2Mass\n";
	    int $state = `checkBox -q -value $chkPdiSet2Mass`;	
		if( $state == true)
        {
    	    setAttr ($selected[0] + ".stress2mass" ) true;
//			$result = `fBody_selStresses -stm $selected`;    	    
        }
        else
        {
    	    setAttr ($selected[0] + ".stress2mass" ) false;
//			$result = `fBody_selStresses -ra $selected`;    	    
        }
         //force update
	     getAttr ($selected[0] + ".ca_stress2mass" );	    
}
global proc resetAllStresses()
{
    global string $selectedPdiFbody;

    string $result = `confirmDialog -title "Confirm" -message " this action will reset all stresses to default, are you sure?"
    -button "Yes" -button "No" -defaultButton "Yes"
    -cancelButton "No" -dismissString "No"`;

    if( $result == "Yes")
    {
        string $selection[1];
        $selection[0]=$selectedPdiFbody;

        string $result[];
        $result = `fBody_selStresses -resetAll $selection`;
    }

}
global proc setPptyPdiBreakEnergyStress()
{
    global string $pdiStressesList;
    global string $fieldPdiBreakEnergy;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `floatSliderGrp -q -value $fieldPdiBreakEnergy`;	

        for( $obj in $selected)
        {
             setAttr( $obj + ".breakEnergy" ) $value;
             //force update
	         getAttr ($obj + ".ca_breakEnergy" );
        }
    }


}
global proc setPptyPdiBreakForceStress()
{
    global string $pdiStressesList;
    global string $fieldPdiBreakForce;
    global string $pdiCurrentSelectedFragments[];

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `floatSliderGrp -q -value $fieldPdiBreakForce`;	

        for( $obj in $selected)
        {
             setAttr( $obj + ".forceStrenght" ) $value;
             //force update
	         getAttr ($obj + ".ca_forceStrenght" );
        }
    }

    //remove select color for last selected
    for ( $obj in $pdiCurrentSelectedFragments )
    {
	    setAttr ($obj + ".selected" ) 0;
        //force update 
        getAttr ($obj + ".ca_selected" );            
    }

    clear($pdiCurrentSelectedFragments);

}
global proc setBreakableByForcePdiStress()
{
    global string $pdiStressesList;
    global string $chkPdiBreakableForce;
    global string $fieldPdiBreakForce;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `checkBox -q -value $chkPdiBreakableForce`;	

        for( $obj in $selected)
        {

            if( $value > 0)
            {
    	        setAttr( $obj + ".breakByForce" ) true;
            }
            else
            {
    	        setAttr( $obj + ".breakByForce" ) false;
            }
             //force update
	         getAttr ($obj + ".ca_breakByForce" );
        }
		//update window
		if( $value > 0)
		{
			floatSliderGrp -edit -enable true $fieldPdiBreakForce;
		}
		else
		{
			floatSliderGrp -edit -enable false $fieldPdiBreakForce;
		}                        
    }

}
global proc setStressBreakFrame()
{
    global string $pdiStressesList;
    global string $fieldPdiBreakFrame;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `intSliderGrp -q -value $fieldPdiBreakFrame`;	

        for( $obj in $selected)
        {

             setAttr( $obj + ".frameToBreak" ) $value;
             //force update
	         getAttr ($obj + ".ca_frameToBreak" );
        }
    }

}
global proc setBreakableAtFramePdiStress()
{
    global string $pdiStressesList;
    global string $chkPdiBreakableAtFrame;
    global string $fieldPdiBreakFrame;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `checkBox -q -value $chkPdiBreakableAtFrame`;	

        for( $obj in $selected)
        {
            if( $value > 0)
            {
    	        setAttr( $obj + ".breakByFrame" ) true;
            }
            else
            {
    	        setAttr( $obj + ".breakByFrame" ) false;
            }
             //force update
	         getAttr ($obj + ".ca_breakByFrame" );
        }
        
        //update window
		if( $value > 0)
		{
			intSliderGrp -edit -enable true $fieldPdiBreakFrame;
		}
		else
		{
			intSliderGrp -edit -enable false $fieldPdiBreakFrame;
		}           
    }

}
global proc setStatePdiStressTorsion()
{
    global string $pdiStressesList;
    global string $chkPdiEnabledTorsionStress;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $value = `checkBox -q -value $chkPdiEnabledTorsionStress`;	

        for( $obj in $selected)
        {
            if( $value > 0)
            {
    	        setAttr( $obj + ".torsion" ) true;
            }
            else
            {
    	        setAttr( $obj + ".torsion" ) false;
            }
             //force update
	         getAttr ($obj + ".ca_torsion" );
        }
    }

}
global proc setStatePdiStress()
{
    global string $pdiStressesList;
    global string $chkPdiEnabledStress;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    string $selected[];
    $selected = `textScrollList -query -selectItem $pdiStressesList`;

    if( size( $selected) > 0)
    {
        int $bEnabled = `checkBox -q -value $chkPdiEnabledStress`;	

        for( $obj in $selected)
        {
            if( $bEnabled > 0)
            {
    	        setAttr( $obj + ".enabled" ) true;
            }
            else
            {
    	        setAttr( $obj + ".enabled" ) false;
            }
             //force update
	         getAttr ($obj + ".ca_enabled" );
        }
    }
}

global proc updateSelectedStresses()
{
    global string $SelectCollection;
   	global string $strType[2];
    global string $pdiFragmentsList;
    global string $pdiStressesList;

    global int $pdiUpdate;
	if( $pdiUpdate==0)
	{
		return;	
	}	

    $pdiUpdate = 0;
//    print("update selected" + "\n");

    textScrollList -edit -removeAll $pdiStressesList;

    string $selection[];
    $selection = `textScrollList -query -selectItem $pdiFragmentsList`;

    if( size( $selection ) > 0)
    {
        int $type = 0;
        if( size( $selection ) == 1)
        {    
//            print " selected one item\n";
            $type = 1;
            if( `radioButton -query -select $strType[0]`)
            {                
                radioCollection -edit -select $strType[1] $SelectCollection;
            }
        }
        else
        {
            if( `radioButton -query -select $strType[0]`)
            {
                $type = 0;
//                print "type 0\n";
            }
            else
            {
                $type = 1;
//                print "type 1\n";
            }
        }

        waitCursor -state on;

        string $result[];
        $result = `fBody_selStresses -type $type $selection`;

        if( size( $result) > 0)
        {
            for( $str in $result)
            {
               textScrollList -edit -append $str $pdiStressesList;
            }
        }

        waitCursor -state off;

    }

    $pdiUpdate = 1;

}

global proc detachPdiFragments()
{
    global string $pdiFragmentsList;

    string $selection[];
    $selection = `textScrollList -query -selectItem $pdiFragmentsList`;

	if( size($selection) == 0)
	{
	    $selection = `textScrollList -query -allItems $pdiFragmentsList`;	
	}

    waitCursor -state on;

    Detach_FBody $selection;

    for( $obj in $selection)
    {
        textScrollList -edit -ri $obj $pdiFragmentsList;
    }

    polyOptions -cs false;

    waitCursor -state off;

}
global proc selectAllFragments()
{
    global string $pdiFragmentsList;

    int $nItems = `textScrollList -q -ni $pdiFragmentsList`;

    int $i;
    for( $i=1; $i <= $nItems; $i++)
    {
        textScrollList -edit -sii $i $pdiFragmentsList;
    }
}
global proc InvertSelectedFragments()
{
    global string $pdiFragmentsList;
     //get number of selected items
    int $nItems = `textScrollList -q -nsi $pdiFragmentsList`;
    int $n_all_iTems = `textScrollList -q -ni $pdiFragmentsList`;
    if ($nItems == $n_all_iTems)
    {
        textScrollList -edit -da $pdiFragmentsList;
    }
    else
    {
       //get selected items
        string $sel_iTems_list[] = `textScrollList -q -si $pdiFragmentsList`;
       selectAllFragments();
       int $j;
       for( $j=0; $j < size($sel_iTems_list); $j++)
       {
            textScrollList -edit -di $sel_iTems_list[$j] $pdiFragmentsList;
        //print($sel_iTems_list[$j] + "\n");
       }
    }
}
global proc selectAllStresses()
{
    global string $pdiStressesList;

    int $nItems = `textScrollList -q -ni $pdiStressesList`;

    int $i;
    for( $i=1; $i <= $nItems; $i++)
    {
        textScrollList -edit -sii $i $pdiStressesList;
    }
}
global proc InvertSelectedStresses()
{
    global string $pdiStressesList;
     //get number of selected items
    int $nItems = `textScrollList -q -nsi $pdiStressesList`;
    int $n_all_iTems = `textScrollList -q -ni $pdiStressesList`;
    if ($nItems == $n_all_iTems)
    {
        textScrollList -edit -da $pdiStressesList;
    }
    else
    {
       //get selected items
        string $sel_iTems_list[] = `textScrollList -q -si $pdiStressesList`;
       selectAllStresses();
       int $j;
       for( $j=0; $j < size($sel_iTems_list); $j++)
       {
            textScrollList -edit -di $sel_iTems_list[$j] $pdiStressesList;
       }
    }
}
global proc selectAllStressesAction()
{
    selectAllStresses();
    updatePdiStresses();
}
global proc InvertSelectedStressesAction()
{
    InvertSelectedStresses();
    updatePdiStresses();
}

global proc selectAllFragmentsAction()
{
    selectAllFragments();
    updateSelectedStresses();
}
global proc InvertSelectedFragmentsAction()
{
    InvertSelectedFragments();
    updateSelectedStresses();
}
global proc updatePdiStresses()
{
    global string $pdiAdvancedFracturesWindow;
    global string $pdiStressesList;
    global string $chkPdiEnabledStress;
    global string $chkPdiEnabledTorsionStress;
    global string $chkPdiBreakableAtFrame;
    global string $fieldPdiBreakFrame;
    global string $chkPdiBreakableForce;
    global string $fieldPdiBreakForce;
    global string $fieldPdiBreakEnergy;

    int $bWExist = `window -ex winPdiAvFractures`;
    //int $bIconify = `window -query -iconify $pdiCreateBodyWindow`;
    int $bListExist  = `textScrollList -ex $pdiStressesList`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bListExist == false)
    {
//		print("pdiAdvancedFracturesWindow iconified, abort\n");			
        return;
    }

    string $sel_iTems_list[] = `textScrollList -q -si $pdiStressesList`;
    if( size( $sel_iTems_list) > 0)
    {
		$pdiUpdate = 0;//disable update			

        int $enabled = getAttr ($sel_iTems_list[0] + ".enabled" );
        int $torsion = getAttr ($sel_iTems_list[0] + ".torsion" );
        int $breakAtFrame = getAttr ($sel_iTems_list[0] + ".breakByFrame" );
        int $breakFrame = getAttr ($sel_iTems_list[0] + ".frameToBreak" );
        int $breakByForce = getAttr ($sel_iTems_list[0] + ".breakByForce" );
        float $breakForce = getAttr ($sel_iTems_list[0] + ".forceStrenght" );
        float $breakEnergy = getAttr ($sel_iTems_list[0] + ".breakEnergy" );

        checkBox -edit -value $enabled $chkPdiEnabledStress;
        checkBox -edit -value $torsion $chkPdiEnabledTorsionStress;
        checkBox -edit -value $breakAtFrame $chkPdiBreakableAtFrame;
        checkBox -edit -value $breakByForce $chkPdiBreakableForce;
	floatSliderGrp -edit -enable $breakByForce $fieldPdiBreakForce;
	intSliderGrp -edit -enable $breakAtFrame $fieldPdiBreakFrame;
        floatSliderGrp -edit -value $breakForce $fieldPdiBreakForce;
        intSliderGrp -edit -value $breakFrame $fieldPdiBreakFrame;        
        floatSliderGrp -edit -value $breakEnergy $fieldPdiBreakEnergy;

		$pdiUpdate = 1;//enable update			
    }

}
global proc updatePdiFragments()
{
    global string $pdiAdvancedFracturesWindow;
    global string $pdiFragmentsList;
    global string $pdiCurrentSelectedFragments[];
    global string $selectedPdiFbody;

    int $bWExist = `window -ex winPdiAvFractures`;
    //int $bIconify = `window -query -iconify $pdiCreateBodyWindow`;
    int $bListExist  = `textScrollList -ex $pdiFragmentsList`;//the window ex flag seem doesnt work
    if( $bWExist == false || $bListExist == false)
    {
//		print("pdiAdvancedFracturesWindow iconified, abort\n");			
        return;
    }
    
    //remove select color for last selected
    for ( $obj in $pdiCurrentSelectedFragments )
    {
	    setAttr ($obj + ".selected" ) 0;
        //force update 
        getAttr ($obj + ".ca_selected" );            
    }
    
    clear( $pdiCurrentSelectedFragments);

    //remove last selected
    textScrollList -edit -removeAll $pdiFragmentsList;

	string  $PDISelected[];
    $PDISelected = `ls -selection -dag -leaf -type pdiRigidBody`;

    if( size($PDISelected) > 0)
    {
//            textScrollList -edit -selectItem $PDISelected[0] $pdiBodiesList;
        for ( $obj in $PDISelected )
        {
//            $connections = `listConnections -d off -s on ($obj +".fracture")`;
    		string  $connections[];
	    	$connections = `listConnections -type fBodyNode $obj`;

            if( size($connections) > 0 && $connections[0]== $selectedPdiFbody)
            {
                textScrollList -edit -append $obj $pdiFragmentsList;

			    setAttr ($obj + ".selected" ) 1;
                //force update 
		        getAttr ($obj + ".ca_selected" );            

            }
//    		print("pdiFragmentsList updated\n");			
        }
        $pdiCurrentSelectedFragments = `textScrollList -query -allItems $pdiFragmentsList`;
    }

	setFocus MayaWindow;    
}
global proc updatePidSecCracks()
{
    global string $selectedPdiFbody;

    global string $pdiAdvancedFracturesWindow;
    global string $framePdiSecCracks;
    global string $chkPdiEnableSecCracks;
    global string $fieldPdiPercentSecCracks;

	int $bSecCracks = `checkBox -q -value $chkPdiEnableSecCracks`;	
    if( $bSecCracks > 0)
    {
	    setAttr ($selectedPdiFbody + ".secCracks" ) true;
    }
    else
    {
	    setAttr ($selectedPdiFbody + ".secCracks" ) false;
    }
	int $percentSecCracks = `intField -q -value $fieldPdiPercentSecCracks`;	

	setAttr ($selectedPdiFbody + ".percentSecCracks" ) $percentSecCracks;
    
    //force update
    getAttr ($selectedPdiFbody + ".ca_secCracks" );
    
}
global proc setStaticPdiFragments()
{
    global string $pdiFragmentsList;

    string $selection[];
    $selection = `textScrollList -query -selectItem $pdiFragmentsList`;
	if( size($selection) == 0)
	{
	    $selection = `textScrollList -query -allItems $pdiFragmentsList`;	
	}
    
    update_dFBody -staticFrags true $selection;		
}
global proc setDynamicPdiFragments()
{
    global string $pdiFragmentsList;

    string $selection[];
    $selection = `textScrollList -query -selectItem $pdiFragmentsList`;
	if( size($selection) == 0)
	{
	    $selection = `textScrollList -query -allItems $pdiFragmentsList`;	
	}
    
    update_dFBody -staticFrags false $selection;		
}
global proc updatePdiAdvancedFracturesPanel()
{
    global string $pdiAdvancedFracturesWindow;
    global string $selectedPdiFbody;
    global string $chkPdiEnabledFbody;
    global string $chkPdiEnableSecCracks;    
    global string $fieldPdiPercentSecCracks;    
    global string $namePdiFbodyAv;
    global string $pdiFragmentsList;
    global string $pdiStressesList;
    global string $chkPdiBreakableAtFrame; 
    global string $chkPdiBreakableForce;    
    global string $fieldPdiBreakForce;
    global string $fieldPdiBreakFrame;

    nameField -edit -object $selectedPdiFbody $namePdiFbodyAv;
   
    if( $selectedPdiFbody != "" && `objExists $selectedPdiFbody` )
    {
//		print $selectedPdiFbody;
		int $bSecCracks = getAttr ($selectedPdiFbody + ".secCracks" );

		checkBox -edit -value $bSecCracks $chkPdiEnableSecCracks;
		
		int $percentSecCracks = getAttr ($selectedPdiFbody + ".percentSecCracks" );
		
	    intField -edit -value $percentSecCracks $fieldPdiPercentSecCracks;			
    }			
    
    textScrollList -edit -removeAll $pdiFragmentsList;

    textScrollList -edit -removeAll $pdiStressesList;
        
    updatePdiFragments();

    //string  $connections[];
    //$connections = `listConnections -type pdiRigidBody $selectedPdiFbody`;

    //if( size($connections) > 0 )
    //{
    //    for ( $obj in $connections )
    //    {
    //      textScrollList -edit -append $obj $pdiFragmentsList;
    //    }
    //}
    
    if( `checkBox -query -value $chkPdiBreakableForce`)
    {
        floatSliderGrp -edit -enable true $fieldPdiBreakForce;
    }
    else
    {
        floatSliderGrp -edit -enable false $fieldPdiBreakForce;
    }
   
    if( `checkBox -query -value $chkPdiBreakableAtFrame`)
    {
        intSliderGrp -edit -enable true $fieldPdiBreakFrame;
    }
    else
    {
        intSliderGrp -edit -enable false $fieldPdiBreakFrame;
    }


    showWindow $pdiAdvancedFracturesWindow;
}

global proc createPdiAvFracturesPanel()
{
    global string $pdiAdvancedFracturesWindow;
    global string $framePdiSecCracks;
    global string $chkPdiEnableSecCracks;
    global string $fieldPdiPercentSecCracks;
    global string $pdiFragmentsList;
    global string $pdiStressesList;
    global int $pdiUpdate;
    global string $SelectCollection;
   	global string $strType[2];
    global string $strTypeCollection;
    global string $selectedPdiFbody;
    global string $namePdiFbodyAv;
    global string $chkPdiEnabledStress;
    global string $chkPdiEnabledTorsionStress;
    global string $chkPdiBreakableAtFrame;
    global string $fieldPdiBreakFrame;
    global string $chkPdiBreakableForce;
    global string $fieldPdiBreakForce;
    global string $fieldPdiBreakEnergy;
    global int $UpdatePdiFragmentsJobId;


//    setToolTo `selectPdiContext`; 
//    headsUpMessage "Pdi Interactive selection Tool has been activated";

    //check if the window exists
    if ( `window -ex winPdiAvFractures` )
    {                
        updatePdiAdvancedFracturesPanel();
        
        return;
    }
    $pdiUpdate = 1;//enable update			

	$pdiAdvancedFracturesWindow = `window -rtf true -t "Pdi Advanced Fractures" -width 200 -height 310 winPdiAvFractures`;
    string $form = `formLayout -numberOfDivisions 200`;
    $namePdiFbodyAv =`nameField -object $selectedPdiFbody`;
    $framePdiSecCracks = `frameLayout -width 100 -height 50 -label "Secundary Cracks" -borderStyle "etchedOut"`; 
        rowColumnLayout -numberOfRows 1 -co 1 "left" 14;
        $chkPdiEnableSecCracks = `checkBox -label "enabled" -align "left" -value true -enable 1 -changeCommand "updatePidSecCracks"`;	              
        text -label "% Volume"   -align "left";   
        $fieldPdiPercentSecCracks = `intField -w 30 -value 5 -changeCommand "updatePidSecCracks" -ann "min volume of the fragment to be cracked" `;            
	    setParent..;
    setParent..;
    string $butResetAll =`button  -w 250 -label "Reset all stresses to default" -command "resetAllStresses"`;
//    string $butSet2Mass =`button  -w 250 -label "Set stresses relative to mass" -command "SetPdiStresses2Mass"`;
    $pdiLabelFragments = `text -w 250 -font "smallBoldLabelFont" -label "Fragments( select from viewport)"`;
    $pdiFragmentsList = `textScrollList -w 100 -numberOfRows 6 -allowMultiSelection true -selectCommand "updateSelectedStresses"` ;
    string $butSelectAll = `button  -w 120 -label "Select All" -command "selectAllFragmentsAction"`;
    string $butSelect = `button  -w 120 -label "Invert Selection" -command "InvertSelectedFragmentsAction"`;
    string $butSetStatic =`button  -w 120 -label "Set Static" -command "setStaticPdiFragments"`;
    string $butSetDynamic =`button  -w 120 -label "Set Dynamic" -command "setDynamicPdiFragments"`;
    string $butDetach =`button  -w 120 -label "Detach" -command "detachPdiFragments"`;
	$SelectCollection = `radioCollection`;
	$strType[0] = `radioButton -label "Show only selection inner stresses" -onCommand "updateSelectedStresses"`;
	$strType[1] = `radioButton -label "Show only selection border stresses" -select -onCommand "updateSelectedStresses"`;
    $pdiLabelStresses = `text -w 100 -font "smallBoldLabelFont" -label "Stresses"`;
    $pdiStressesList = `textScrollList -w 100 -numberOfRows 8 -allowMultiSelection true -selectCommand "updatePdiStresses"` ;
    string $butSelectAllStresses = `button  -w 120 -label "Select All" -command "selectAllStressesAction"`;
    string $butSelectStresses = `button  -w 120 -label "Invert Selection" -command "InvertSelectedStressesAction"`;
    $chkPdiEnabledStress = `checkBox -label "enabled"  -enable 1 -changeCommand "setStatePdiStress"`;
    $chkPdiEnabledTorsionStress = `checkBox -label "Torsion" -enable 1 -changeCommand "setStatePdiStressTorsion"`;
    $chkPdiBreakableAtFrame = `checkBox -label "Breakable at Frame" -enable 1 -changeCommand "setBreakableAtFramePdiStress"`;
    $fieldPdiBreakFrame = `intSliderGrp -label "Break Frame" -field true
	        -minValue 0 -maxValue 1000 
            -changeCommand "setStressBreakFrame"` ;
    $chkPdiBreakableForce = `checkBox -label "Breakable by Force" -enable 1 -changeCommand "setBreakableByForcePdiStress"`;
    $fieldPdiBreakForce = `floatSliderGrp -label "Break Threshold" -field true
	        -minValue 0.0 -maxValue 1000000.0 
            -changeCommand "setPptyPdiBreakForceStress"` ;
    $fieldPdiBreakEnergy = `floatSliderGrp -label "Break Energy" -field true
	        -minValue 0.0 -maxValue 1000000.0 
            -changeCommand "setPptyPdiBreakEnergyStress"` ;
    formLayout -edit
        -attachForm     $namePdiFbodyAv         "top"    10   
        -attachForm     $namePdiFbodyAv            "left"    0
        -attachForm     $namePdiFbodyAv            "right"    0
        -attachForm     $framePdiSecCracks      "left" 5   
        -attachForm     $framePdiSecCracks      "right" 5   
        -attachControl  $framePdiSecCracks      "top" 10 $namePdiFbodyAv        
        -attachControl     $butResetAll            "top"    10 $framePdiSecCracks
        -attachForm     $butResetAll            "left"    5
        -attachForm     $butResetAll            "right"    5
        -attachControl     $pdiLabelFragments      "top"    10 $butResetAll 
        -attachForm     $pdiLabelFragments      "left"    0
        -attachControl     $pdiFragmentsList    "top"   10 $pdiLabelFragments
        -attachForm     $pdiFragmentsList      "left"    0
        -attachForm     $pdiFragmentsList      "right" 0
        -attachControl  $butSelectAll      "top"    0 $pdiFragmentsList
        -attachForm     $butSelectAll      "left"   0
        -attachControl      $butSelect      "top"    0 $pdiFragmentsList
        -attachControl     $butSelect     "left" 0 $butSelectAll
        -attachControl      $butDetach      "top"    0 $pdiFragmentsList
        -attachControl     $butDetach     "left" 0 $butSelect
        -attachForm       $butDetach     "right" 0 
        -attachControl      $butSetStatic      "top"    0 $butDetach
        -attachForm         $butSetStatic     "left" 0 
        -attachForm       $butSetStatic     "right" 0 
        -attachControl      $butSetDynamic      "top"    0 $butSetStatic
         -attachForm        $butSetDynamic     "left" 0 
        -attachForm       $butSetDynamic     "right" 0 
        -attachControl     $strType[0]     "top"    10 $butSetDynamic
        -attachControl     $strType[1]     "top"    10 $strType[0]
        -attachControl     $pdiLabelStresses      "top"    10 $strType[1]
        -attachForm     $pdiLabelStresses      "left"    0
        -attachControl     $pdiStressesList    "top"   10 $pdiLabelStresses
        -attachForm     $pdiStressesList      "left"    0
        -attachForm     $pdiStressesList      "right" 0
        -attachControl  $butSelectAllStresses      "top"    0 $pdiStressesList
        -attachForm     $butSelectAllStresses      "left"   0
        -attachControl  $butSelectStresses      "top"    0 $pdiStressesList
        -attachControl  $butSelectStresses      "left"   0 $butSelectAllStresses
        -attachControl  $chkPdiEnabledStress      "top" 10 $butSelectStresses
        -attachForm  $chkPdiEnabledStress      "left" 70 
        -attachControl  $chkPdiEnabledTorsionStress    "top" 10 $butSelectStresses
        -attachForm  $chkPdiEnabledTorsionStress      "left" 170 
        -attachControl  $chkPdiBreakableAtFrame    "top" 10 $chkPdiEnabledStress
        -attachForm  $chkPdiBreakableAtFrame      "left" 70      
        -attachControl     $fieldPdiBreakFrame     "top" 10 $chkPdiBreakableAtFrame
        -attachForm  $fieldPdiBreakFrame       "left" 10 
        -attachControl  $chkPdiBreakableForce    "top" 10 $fieldPdiBreakFrame
        -attachForm  $chkPdiBreakableForce      "left" 70   
        -attachControl     $fieldPdiBreakForce     "top" 10 $chkPdiBreakableForce
        -attachForm  $fieldPdiBreakForce       "left" 10 
        -attachControl     $fieldPdiBreakEnergy     "top" 10 $fieldPdiBreakForce
        -attachForm  $fieldPdiBreakEnergy       "left" 0 

    $form;

    $UpdatePdiFragmentsJobId = `scriptJob -event SelectionChanged updatePdiFragments`;
    scriptJob -uiDeleted $pdiAdvancedFracturesWindow killUpdatePdiFragmentsJobId;

    updatePdiAdvancedFracturesPanel();

}
//////////////////////////////////////////////////
global proc undoLastShatter()
{
 	undo;
}
global proc pdiShatterIt()
{
//    global string $gMainProgressBar;  // This is defined on maya startup
    global string $fieldPdiShatterItNumShards;
    global string $fieldPdiShardsSeed;
    global string $pdiOriginalMeshcollection;
   	global string $pdiOM[2];
   	global string $pdiRAXIS[3];
    global string $chkShItCreatePdiBodies;
    global string $chkShItCreatePdiFracture;
    global string $pdiCurvePath;
    global string $chkShItAssignNewMat;
    global string $fieldPdiNumberOfRings;
    global string $fieldPdiNumberOfCenters;    
    global string $fieldPdiMinRings;
    global string $fieldPdiMaxRings;
    global string $fieldPdiRingsNoise;
    global string $chkShItCreateCracker;
    global string $pdiExclusionObject;
    global string $pdiBodiesList;
    global string $fieldPdiShatterWidth;
    global string $chkShItApplyToCortners;    
    global string $comboShatterStyle;
    global string $ShItCutMatName;    
    global string $butPdiCustomMapping;
    
    updatePdiShatterPanel();//update the options depending on geometry

    //delete all existing pdi objects
    string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
    if ( `textScrollList -ex $pdiBodiesList` )
    {
	    for ( $obj in $PDIObjects )
        {
            textScrollList -edit -removeItem $obj $pdiBodiesList;
        }
    }
	for ( $obj in $PDIObjects )
    {
//            deleteUserNode $obj;
        delete $obj;
    }

//    string $selection[] = `ls -selection -dag -type "transform"`;
    string $selection[] = `ls -selection -dag -type "mesh"`;

	if( size($selection) == 0)
	{
		//look for a selected locator
		string $shatterLocator[] = `ls  -selection -dag -type "ShatterLocator"`;
		if( size($shatterLocator) > 0)
		{
			$transform = `listRelatives -parent $shatterLocator[0]`;
			select $transform;//select parent node
			$selection = `ls -selection -dag -type "mesh"`;					
		}
	}		
	    

//    print $selection;
//    print ("numShards = " + $numShards);

    string $result[];
    int $shatterStyle;
    int $deleteGeo;
    int $radialAxis;
    int $numShards = `intSliderGrp -query -value $fieldPdiShatterItNumShards`;
    int $shardsSeed = `intField -query -value $fieldPdiShardsSeed`;
    $bOnCortners = `checkBox -query -value $chkShItApplyToCortners`;
           
	$shatterStyle = `optionMenu -query -select $comboShatterStyle`;
   
    $newmat = `checkBox -query -value $chkShItAssignNewMat`;    
    string $cutmatname =`textField -query -tx $ShItCutMatName`;
    $bCracker = `checkBox -query -value $chkShItCreateCracker`; 
    float $shatterWidth = `floatSliderGrp -query -value $fieldPdiShatterWidth`;

    if( `radioButton -query -select $pdiOM[0]`)
    {
        $deleteGeo = 0;
    }
    else
    {
        $deleteGeo = 1;
    }

//    displaySurface -xRay true $selection;
    
	//done in code
    //progressBar -edit
    //-beginProgress
    //-isInterruptable false
    //-status "Precutting..."
    //-maxValue 100
    //$gMainProgressBar;


    waitCursor -state on;

    if( $pdiCurvePath=="")
    {
        if( $shatterStyle==3)
        {
            if( `radioButton -query -select $pdiRAXIS[0]`)
            {
                $radialAxis=0;
            }
            else
            if( `radioButton -query -select $pdiRAXIS[1]`)
            {
                $radialAxis=1;
            }
            else
            {
               $radialAxis=2;
            }

            int $numRings = `intSliderGrp -query -value $fieldPdiNumberOfRings`;
            float $noiseRings = `floatSliderGrp -query -value $fieldPdiRingsNoise`;

            $result = `pdiShatter -ns $numShards -seed $shardsSeed -st $shatterStyle
                -nr $numRings -nsr $noiseRings                
                -ra $radialAxis -nm $newmat -cmn $cutmatname -dog $deleteGeo $selection`;
        }
        else
        if( $shatterStyle==4)
        {
            if( `radioButton -query -select $pdiRAXIS[0]`)
            {
                $radialAxis=0;
            }
            else
            if( `radioButton -query -select $pdiRAXIS[1]`)
            {
                $radialAxis=1;
            }
            else
            {
               $radialAxis=2;
            }

            $result = `pdiShatter -ns $numShards -seed $shardsSeed -st $shatterStyle
                -nm $newmat -cmn $cutmatname -dog $deleteGeo -ra $radialAxis -wid $shatterWidth $selection`;

		}        
        else
        {
		    int $numCenters = `intSliderGrp -query -value $fieldPdiNumberOfCenters`;
			
			$result = `pdiShatter -ns $numShards -seed $shardsSeed -st $shatterStyle
            -nm $newmat -cmn $cutmatname -dog $deleteGeo -wid $shatterWidth -noc $numCenters -oco $bOnCortners $selection`;
        }
    }
    else
    {

        if( $bCracker)
        {
            //create cracking object
            int $startTime = `playbackOptions -query -minTime`;
            int $endTime = `playbackOptions -query -maxTime`;

            // create an object, e.g. a sphere
            float $bbox[]= `polyEvaluate -b -ae $selection`;
            float $x = $bbox[1] - $bbox[0];
            float $y = $bbox[3] - $bbox[2];
            float $z = $bbox[5] - $bbox[4];
            vector $v = <<$x,$y,$z>>;
            float $radius = mag($v)/200;
                     
            string $crackerName = "cracker_" + $pdiCurvePath;
            string $cracker[] = `ls $crackerName`;
            if( size($cracker) > 0)
            {
                delete $cracker;
            }           

            $cracker = `polySphere -n $crackerName -sx 12 -sy 12 -axis 0 1 0 -radius $radius`;
            pathAnimation  -stu $startTime -etu $endTime  -c $pdiCurvePath $cracker[0];

            //create pdiSolver node if necessary
            pdiSolver;
            string $crackerShapes[] = `listRelatives -s -path $cracker[0]`;

            new_PDIBody -a 0 -st 2 $crackerShapes[0];
        }

//        select $selection;
            $result = `pdiShatter -ns $numShards -seed $shardsSeed -st $shatterStyle
                -nm $newmat -cmn $cutmatname -dog $deleteGeo -wid $shatterWidth -spline $pdiCurvePath $selection`;            

    }


//		xform -cp $result;//center pivots
	
    // fix vertex normals
//    select $result;
//    polySetToFaceNormal	-setUserNormal;
    
//    print ( "shatter done");
    
    $bcreaterigidbodies = `checkBox -query -value $chkShItCreatePdiBodies`;
//    $bcreatefracturebody = `checkBox -query -value $chkShItCreatePdiFracture`;
	if(0)// $bcreatefracturebody==true)
    {
	    string $PDISolver = `pdiSolver`;
	    int $indexFbodies = getAttr ($PDISolver + ".caif" );
		string $nameFbody = "Fbody" + $indexFbodies;
    
        new_PDIBody -a 0 -st 4 $result;
    
		string $pdiobjects[];
		for($obj in $result)
		{
			string $parentobject[] =`listRelatives -p $obj`;

			string $pdiRigidBody[] =`listRelatives -type pdiRigidBody $parentobject[0]`;
			if( size($pdiRigidBody) > 0)
			{
				$pdiobjects[size($pdiobjects)] = $pdiRigidBody[0];
			}
		}
    
//        print $pdiobjects[0];
		
       $fractureBodyNode = `new_dFractureBody -n $nameFbody -dTh 0.05 $pdiobjects`;    
	}
	else 
	if( $bcreaterigidbodies==true)
    {
        //create pdiSolver node if necessary
        pdiSolver;

        new_PDIBody -a 0 -st 4 $result;
    }

	if( size($result) > 0)
	{
	    undoInfo -swf off;						
    
//		if( $newmat )
//		{	
//			button -edit -enable true  $butPdiCustomMapping;	
//		}	
		
		// remove shatter locator if exist
		string $shatterLocator[] = `ls  -dag -type "ShatterLocator"`;
		if( size($shatterLocator) > 0)
		{
			delete $shatterLocator[0];
		}	

	    undoInfo -swf on;						
	}	

    waitCursor -state off;
}
global proc pdiUpdateNumCenters()
{
//    global string $fieldPdiNumberOfCenters;
    
//	int $numCenters = `intSliderGrp -query -value $fieldPdiNumberOfCenters`;	
	string $shatterLocator[] = `ls  -dag -type "ShatterLocator"`;
	if( size($shatterLocator) > 0)
	{
		//locator exist already, update it
		$transform = `listRelatives -parent $shatterLocator[0]`;
		select $transform;//select parent node
		delete $shatterLocator[0];		
		pdiEditCenters();		
	}		
}
global proc pdiEditCenters()
{
    global string $butPdiEditCenters;
    global string $fieldPdiNumberOfCenters;
    global string $fieldPdiShardsSeed;
    global string $fieldPdiNumberOfCenters;
    global string $chkShItApplyToCortners;

    int $shardsSeed = `intField -query -value $fieldPdiShardsSeed`;
	int $numCenters = `intSliderGrp -query -value $fieldPdiNumberOfCenters`;
    $bOnCortners = `checkBox -query -value $chkShItApplyToCortners`;

    string $selection[] = `ls -selection -dag -type "mesh"`;
	
	if( size($selection) == 0)
	{
         print( "no objects selected");	    			
	}		
	else if( size($selection) > 1)
	{
        print( "no multiple selection allowed for edit centers");	    				
	}
	else
	{
		int $numCenters = `intSliderGrp -query -value $fieldPdiNumberOfCenters`;

		string $shatterLocator[] = `ls  -dag -type "ShatterLocator"`;
		if( size($shatterLocator)== 0)
		{
			//create locator node
			$transform = `listRelatives -parent $selection[0]`;
			createNode -p $transform ShatterLocator ;	
			pdiShatterPreview -st 2 -seed $shardsSeed -noc $numCenters -oco $bOnCortners $selection[0];
		}
		else
		{
			//locator exist already, select it
			$transform = `listRelatives -parent $selection[0]`;
			$currentCenters=`getAttr -mi ($shatterLocator[0] + ".shatter_pos")`;
			print (size($currentCenters));
			print ("num centers " + $numCenters);
			if( size($currentCenters)!=$numCenters)
			{
				//numcenters changed, rebuild locator
				delete $shatterLocator[0];
				createNode -p $transform ShatterLocator ;
				pdiShatterPreview -st 2 -seed $shardsSeed -noc $numCenters -oco $bOnCortners $selection[0];													
			}
			else
			{			
				$transform2 = `listRelatives -parent $shatterLocator[0]`;
				if( $transform[0]!=$transform2[0])
				{
					//selecttion changed, move locator to this node
					delete $shatterLocator[0];
					createNode -p $transform ShatterLocator ;
					pdiShatterPreview -st 2 -seed $shardsSeed -noc $numCenters -oco $bOnCortners $selection[0];									
}
			}						
			select $shatterLocator[0];		
		}		
		setToolTo ShowManips;
	}	
}

global proc pdiSelectPath()
{
    global string $pdiCurvePath;
    global string $butPdiSelectPath;

    string $selection[] = `ls -selection -dag -type "nurbsCurve"`;

    if( size($selection) > 0)
    {
        $pdiCurvePath = $selection[0];
        string $selected = "Path: " +  $pdiCurvePath;
        button -edit -label $selected $butPdiSelectPath;

//        pdiChangeContext();
    }
    else
    {
        button -edit -label "Select path" $butPdiSelectPath;
        warning "No curve object selected";
    }
}

global proc updateShatterStyle()
{
    global string $pdiShatterItWindow;
    global string $butPdiSelectPath;
    global string $pdiCurvePath;
    global string $fieldPdiNumberOfRings;
    global string $fieldPdiNumberOfCenters;    
    global string $fieldPdiRingsNoise;
    global string $chkShItCreateCracker;
    global string $pdiRadialAxisFrame;
    global string $fieldPdiShatterWidth;
    global string $chkShItApplyToCortners;    
   	global string $pdiRAXIS[3];    
    global string $comboShatterStyle;    
    global string $butPdiEditCenters;

	//disable all shatter param first
    button -edit -enable false $butPdiSelectPath;
    button -edit -enable false $butPdiEditCenters;
    checkBox -edit -enable false $chkShItCreateCracker; 
    floatSliderGrp -edit -enable false $fieldPdiShatterWidth;
    intSliderGrp -edit -enable false $fieldPdiNumberOfCenters;
    intSliderGrp -edit -enable false $fieldPdiNumberOfRings;
    floatSliderGrp -edit -enable false $fieldPdiRingsNoise;
    frameLayout -edit -enable false $pdiRadialAxisFrame;
    checkBox -edit -enable false $chkShItApplyToCortners;

	int $shatterStyle = `optionMenu -query -select $comboShatterStyle`;

	if( $shatterStyle==5)
    {
        button -edit -enable true $butPdiSelectPath;
        checkBox -edit -enable true $chkShItCreateCracker; 
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;
    }
    else
    {
        button -edit -label "Select path" $butPdiSelectPath;
        $pdiCurvePath = "";
    }
    
	if( $shatterStyle==2)
    {
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;
        floatSliderGrp -edit -value 0.2 $fieldPdiShatterWidth;                
        intSliderGrp -edit -enable true $fieldPdiNumberOfCenters;
	    checkBox -edit -enable true $chkShItApplyToCortners;       
	    button -edit -enable true $butPdiEditCenters;	       
    }
    else
	{
		// remove shatter locator if exist
		string $shatterLocator[] = `ls  -dag -type "ShatterLocator"`;
		if( size($shatterLocator) > 0)
		{
			delete $shatterLocator[0];
		}		
    }

	if( $shatterStyle==3)
	{
	    intSliderGrp -edit -enable true $fieldPdiNumberOfRings;
		floatSliderGrp -edit -enable true $fieldPdiRingsNoise;

        frameLayout -edit -enable true $pdiRadialAxisFrame;	
		radioButton -edit -select $pdiRAXIS[0];       
	} 
	
	if( $shatterStyle==4)
	{
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;	
        floatSliderGrp -edit -value 1.0 $fieldPdiShatterWidth;        
        frameLayout -edit -enable true $pdiRadialAxisFrame;	
		radioButton -edit -select $pdiRAXIS[1];       
	} 

    showWindow $pdiShatterItWindow;
}
global proc string getCurrentPdiCutMaterial()
{
	string $selection[] = `ls -selection -dag -type "mesh"`;
	for($obj in $selection)
	{
		string $shaders[] = `listConnections -d false -type "shadingEngine" $obj`;
		for($shader in $shaders)
		{
			string $materials[] = `listConnections -type "lambert" $shader`;
			for($mat in $materials)
			{    
				int $result=`attributeQuery -node $mat -ex "cutmat"`;
				if( $result > 0)
				{
				   return $mat; 
				} 
			} 
		} 
	} 

	return (""); 
}
global proc updatePdiShatterPanel()
{
    global string $pdiShatterItWindow;
    global string $butPdiSelectPath;
    global string $chkShItAssignNewMat;
    global string $ShItCutMatName;    
    global string $pdiCurvePath;
    global string $fieldPdiNumberOfRings;
    global string $fieldPdiNumberOfCenters;    
    global string $fieldPdiRingsNoise;
    global string $chkShItCreateCracker;
    global string $pdiRadialAxisFrame;
    global string $fieldPdiShatterWidth;
    global string $chkShItApplyToCortners;    
    global string $comboShatterStyle;    
    global string $butPdiEditCenters;
    global string $butPdiCustomMapping;
	
	//disable all shatter param first
    button -edit -enable false $butPdiSelectPath;
    button -edit -enable false   $butPdiEditCenters;  
    checkBox -edit -enable false $chkShItCreateCracker; 
    floatSliderGrp -edit -enable false $fieldPdiShatterWidth;
    intSliderGrp -edit -enable false $fieldPdiNumberOfCenters;
    intSliderGrp -edit -enable false $fieldPdiNumberOfRings;
    floatSliderGrp -edit -enable false $fieldPdiRingsNoise;
    frameLayout -edit -enable false $pdiRadialAxisFrame;
    checkBox -edit -enable false $chkShItApplyToCortners;
//    button -edit -enable false  $butPdiCustomMapping;	
               	
	int $shatterStyle = `optionMenu -query -select $comboShatterStyle`;

//    if( $pathBasedSelected)
	if( $shatterStyle==5)
    {
        button -edit -enable true $butPdiSelectPath;
        checkBox -edit -enable true $chkShItCreateCracker; 
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;
    }
    else
    {
        button -edit -label "Select path" $butPdiSelectPath;
        $pdiCurvePath = "";
    }
    
//    if( $pivotBasedSelected)
	if( $shatterStyle==2)
    {
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;
        intSliderGrp -edit -enable true $fieldPdiNumberOfCenters;
	    checkBox -edit -enable true $chkShItApplyToCortners;       
	    button -edit -enable true  $butPdiEditCenters;       
    }

//    if( $radialSelected )
	if( $shatterStyle==3)
    {
        intSliderGrp -edit -enable true $fieldPdiNumberOfRings;
        floatSliderGrp -edit -enable true $fieldPdiRingsNoise;
        frameLayout -edit -enable true $pdiRadialAxisFrame;
    }
    
//    if($woodSplinterSelected)
	if( $shatterStyle==4)
	{
        frameLayout -edit -enable true $pdiRadialAxisFrame;	
        floatSliderGrp -edit -enable true $fieldPdiShatterWidth;        
	} 

	string $pdicutmat=getCurrentPdiCutMaterial();
     if( $pdicutmat!="")
    {
        checkBox -edit -value 1 $chkShItAssignNewMat;
	textField -edit -tx $pdicutmat $ShItCutMatName;                
    }
      	
    showWindow $pdiShatterItWindow;
    setFocus MayaWindow;   
}
global proc pdiInputNoiseRings()
{
    global string $fieldPdiRingsNoise;

//    global string $fieldPdiMinRingsNoise;
//    global string $fieldPdiMaxRingsNoise;

    float $noiseRingsValue = `floatSliderGrp -query -value  $fieldPdiRingsNoise`;

//    floatField -edit -value $noiseRingsValue $fieldPdiMinRingsNoise;
//    floatField -edit -value $noiseRingsValue $fieldPdiMaxRingsNoise;
}

global proc pdiInputNumRings()
{
    global string $fieldPdiNumberOfRings;

    int $numShards = `intSliderGrp -query -value  $fieldPdiNumberOfRings`;
}
global proc pdiInputNumShards()
{
    global string $fieldPdiShatterItNumShards;

    int $numShards = `intSliderGrp -query -value $fieldPdiShatterItNumShards`;
}
global proc pdiNewMaterialName()
{
    global int $pdimatcounter=1;
    global string $ShItCutMatName;
    
    string $newName="pdiCutMat";
    $newName+=$pdimatcounter;
    
    textField -edit -tx $newName $ShItCutMatName;

    $pdimatcounter++;
}
global proc  createShatterItPanel()
{ 
    global string $pdiShatterItWindow;
    global string $fieldPdiShatterItNumShards;
    global string $fieldPdiShardsSeed;
    global string $pdiOriginalMeshcollection;
    global string $pdiRadialAxisFrame;
    global string $pdiRadialAxiscollection;
   	global string $pdiOM[2];
   	global string $pdiRAXIS[3];
    global string $chkShItCreatePdiBodies;
    global string $chkShItCreatePdiFracture;
    global string $butPdiSelectPath;
    global string $chkShItAssignNewMat;
    global string $fieldPdiNumberOfRings;
    global string $fieldPdiNumberOfCenters;
    global string $fieldPdiRingsNoise;
    global string $chkShItCreateCracker;
    global string $fieldPdiShatterWidth;
    global string $chkShItApplyToCortners;
    global string $comboShatterStyle;
    global string $butPdiEditCenters;
    global string $butPdiCustomMapping;
    global string $ShItCutMatName;    
    global int $pdimatcounter;
    
    global int $updatePdiShatterPanelJobId;

    //check if the window exists
    if ( `window -ex pdiShatterWin` )
    {                
        updatePdiShatterPanel();
        
        return;
    }

	string $baseName="pdiCutMat";
	string $cutMatName=$baseName+$pdimatcounter;    
	while(1)
	{
		string $exist[]=`ls $cutMatName`;
		if($exist[0]=="")break;
		
		$pdimatcounter++;			
		$cutMatName=$baseName+$pdimatcounter; 		
	}		

	$pdiShatterItWindow = `window -rtf true -t "ShatterIt feature" -width 100 -height 180 pdiShatterWin`;
//	string $form = `columnLayout`;
//	string $tabs = `tabLayout -innerMarginWidth 5 -innerMarginHeight 5`;
	
	string $child1 = `columnLayout -adjustableColumn true -rs 10 mainShatterItOptions`; 
		rowColumnLayout -numberOfRows 1  -co 1 "left" 1;        
			text -label "seed"   -align "left";
			$fieldPdiShardsSeed = `intField -w 30 -s 1 -minValue -0 -maxValue 100 -value 0
                    			-ann " seed value diferent than 0 allows shatter repeteability" `;  
		setParent..;                	
        $fieldPdiShatterItNumShards = `intSliderGrp -label "Num Shards" -field true
	            -minValue 2 -maxValue 2048 -fieldMinValue 2 -fieldMaxValue 100000
	            -ann " aprox. number of fragments"
                -value 64 -step 8 -cc "pdiInputNumShards"`;
		$comboShatterStyle = `optionMenu -width 30 -label "Shatter Style"
	            -ann " select style of the fragments generated"
		 -cc "updateShatterStyle"`;
        menuItem -label "Uniform";
        menuItem -label "Local";
        menuItem -label "Radial";
        menuItem -label "Wood splinters";
        menuItem -label "Path Based";
//    setParent..;
    
//local shatter parameters   
    $chkShItApplyToCortners = `checkBox -label "apply only on corners" -align "left"
	-ann " generate local fragments on cortners of the object"
	-value false -cc "pdiUpdateNumCenters"`;	              	
    $fieldPdiShatterWidth = `floatSliderGrp -label "Width" -field true
	-ann " set average size of local fragments"                
	-minValue 0.0 -maxValue 1.0 -value 0.2 -step 0.5`;
    $fieldPdiNumberOfCenters = `intSliderGrp -label "Num Centers" -field true
	-ann " set number of centers for local fragments"
    -minValue 1 -maxValue 16 -value 1 -step 1 -cc "pdiUpdateNumCenters"` ;	
                
//radial parameters
        rowColumnLayout -numberOfRows 1;// -co 1 "left" 34;
            $fieldPdiNumberOfRings = `intSliderGrp -label "Num Rings" -field true
				-ann " set number of concentric rings afte shattering"                           
	            -minValue 1 -maxValue 20 -value 3 -step 1 -cc "pdiInputNumRings"` ;
        setParent..;
        rowColumnLayout -numberOfRows 1;// -co 1 "left" 34;
        $fieldPdiRingsNoise = `floatSliderGrp -label "Noise" -field true   
			-ann " add noise in radial fragments distance"                                
	        -minValue 0.0 -maxValue 10.0 -value 0.3 -step 0.2 -cc "pdiInputNoiseRings"`;
        setParent..;
    $pdiRadialAxisFrame = `frameLayout -label "Main Axis" -borderStyle "etchedOut"
    			-ann " fragments will be generated around main axis" `;
    	rowLayout -numberOfColumns 3;
        	$pdiRadialAxiscollection = `radioCollection `;
	        $pdiRAXIS[0]=`radioButton -label "S (shortest)" -select `;
            $pdiRAXIS[1]=`radioButton -label "M (medium)" `;
	        $pdiRAXIS[2]=`radioButton -label "L (longest)" `;
	    setParent..;	
	    setParent..;	
//path based parameters        
    $chkShItCreateCracker = `checkBox -label "Create cracker object" -align "left"
    	-ann " cracker object will allow you to control the speed and spread of the cracks"                
		 -value true`;	                 
//    setParent..;

	$butPdiEditCenters=`button  -w 100 -label "Edit centers" -aop true
    	-ann " edit centers for local shatter" -command "pdiEditCenters"`;
    $butPdiSelectPath = `button  -w 100 -label "Select path" -aop true
    	-ann " select spline for path-based shatter" -command "pdiSelectPath"`;

    frameLayout -width 400 -height 50 -label "Original Geometry" -borderStyle "etchedOut"
    			-ann " hide or remove original geometry after shattering";
        rowLayout -columnWidth2 100 100 -numberOfColumns 2;
        $pdiOriginalMeshcollection=`radioCollection`;
            $pdiOM[0]=`radioButton -label "Hide" -select `;
            $pdiOM[1]=`radioButton -label "Remove" `;
        setParent..;
    setParent..;

    frameLayout -width 400 -height 70 -label "Cut Material" -borderStyle "etchedOut"
    			-ann " assign diferent material to cut faces" layOutCutMat;
        columnLayout -adjustableColumn false; 
		$chkShItAssignNewMat = `checkBox -label "apply" -align "left"
            			-ann " cut faces will be mapped with this new material"`;	
			rowLayout -numberOfColumns 3;     
				text -label "Name:";			       			
				$ShItCutMatName = `textField -w 200 -h 20 -tx $cutMatName`;
		        $butShItNewMat = `button  -w 50 -h 20 -label "New"
            			-ann " create a new material"	              	              
						-command "pdiNewMaterialName"`;				           			 
			setParent..;
        setParent..;
    setParent..;
                			             	              
    $chkShItCreatePdiBodies = `checkBox -label "Create Rigid bodies from fragments" -align "left"
        			-ann " auto create rigid bodies from fragments"`;	              
//    $chkShItCreatePdiFracture = `checkBox -label "Create Fracture body from fragments" -align "left"`;	              

    string $butPdiShatterIt = `button  -w 100 -h 40 -label "Shatter It!"
            			-ann " trigger the shatter process"	              	              
						-command "pdiShatterIt"`;

    string $butPdiUndoShatter = `button  -w 100 -h 20 -label "Undo"
            			-ann " undo last shatter"	              	              
						-command "undoLastShatter"`;						
    
//    $butPdiCustomMapping = `button  -w 100 -h 20 -label "Add custom mapping" -enable false
//            			-ann " add custom mapping"	              	              
//						-command "pdiMapCutFacesCustom"`;
						
    setParent..;

//    tabLayout -edit
//         -tabLabel $child1 "Basic" -tabLabel $child2 "Advanced"
//         $tabs;

    $updatePdiShatterPanelJobId = `scriptJob -event SelectionChanged updatePdiShatterPanel`;
    scriptJob -uiDeleted $pdiShatterItWindow killUpdatePdiShatterPanelJobId;

    updatePdiShatterPanel();
}
global proc pdiAddJagginessExecute()
{
    global string $gMainProgressBar;  // This is defined on maya startup
    global string $butPdiAddJagginess;
	global string $butPdiRemoveJagginess;    
    global string $fielShitJagResolution;
    global string $fieldShitJagStrenght;
	global string $SelectJaggyFragment;
	global string $chkShitSoftEdges;	
   	global string $pdijaggyAffect[3];		
	
	int $startTime = `playbackOptions -query -minTime`;
	int $currentTime =`currentTime -query`;	
	
	if( $currentTime > $startTime)
	{
	    warning "time set to start frame for adding jagginess";			
	
		currentTime -edit $startTime;		
		
//		string $msg="Warning: adding jagginess in a frame diferent than start frame" +
//		"\ncan cause fragments edges doenst match with others, proceed anyway?";
//		string $result = `confirmDialog -title "Confirm" -message $msg
//			-button "Yes" -button "No" -defaultButton "No"
//			-cancelButton "No" -dismissString "No"`;

//		if( $result == "No")
//		{
//			return;
//		}	
	}	

    int $resolution = `intSliderGrp -query -value $fielShitJagResolution`;
    float $strength = `floatSliderGrp -query -value $fieldShitJagStrenght`;
    int $bSoftEdges = `checkBox -q -value $chkShitSoftEdges`;	
    
//	string  $PDIObjects[];
//    $PDIObjects = `ls -selection -dag -leaf -visible -geometry`;

    int $affect_type;
    if( `radioButton -query -select $pdijaggyAffect[2]`)
    {
        $affect_type = 2;
    }
    else
    if( `radioButton -query -select $pdijaggyAffect[1]`)
    {
        $affect_type = 1;
    }
    else
    {
        $affect_type = 0;
    }

    waitCursor -state on;

    //progressBar -edit
    //-beginProgress
    //-isInterruptable true
    //-status "Baking keys..."
    //-maxValue $simTine
    //$gMainProgressBar;

    $result = `pdiJagginess -com -res $resolution -st $strength -sn $bSoftEdges -affect $affect_type $SelectJaggyFragment`;

	select $SelectJaggyFragment;//trick for returning to object mode
	
//        progressBar -edit -endProgress $gMainProgressBar;

	button -edit -enable false $butPdiAddJagginess;		
	button -edit -enable true $butPdiRemoveJagginess;					
	intSliderGrp -edit -enable false $fielShitJagResolution;										

    waitCursor -state off;
}
global proc pdiRemovejagginessExecute()
{
	global string $SelectJaggyFragment;
	global string $butPdiAddJagginess;
	global string $butPdiRemoveJagginess;   
    global string $fielShitJagResolution;	 
   	global string $pdijaggyAffect[3];		
	
    int $affect_type;
    if( `radioButton -query -select $pdijaggyAffect[2]`)
    {
        $affect_type = 2;
    }
    else
    if( `radioButton -query -select $pdijaggyAffect[1]`)
    {
        $affect_type = 1;
    }
    else
    {
        $affect_type = 0;
    }

    undoInfo -swf off;    
	
	if( $affect_type == 2)
	{
		string $result = `confirmDialog -title "Confirm" -message "This action will delete jaggyness for whole shatter-group, are you sure?"
		-button "Yes" -button "No" -defaultButton "Yes"
		-cancelButton "No" -dismissString "No"`;

		if( $result == "No")
		{
			return;
		}

        waitCursor -state on;
        
		//make faces planar
		$result = `pdiJagginess -st 0.0 -affect $affect_type $SelectJaggyFragment`;

		//delete jaggyness for whole shatter group
		$result = `pdiJagginess -del -sn false -affect $affect_type $SelectJaggyFragment`;        
		
        waitCursor -state off;
	}	
	else
	if( $affect_type == 1)
	{
	
		string $result = `confirmDialog -title "Confirm" -message "This action will delete jaggyness for whole sub-group, are you sure?"
		-button "Yes" -button "No" -defaultButton "Yes"
		-cancelButton "No" -dismissString "No"`;

		if( $result == "No")
		{
			return;
		}

        waitCursor -state on;
		
		//make faces planar
		$result = `pdiJagginess -st 0.0 -affect $affect_type $SelectJaggyFragment`;

		//delete jaggyness for sub-shatter group
		$Parent=`listRelatives -p $SelectJaggyFragment`;
		$grandParent=`listRelatives -p $Parent`;
		$transformNodes=`listRelatives -c $grandParent`;
		for($obj in $transformNodes)
		{
			$shapeNodes=`listRelatives -s $obj`;
			
			if( size( $shapeNodes) > 0)
			{	
				if( `attributeQuery -node $shapeNodes[0] -exists "inMesh"`)
				{				
					string $connect[]=`listConnections -d off -s on ($shapeNodes[0]+ ".inMesh")`;
					if(size($connect) > 0)
					{									
						string $type=`nodeType $connect[0]`;
						if( $type=="polySubdFace" || $type=="polyBlindData" || $type=="polyNormalPerVertex" )
						{						
							//delete soften edges
							string $normalPervertex[] = `listConnections -s true -d false -t "polyNormalPerVertex" $shapeNodes[0]`;
							if( size($normalPervertex) > 0)
							{
								delete $normalPervertex[0];
							}	
							
							//delete all blind data
							while(1)// removedAlLBlind==false)
							{	 
								string $blindcon[] = `listConnections -s true -d false -t "polyBlindData" $shapeNodes[0]`;
								if( size($blindcon)==0)break;
								
								delete $blindcon[0];					
							}		
				
							//delete history
							string $connect2[]=`listConnections -d off -s on ($shapeNodes[0]+ ".inMesh")`;					
							delete $connect2[0];				
//							print " polySubdFace deleted ";							
							
							delete -ch $shapeNodes[0];	
						}	
					}	
				}	
			}
		}	
        waitCursor -state off;			
	}		
	else
	{	
		//make faces planar
		$result = `pdiJagginess -st 0.0 -affect $affect_type $SelectJaggyFragment`;

		//delete soften edges
		string $normalPervertex[] = `listConnections -s true -d false -t "polyNormalPerVertex" $SelectJaggyFragment`;
		if( size($normalPervertex) > 0)
		{
			delete $normalPervertex[0];
		}	
		
		//delete all blind data
	//	bool removedAlLBlind=false;
		while(1)// removedAlLBlind==false)
		{	 
			string $blindcon[] = `listConnections -s true -d false -t "polyBlindData" $SelectJaggyFragment`;
			if( size($blindcon)==0)break;
			
			delete $blindcon[0];
			
		}		
		
		//delete history
		string $connect[]=`listConnections -d off -s on ($SelectJaggyFragment+ ".inMesh")`;
		delete $connect[0];
		
		delete -ch $SelectJaggyFragment;	
	}
	button -edit -enable true $butPdiAddJagginess;		
	button -edit -enable false $butPdiRemoveJagginess;		
	intSliderGrp -edit -enable true $fielShitJagResolution;										
	
    undoInfo -swf on;				
	
}
global proc setPdiJaggySoftEdges()
{
	global string $SelectJaggyFragment;
	global string $chkShitSoftEdges;
   	global string $pdijaggyAffect[3];		

	if( $SelectJaggyFragment=="")
	{
		return;//no fragmetn selected
	}
	
    int $bSoftEdges = `checkBox -q -value $chkShitSoftEdges`;	
	
    int $affect_type;
    if( `radioButton -query -select $pdijaggyAffect[2]`)
    {
        $affect_type = 2;
    }
    else
    if( `radioButton -query -select $pdijaggyAffect[1]`)
    {
        $affect_type = 1;
    }
    else
    {
        $affect_type = 0;
    }

	if( $bSoftEdges > 0)
	{	
		//add soften edges		
		$result = `pdiJagginess -sn true -affect $affect_type $SelectJaggyFragment`;			
	}
	else
	{	
		//delete soften edges
	    
		if( $affect_type == 2)
		{
	        waitCursor -state on;
		
			$result = `pdiJagginess -del -sn true -affect $affect_type $SelectJaggyFragment`;        	
			
	        waitCursor -state off;		
		}
		else
		if( $affect_type == 1)
		{			
			undoInfo -swf off;				
			
			$Parent=`listRelatives -p $SelectJaggyFragment`;
			$grandParent=`listRelatives -p $Parent`;
			$transformNodes=`listRelatives -c $grandParent`;
			for($obj in $transformNodes)
			{
				$shapeNodes=`listRelatives -s $obj`;
				
				if( size( $shapeNodes) > 0)
				{					
					string $normalPervertex[] = `listConnections -s true -d false -t "polyNormalPerVertex" $shapeNodes[0]`;
					if( size($normalPervertex) > 0)
					{
						delete $normalPervertex[0];
					}		
				}	
			}				
			undoInfo -swf on;					
		}
		else
		{
			undoInfo -swf off;				
			
			string $normalPervertex[] = `listConnections -s true -d false -t "polyNormalPerVertex" $SelectJaggyFragment`;
			if( size($normalPervertex) > 0)
			{
				delete $normalPervertex[0];
			}		
			
			undoInfo -swf on;				
		}		    
	}			
}
global proc setPdiJaggyStrenght()
{
	global string $SelectJaggyFragment;
    global string $fieldShitJagStrenght;
   	global string $pdijaggyAffect[3];		
	
    int $affect_type;
    if( `radioButton -query -select $pdijaggyAffect[2]`)
    {
        $affect_type = 2;
    }
    else
    if( `radioButton -query -select $pdijaggyAffect[1]`)
    {
        $affect_type = 1;
    }
    else
    {
        $affect_type = 0;
    }
	
	if( $SelectJaggyFragment!="")
	{	
		float $strength = `floatSliderGrp -query -value $fieldShitJagStrenght`;

		$result = `pdiJagginess -st $strength -affect $affect_type $SelectJaggyFragment`;
	}
	else
	{
       warning "Only one fragment selection allowed";  	
	}			
}
global proc pdiSelectFragmentForJaggy()
{
	global string $butPdiSelectJaggyFragment;    
    global string $butPdiAddJagginess;
	global string $butPdiRemoveJagginess;
    global string $fieldShitJagStrenght;
    global string $fielShitJagResolution;    
	global string $chkShitSoftEdges;   
	global string $pdiJaggyAfectcollection;
   	global string $pdijaggyAffect[3];			 	
	global string $SelectJaggyFragment;	
			
//	print "update selected fragment";			
//	print $SelectJaggyFragment;

	string $lastSelectJaggyFragment=$SelectJaggyFragment;
	string $PDIFragments[];
	
	$PDIFragments =`ls -selection -dag -type "transform"`;
		
    if( size($PDIFragments) == 0)
    {
		$PDIShapes =`ls -selection -dag -type "mesh"`;
		if( size($PDIShapes)==0)		
		{
			button -edit -label "Select PDI Fragment" $butPdiSelectJaggyFragment;	
			button -edit -enable false $butPdiAddJagginess;		
			button -edit -enable false $butPdiRemoveJagginess;		
		
			$SelectJaggyFragment="";	
			
			return;	             		   	             
		}
		else
		{
			$PDIFragments=`listRelatives -p $PDIShapes[0]`;		
		}			
	}
//	else
//	{			
		$Parent=`listRelatives -p $PDIFragments[0]`;
		$voronoiNode=`listRelatives -ad -typ voronoiNode $Parent`;
		if( size($voronoiNode) == 0)
		{
//		   error "This fragment isnt generated with  Shatter it tool";        
  		   button -edit -label "Select PDI Fragment" $butPdiSelectJaggyFragment;
  		   
  		   $SelectJaggyFragment="";		             		   
		}
		else
		{	
//			print " checking if it is voronoi Node";
			
			$PdiShapes=`listRelatives -s $PDIFragments[0]`;
			
			if( $SelectJaggyFragment!=$PdiShapes[0])
			{			
				$SelectJaggyFragment=$PdiShapes[0];
				string $selected = " fragment: " +  $SelectJaggyFragment;
				button -edit -label $selected $butPdiSelectJaggyFragment;	
					
				if( $lastSelectJaggyFragment!=$SelectJaggyFragment)
				{
					//select affect sub-group when changing selection
					radioCollection -edit -select  $pdijaggyAffect[1] $pdiJaggyAfectcollection;		
				}		
				
				
				if( size($PdiShapes) == 1)
				{
					//it is a clean fragment
//					print (" clean fragment");					
					button -edit -enable true $butPdiAddJagginess;		
					button -edit -enable false $butPdiRemoveJagginess;	
					checkBox -edit -value 0 $chkShitSoftEdges;		
			        intSliderGrp -edit -enable true $fielShitJagResolution;										
				}		
				else
				{
					//check if it is really a jaggy fragment
					string $connect[]=`listConnections -d off -s on ($PdiShapes[0]+ ".inMesh")`;
					if(size($connect) > 0)
					{									
						string $type=`nodeType $connect[0]`;
						if( $type=="polySubdFace" || $type=="polyBlindData" || $type=="polyNormalPerVertex")
						{	
							//it is probably a jaggy fragment, turn on remove jagginess
							button -edit -enable false $butPdiAddJagginess;		
							button -edit -enable true $butPdiRemoveJagginess;	
					        intSliderGrp -edit -enable false $fielShitJagResolution;										
							
							//update softedges control
							
							if( $type=="polyNormalPerVertex")
							{	
								checkBox -edit -value 1 $chkShitSoftEdges;	
							}	
							else
							{	
								checkBox -edit -value 0 $chkShitSoftEdges;	
							}	

							//update strenght to group value
							float $UIstrength=`floatSliderGrp -query -value $fieldShitJagStrenght`;					        	
					        float $strength = getAttr ($voronoiNode[0] + ".jaggyStrenght" );
					        if( $UIstrength != $strength)
							{ 
								floatSliderGrp -edit -value $strength $fieldShitJagStrenght;					        	
//								setPdiJaggyStrenght();// update params( causes great slow down)				
							}	
						}
						else
						if( $type=="polyAutoProj" || $type=="polyPlanarProj"
						 || $type=="polyCylProj" || $type=="polySphProj") 						
						{
							//it is a clean fragment with uv mapping
		//					print (" uv mapped  fragment");					
							button -edit -enable true $butPdiAddJagginess;		
							button -edit -enable false $butPdiRemoveJagginess;	
							checkBox -edit -value 0 $chkShitSoftEdges;		
							intSliderGrp -edit -enable true $fielShitJagResolution;																	
						}																						
					}
					else
					{
						//possibly a pdi rigid body 
//						print $PdiShapes[1];
				        if( `nodeType $PdiShapes[1]`== "pdiRigidBody")
						{
//							print (" is a pdi body");
							button -edit -enable true $butPdiAddJagginess;		
							button -edit -enable false $butPdiRemoveJagginess;	
							checkBox -edit -value 0 $chkShitSoftEdges;		
							intSliderGrp -edit -enable true $fielShitJagResolution;																
						}	 
						else
						{	
						    error "cannot add jagginess to this shape";					    			
							button -edit -enable false $butPdiAddJagginess;		
							button -edit -enable false $butPdiRemoveJagginess;	
						}	
					}							
				}	
			}
//			else
//			{
//				print " same fragment, cancel";		
//			}			
		}	
//	}		
}
global proc  pdiAddJagginessPanel()
{ 
    global string $pdiShatterItJaginessWindow;
	global string $butPdiSelectJaggyFragment;    
    global string $fielShitJagResolution;
    global string $fieldShitJagStrenght;
    global string $butPdiAddJagginess;
	global string $butPdiRemoveJagginess;
	global string $chkShitSoftEdges;
	global string $pdiJaggyAfectcollection;
   	global string $pdijaggyAffect[3];	
	global string $SelectJaggyFragment;	   	
    global int $UpdateJaggyWindowJobId;	

    //check if the window exists
    if ( `window -ex pdiShatterJagginessWin` )
    {                
//        updatePdiAddJagginessWindow();
        
	    showWindow pdiShatterJagginessWin;
        
        return;
    }

	$pdiShatterItJaginessWindow = `window -rtf true -t " Add Jagginess Deformer" -width 75 -height 300 pdiShatterJagginessWin`;
	columnLayout -adjustableColumn true -rs 5;               
    $butPdiSelectJaggyFragment = `button  -w 75 -h 30 -label "Select PDI Fragment" -command "pdiSelectFragmentForJaggy"`;
    $butPdiAddJagginess = `button  -w 75 -h 20 -label "Add Jagginess" -aop true -enable false
    -ann " HINT: adding jagginess after baking keys for faster computing"
     -command "pdiAddJagginessExecute"`; 
    $butPdiRemoveJagginess = `button  -w 75 -h 20 -label "Remove Jagginess" -aop true -enable false -command "pdiRemovejagginessExecute"`; 
	
    frameLayout -width 75 -height 50 -label "Affect" -borderStyle "etchedOut";
        rowLayout -columnWidth3 125 125 125 -numberOfColumns 3;
        $pdiJaggyAfectcollection=`radioCollection`;
            $pdijaggyAffect[0]=`radioButton -label "single fragment" `;
            $pdijaggyAffect[1]=`radioButton -label "sub-group" -select`;// -onCommand "setPdiJaggyStrenght"`;
            $pdijaggyAffect[2]=`radioButton -label "shatter group"`;// -onCommand "setPdiJaggyStrenght"`;
        setParent..;
    setParent..;
	
    frameLayout -width 75 -height 100 -label "" -borderStyle "etchedOut";
    	columnLayout -adjustableColumn true; 
        $fieldShitJagStrenght = `floatSliderGrp -label "Strenght" -field true
	        -minValue 0.0 -maxValue 4.0 -value 1.0  -cc "setPdiJaggyStrenght"`; 
        $fielShitJagResolution = `intSliderGrp -label "Resolution" -field true
	        -minValue 1 -maxValue 4 -value 3`;
		$chkShitSoftEdges = `checkBox -label "soften edges" -align "left" -enable 1 -changeCommand "setPdiJaggySoftEdges"`;	        
        setParent..;
    setParent..;

    $UpdateJaggyWindowJobId = `scriptJob -event SelectionChanged pdiSelectFragmentForJaggy`;
    scriptJob -uiDeleted $pdiShatterItJaginessWindow killUpdateJaggyWindowJob;

	if( $SelectJaggyFragment!="")
	{	
		$SelectJaggyFragment="";
		pdiSelectFragmentForJaggy();
	}	

    showWindow $pdiShatterItJaginessWindow;
}
global proc killUpdateJaggyWindowJob()
{
    global int $UpdateJaggyWindowJobId;

    scriptJob -kill $UpdateJaggyWindowJobId;
}

global proc showPdiLicenseInfo()
{
    global string $pdiLicenseWindow;
    global string $PdiUserNamePrompt;

    $PdiUserName = "free version";

    text -edit -label $PdiUserName $PdiUserNamePrompt;

    showWindow $pdiLicenseWindow;
}
/////////////////////////////////////////////////
global proc pdiUI_createShelfButtons()
{
    // The shelf we want to add the button to.
    string $shelf = "PulldownIt";

    // The icon we want to use on the button.
    string $iconImage = "pdiCreateIcon.xpm";
    string $iconImage2 = "pdiSolverIcon.xpm";
    string $iconImage3 = "pdiManageIcon.xpm";
    string $iconImage4 = "pdiPropertiesIcon.xpm";
    string $iconImage5 = "pdiKeysIcon.xpm";
    string $iconImage6 = "pdiFracturesIcon.xpm";
    string $iconImage7 = "pdiAvFracturesIcon.xpm";
    string $iconImage8 = "shatteritIcon.xpm";
    string $iconImage9 = "thinkineticIcon.xpm";
    string $iconImage10 = "pdiAddJagginess.xpm";

    // The button label.
    string $iconBtnLabel = "Create Pdi Body";
    string $iconBtnLabel2 = "PDI Solver Options";
    string $iconBtnLabel3 = "Manage Pdi World";
    string $iconBtnLabel4 = " Set Pdi Dynamics Properties";
    string $iconBtnLabel5 = " Bake Pdi simulation";
    string $iconBtnLabel6 = " Pdi Basic Fractures";
    string $iconBtnLabel7 = " Pdi Advanced Fractures";
    string $iconBtnLabel8 = " ShatterIt feature";
    string $iconBtnLabel9 = " License info";
    string $iconBtnLabel10 = "Add jagginess to fragments";

    // Specify the shelf as the parent for our tool button.  If there
    // is no such shelf, we create it.
    //
    string $btnParent;
    if ( `shelfLayout -ex $shelf` )
    {
//	    print "Deleting old shelf buttons";
    
        $btnParent = $shelf;

        // delete existing buttons on the shelf.

        string $existingShelfButtons[] = `shelfLayout -q -ca $shelf`;
        for ( $btn in $existingShelfButtons )
		{ 	        
//            string $btnName = `shelfButton -q -l $btn`;

            deleteUI $btn;                
        }        
    } else {
		print "Create new shelf ";

        // Create the shelf under the global shelf parent.
        global string $gShelfTopLevel;
        $btnParent = `shelfLayout -p $gShelfTopLevel $shelf`;
    }

    // Make (or remake) the tool button.
    shelfButton -annotation $iconBtnLabel -l $iconBtnLabel -image1 $iconImage
                -command "createPdiBodyPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel4 -l $iconBtnLabel4 -image1 $iconImage4
                -command "createPdiPropertiesPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel2 -l $iconBtnLabel2 -image1 $iconImage2
                -command "createPdiSolverPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel3 -l $iconBtnLabel3 -image1 $iconImage3
                -command "createPdiManagePanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel8 -l $iconBtnLabel8 -image1 $iconImage8
                -command "createShatterItPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel6 -l $iconBtnLabel6 -image1 $iconImage6
                -command "createPdiBasicFracturesPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel7 -l $iconBtnLabel7 -image1 $iconImage7
                -command "createPdiAvFracturesPanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel5 -l $iconBtnLabel5 -image1 $iconImage5
                -command "createPdiBakePanel" -p $btnParent;
    shelfButton -annotation $iconBtnLabel10 -l $iconBtnLabel10 -image1 $iconImage10
                -command "pdiAddJagginessPanel" -p $btnParent;                
    shelfButton -annotation $iconBtnLabel9 -l $iconBtnLabel9 -image1 $iconImage9
                -command "showPdiLicenseInfo" -p $btnParent pdiLicenseicon;

    global string $pdiLicenseWindow;
    global string $PdiUserNamePrompt;
    string $PdiUserName = "free version";
    string $numLicenses = "NA";
    string $numLicensestext = "Num licenses:" + $numLicenses;
	$pdiLicenseWindow = `window -rtf true -t "License Info" -ret -width 200 -height 300 pdiLicenseWin`;
	columnLayout -adjustableColumn true -rs 20;               
        text -label "Pulldownit 2.0 licensed to: "   -align "center";
    $PdiUserNamePrompt = `text -label $PdiUserName  -align "center"`;
//        text -label $numLicensestext   -align "center";
        $butNewLicense = `button  -w 120 -label "New License" -command "pdiInputNewLicenseCmd"`;
        text -label " Created by Carlos Pegar "  -align "center" pdiAuthoredText;
        text -label " Copyright 2012/2013 Thinkinetic "  -align "center" pdiLicenseText;
}

global proc pdiActivateSingleLicense()
{
    confirmDialog
    -title "Pdi Info"
    -message "Please copy the REQUEST CODE displayed and send an email to marketing@pulldownit.com to get your license key"
    -button "OK" 
    -defaultButton "OK" ;
}

global proc pdiStoreSingleLicenseInfo()
{
    global string $pdiSingleusername;
    global string $pdiSinglelicensenumber;

    global string $strPdiusername;
    global string $strPdilicensenumber;

    $strPdiusername = `textField -query -text $pdiSingleusername`;

    $strPdilicensenumber = `textField -query -text $pdiSinglelicensenumber`;

    layoutDialog -dismiss "OK";
}

global proc pdiInputLicensePromt()
{
    global string $pdiSingleusername;
    global string $pdiSinglelicensenumber;

    // Get the dialog's formLayout.
    //
    string $form = `setParent -q`;

    // layoutDialog's are not resizable, so hard code a size here,
    // to make sure all UI elements are visible.
    //
    string $RequestCode = `pdiGetComputerInfo`;
    print ("REQUEST CODE: " + $RequestCode + "\n");

    formLayout -e -width 400 $form;

	string $txt1= `text -label "REQUEST CODE:"`;
	string $requestcode = `textField -w 300 -ed false -tx $RequestCode`;
	string $txt2= `text -label "Username: "`;
	$pdiSingleusername = `textField -w 200`;  
	string $txt3 = `text -label "License number"`;
	$pdiSinglelicensenumber = `textField -w 200`;
    string $logo = `picture -image "logoMax.xpm"`;
	string $b1 = `button -w 50 -l "OK"    -c "pdiStoreSingleLicenseInfo"`;
	string $b2 = `button -w 300 -l "Get a license key" -c "pdiActivateSingleLicense"`;


    int $spacer = 15;
    int $top = 5;
    int $edge = 5;

    formLayout -edit
        -attachForm            $txt1  "left"   $edge
        -attachControl         $requestcode  "left" 5 $txt1
        -attachControl         $txt2 "top"    $spacer  $txt1
	-attachForm           $txt2 "left"   $edge
        -attachControl         $pdiSingleusername  "left" 30 $txt2
        -attachControl         $pdiSingleusername  "top"  $spacer $txt1
        -attachControl         $txt3 "top"    $spacer  $txt2
	-attachForm           $txt3 "left"   $edge
        -attachControl         $pdiSinglelicensenumber  "left" 5 $txt3
        -attachControl         $pdiSinglelicensenumber  "top" $spacer $txt2
        -attachControl         $logo "top"    40  $txt3
        -attachControl         $b1 "top"    $spacer  $txt3
	-attachForm           $b1 "right"   $edge
        -attachControl         $b2 "top"    $spacer  $txt3
	-attachForm           $b2 "left"   $edge
   $form;
}
global proc pdiUseProxy()
{
	global string $framePdiUseProxy;
	global string $chkPdiUseProxy;

    int $bUseProxy = `checkBox -q -value $chkPdiUseProxy`;	

    if( $bUseProxy > 0)
    {
		frameLayout -edit -enable true $framePdiUseProxy;
	}
	else
	{
		frameLayout -edit -enable false $framePdiUseProxy;
	}
}
global proc pdiInputFloatingLicensePromt()
{
    global string $pdiusername;
    global string $pdilicensenumber;
	global string $framePdiUseProxy;
	global string $chkPdiUseProxy;
	global string $pdiProxyHostName;
	global string $pdiProxyPort;
	global string $pdiProxyUser;
	global string $pdiProxyPassword;

    // Get the dialog's formLayout.
    //
    string $form = `setParent -q`;

    // layoutDialog's are not resizable, so hard code a size here,
    // to make sure all UI elements are visible.
    //
    formLayout -e -width 400 $form;

	string $txt2= `text -label "Username:"`;
	$pdiusername = `textField -w 200`;
	string $txt3 = `text -label "License number:"`;
	$pdilicensenumber = `textField -w 200`;
	$chkPdiUseProxy = `checkBox -label "use Proxy" -enable 1 -changeCommand "pdiUseProxy"`;	     
    $framePdiUseProxy = `frameLayout -width 250 -height 120 -enable false -label "Proxy Server Setting" -borderStyle "etchedOut"`;	
		rowColumnLayout -numberOfColumns 2;
		string $txt4 = `text -label "IP/HOST:"`;
		$pdiProxyHostName = `textField -w 200`;  
		string $txt5 = `text -label "PORT:"`;
		$pdiProxyPort = `textField -w 200`;  
		string $txt6= `text -label "Userid:"`;
		$pdiProxyUser = `textField -w 200`;  
		string $txt7 = `text -label "Password:"`;
		$pdiProxyPassword = `textField -w 200`;
	    setParent..;             	
    setParent..;             	
    string $logo = `picture -image "logoMax.xpm"`;
	string $b1 = `button -w 50 -l "OK"    -c "pdiStoreLicenseInfo"`;

	frameLayout -edit -enable false $framePdiUseProxy;

    int $spacer = 15;
    int $top = 5;
    int $edge = 5;

    formLayout -edit
		-attachForm            $txt2 "left"   $edge
        -attachControl         $pdiusername  "left" 30 $txt2
        -attachControl         $txt3 "top"    $spacer  $txt2
		-attachForm            $txt3 "left"   $edge
        -attachControl         $pdilicensenumber  "left" 30 $txt3
        -attachControl         $pdilicensenumber  "top" $spacer $txt2
        -attachControl         $chkPdiUseProxy  "top" 20 $txt3
        -attachControl         $framePdiUseProxy  "top" $top $chkPdiUseProxy
        -attachControl         $logo "top"    40  $framePdiUseProxy
        -attachControl         $b1 "top"    $spacer $framePdiUseProxy
		-attachForm           $b1 "right"   $edge
   $form;
}


global proc string pdiUiInputLicense()
{
    string $result = `layoutDialog -t "Pdi License info" -ui "pdiInputLicensePromt"`;

    return $result;

}

global proc string pdiUiInputFloatingLicense()
{
    string $result = `layoutDialog -t "Pdi Floating License info" -ui "pdiInputFloatingLicensePromt"`;

    return $result;
}

global proc string pdiGetLicenseNumber()
{
    global string $strPdilicensenumber;

//    print $strPdilicensenumber;

    return $strPdilicensenumber;
}
global proc string pdiGetInputCompany()
{
    global string $strPdiusername;
    
    return $strPdiusername;
}
global proc string pdiGetProxyHost()
{
	global string $strPdiproxyHost;
    
    return $strPdiproxyHost;
}
global proc string pdiGetProxyPort()
{
	global string $strPdiproxyPort;
    
    return $strPdiproxyPort;
}
global proc string pdiGetProxyLog()
{
    global string $strPdiproxyLog;
    
    return $strPdiproxyLog;
}
global proc string pdiGetProxyKey()
{
    global string $strPdiproxyKey;

    return $strPdiproxyKey;
}

global proc int pdiFloatingLicenseUseProxy()
{
	global int $bPdiUseProxy;

	return $bPdiUseProxy;
}
global proc pdiStoreLicenseInfo()
{
    global string $pdiusername;
    global string $pdilicensenumber;
	global string $chkPdiUseProxy;
    global string $pdiProxyHostName;
	global string $pdiProxyPort;
	global string $pdiProxyUser;
	global string $pdiProxyPassword;

    global string $strPdiusername;
    global string $strPdilicensenumber;
    global string $strPdiproxyHost;
    global string $strPdiproxyPort;
    global string $strPdiproxyLog;
    global string $strPdiproxyKey;    
	global int $bPdiUseProxy;

    $strPdiusername = `textField -query -text $pdiusername`;
    $strPdilicensenumber = `textField -query -text $pdilicensenumber`;
    $bPdiUseProxy = `checkBox -q -value $chkPdiUseProxy`;	
    $strPdiproxyHost = `textField -query -text $pdiProxyHostName`;
    $strPdiproxyPort = `textField -query -text $pdiProxyPort`;
    $strPdiproxyLog = `textField -query -text $pdiProxyUser`;
    $strPdiproxyKey = `textField -query -text $pdiProxyPassword`;
    
    layoutDialog -dismiss "OK";
}


global proc pdiUI_initialize()
{
    pdiUI_createShelfButtons();
	            
//    pdiUiInputLicense();
}

global proc pdiSetGravityAxis()
{
     string $PDISolver = `pdiSolver`;
     string $axis=`upAxis -q -axis`; 
    
     if( $axis=="y")
     { 
//         setAttr($PDISolver + ".gravity") -type "float3" 0.0 -10.0 0.0;
         setAttr($PDISolver + ".gravityX") 0.0;
         setAttr($PDISolver + ".gravityY") -10.0;
         setAttr($PDISolver + ".gravityZ") 0.0;
//         print " Maya axys Y"; 
     }       
     else
     { 
//         setAttr($PDISolver + ".gravity") -type "float3" 0.0 0.0 -10.0;
         setAttr($PDISolver + ".gravityX") 0.0;
         setAttr($PDISolver + ".gravityY") 0.0;
         setAttr($PDISolver + ".gravityZ") -10.0;
//         print " Maya axys Z";
     }       
}

global proc pdi_Getvelocity()
{
	string  $PDIObjects[];
	$PDIObjects = `ls -selection -dag -leaf -type pdiRigidBody`;
    vector $velocity = getAttr ($PDIObjects[0] + ".velocity" );
    vector $spin = getAttr ($PDIObjects[0] + ".welocity" );
    
	print ("velocity = " + ($velocity.x) +"," + ($velocity.y) +"," + ($velocity.z) +"\n");
	print ("spin = " + ($spin.x) +"," + ($spin.y) +"," + ($spin.z) +"\n");
}





